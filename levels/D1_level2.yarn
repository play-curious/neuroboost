# gauges: learning

title: Start
tags:
colorID: 0
position: -893,370
---
<< empty >>
<< fadeIn 1 >>
<< setBackground bedroom >>
<< setTime 8:00>>
<< loopFX Chime_LOOP 2000 >>
<< showGauges >>
<< fadeOut >>

Bip ! Bip ! Il est 8 heures vous rappelle votre horloge connectée. 

<< stopFX Chime_LOOP >>
<< music Neutral >>

Comme tous les matins, vous vous réveillez dans votre chambre d'étudiant de l'Académie d'Omniscience et de Polytechnologie Universelle.

<< loopFX PhoneRing_LOOP 5000 >>
Ronronron... Cette fois, c'est votre palmphone qui vibre : FRED veut vous parler.
-> Prendre l'appel
    <<jump Appel_Fred_Rewind>>
-> Rejeter l'appel
    <<jump Rejet_Fred_Rewind>>
===
title: Appel_Fred_Rewind
tags: 
colorID: 0
position: -592,376
---
<< stopFX PhoneRing_LOOP >>

Fred@holo_excited: Hello ! c'est Fred !
You: Qu'est-ce que tu veux me dire cette fois-ci ?
Fred@holo_smiling: Yo, je vois que tu n'es pas de bonne humeur ce matin...
You: Normal, j'ai foiré le partiel d'Atronometric Data Management.
Fred@holo_smiling: Yes, la bonne blague, c'est cet après-midi qu'on le passe !
You: Quoi ?!
Fred@holo_smiling: Yes, on est lundi matin. C'est ce qui est prévu.
You: Lundi ?! Mais c'était hier !!
Fred@holo_happy: Yes yes, dans ta tête peut-être, coco. Mais dans la vraie vie, c'est aujourd'hui.
You: C'est dingue ! Je rêve ou quoi ?
Fred@holo_smiling: No no, je t'assure que c'est la réalité, coco.
You: Euh...
Fred@holo_excited: Bon, je vais me détendre à la salle de gym, on se retrouve à 14 heures pour l'exam ?
You: Euh, ok...

<< hide >>

Vous raccrochez sans vous en rendre compte

<< advanceTime 0:15 >>

<<jump Phone_off_Rewind>>
===
title: Rejet_Fred_Rewind
tags:
colorID: 0
position: -758,62
---
Décidément, vous ne voulez pas comprendre qui est Fred. Il ne s'avoue jamais vaincu. Il vous rappelle aussitôt !
-> Prendre l'appel
    <<jump Appel_Fred_Rewind>>
===
title: Phone_off_Rewind
tags:
colorID: 0
position: -342,363
---
<< setBackground bedroom >>
<< fadeOut >>
Mais c'est quoi ce truc ?! On dirait que vous avez fait un bon en arrière dans le temps ! Vous allez pouvoir repasser le partiel d'Atronometric Data Management. C'est la chance du siècle !
Vous poussez un puissant "BINGO" qui résonne contre les quatre murs de votre chambre !

<<showGauges>>
Il vous reste 6 heures avant l'heure H... Aujourd'hui, vous allez montrer à Fred qui est le Big Winner de l'Académie !

<<jump Action_List_Rewind>>
===
title: Action_List_Rewind
tags: freechoice
colorID: 0
position: -895,604
---
<< setBackground bedroom >>
<< fadeOut >>

<< if !visited("Action_List") >>
<< visit Action_List >>
Que faites-vous maintenant?
<< endif >>

<< if isTimeOver("12:00") >>
Il est déjà l'heure de déjeuner. C'est tout ce que vous pouvez faire avant d'aller en classe.
-> Manger (à midi)
    <<jump Lunch_Rewind>>
<< else>>
-> Cuisine@cuisine
    <<jump cuisine_subchoice>>
-> Bureau@bureau
    <<jump bureau_subchoice>>
-> Salon@salon
    <<jump salon_subchoice>>
<< endif>>
===
title: cuisine_subchoice
tags: subchoice
position: -319.1514720916748,-55.38755989074707
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>
-> Prendre le petit déjeuner
    <<jump Petit_dej_Rewind>>
-> back
    << fadeIn >>
    <<jump Action_List_Rewind>>
===
title: bureau_subchoice
tags: subchoice
position: -993.6492929458618,-75.37680292129517
---
<< fadeIn >>
<< setBackground bedroom desk >>
<< fadeOut >>
-> Passer du temps sur le chat avec le pote d'une autre Université
    <<jump Chat_Rewind>>
-> Réviser cette fichue Astronometric Data Management
    <<jump Revision_Rewind>>
-> Essayer le rappel libre
    <<jump Rappel_libre>>
-> back
    << fadeIn >>
    <<jump Action_List_Rewind>>
===
title: salon_subchoice
tags: subchoice
position: -1716.8310852050781,-38.99417304992676
---
<< fadeIn >>
<< setBackground bedroom chill >>
<< fadeOut >>
-> Binger votre série TV préférée
    <<jump BingeTV_Rewind>>
-> back
    <<jump Action_List_Rewind>>
===
title: Lunch_Rewind
tags:
colorID: 7
position: -1284,1084
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>
<< fx EatCook >>

Et ce midi pareil... Encore ce Burger Big Proteins Double Cheese et un verre Super Tall de Xeno Cola...
D'accord, ce repas est bien chargé en calories et en caféine, mais vous avez mangé le même hier. Ça commence à bien faire ces sensations de "déjà-vu"...
Espérons que vous n'allez pas reproduire les mêmes erreurs qu'hier. En route !

<< advanceTime 0:30 >>

-> Il est temps de battre votre précédent score!
    <<jump Test_Rewind>>
===
title: Petit_dej_Rewind
tags:
colorID: 0
position: -1040,831
---
<< fx EatCook >>

Encore un petit déjeuner chimicookies arrosé d'un synthécafé. C'est clair que ce matin dégage un étrange air de "déjà-vu"...
Bon, ce n'est pas tout.

<< advanceTime 0:30 >>

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: Chat_Rewind
tags:
colorID: 0
position: -755,827
---
<b>Pensez-vous vraiment que c'est ce que vous a recommandé les 4 Sages de la Métacognition?</b>

Peu importe, il est trop tard maintenant, vous cliquez sur l'icône ChatApp de votre Palmphone. Vous êtes surpris de voir le nombre d'étudiants connectés à cette heure si matinale. Probablement que c'est parce que nous sommes lundi. Vous êtes pingé par Alex, vous engagez la conversation.

<< fx Notification >>

Ensuite, c'est Dom, puis Lou, puis Cam...

<< if visited("Chat") >>
<bi>Étrange... Vous avez l'impression d'avoir déjà vécu ce moment de dilatation du temps...</bi>
<< endif >>

<< advanceTime 2:00 >>

Vous passez deux heures à discuter de tout, mais surtout de rien.

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: Revision_Rewind
tags:
colorID: 0
position: -508,824
---
Vous ouvrez une nouvelle fois votre dossier Astrometric Data Management. Qu'est-ce que c'est barbant... Mais cette fois, vous ravalez votre bile.
Aujourd'hui, vous voulez réussir votre journée. Vous voulez coiffer Fred au poteau! Vous aussi, vous aurez un <i>A+</i>

<< advanceTime 2:00 >>

<< addToGauge learning 45 >>

Vous vous infligez un supplice volontaire : passer les 2 heures suivantes à potasser ce long pensum ennuyeux ! Beuark !

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: BingeTV_Rewind
tags:
colorID: 0
position: -288,823
---

<bi>Pensez-vous vraiment que c'est ce que vous ont recommandé les 4 Sages de la Métacognition?</bi>
Vous lancez votre application Screencast, l'écran d'accueil de Webflix s'affiche aussitôt sur le mur, dévoilant votre liste de programmes préférés. Vous pointez avec le doigt sur l'icône de votre série du moment : Star Mars - The Lone Lightsaber.

<< show lightsaber@holo >>
<< loopFX TVStarwars_LOOP 10000 >>

Le générique démarre et déroule sur un fond scintillant d'étoiles un texte récapitulatif des épisodes précédents. L'infâme Jaba Fett s'est fait cryogénisé par l'impitoyable corsaire Yann Salto.
Yann Salto doit désormais ramener sa capture sur la planète prison Jailhouse. Encore faut-il échapper aux attaques des pirates de l'espace, qui sont nombreux à sillonner autour de Jailhouse...

<< if visited("BingeTV") >>
<bi>Étrange... Vous avez l'impression d'avoir déjà vécu ce moment de dilatation du temps...</bi>
<< endif >>

En tous les cas, vous vous en doutiez, Yann Salto a beau être l'un des meilleurs pilotes de l'univers, il se fait amocher son vaisseau ! Il est obligé d'atterrir en catastrophe sur Tortuga.  Le suspense est haletant, vous ne pouvez pas ne pas regarder l'épisode suivant. Et ainsi de suite.

<< advanceTime 4:00 13:00 1:00 >>
<< stopFX TVStarwars_LOOP >>
<< hide >>

Au final vous passez trop de temps à binger votre série préférée.

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: Rappel_libre
tags:
colorID: 0
position: -42,822
---
Il est temps d'appliquer la méthode miracle que vous ont enseignée ces esprits qui vous ont parlé durant votre sommeil.
Vous essayez de vous souvenir de ce que vous avez retenu en cours. Ce n'est pas brillant. Vous vous ne que souvenez de très peu de choses. Pas étonnant, vous n'aimez pas cette matière.
Mais lorsque vous replongez dans la lecture pour verifier vos réponses, vous constatez que ça vous aide vraiment à mettre de l'ordre dans votre esprit. Il y a encore du travail à faire, mais ça sort plus facilement de votre tête !

<< visit Rappel_libre >>
<< addToGauge learning 100 >>

Vous prenez encore le temps de relire votre cours pour combler les lacunes qui vous gênent encore. Résultat, vous vous sentez bien plus prêt à attaquer l'examen cette fois.

<< advanceTime 2:00 >>

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: Test_Rewind
tags:
colorID: 0
position: -1007,1085
---
<< fadeIn >>
<< setBackground class >>
<< setTime 14:00 >>
<< fadeOut >>

Vous voilà désormais dans la grande salle. Vous vous installez devant votre écran de travail. A peine vous cliquez sur le fichier <b>Sujet d'examen</b> que le compteur se met en route.

<< advanceTime 4:00 >>

Vous passez les 4 heures suivantes à répondre aux questions à choix multiples qui s'enchaînent les unes derrière les autres. Ce coup-ci, vous êtes sûr de vous. Bingo ! Vous allez y arriver !!

Euphorique, vous quittez la salle et retrouvez Fred, qui vous attend à l'entrée de la classe.

<<jump Post_Test_Rewind>>
===
title: Post_Test_Rewind
tags: 
colorID: 0
position: -742,1087
---
<< fadeIn >>
<< setBackground door class >>
<< fadeOut >>

Fred_excited: Alors, ça l'a fait ?
You: Ouais ! Trop facile !
Fred_happy: Yes, je suis content pour toi. Toi qui n'aimais pas cette matière.
You: Ouais, mais ce coup-ci, j'ai mis le paquet !
Fred_smiling: Ahhhh... Génial !
You: Je vais avoir <b>A</b> !
Fred_happy: No... Pas moins ?
You: Je te le dis !
Fred_smiling: C'est génial ! Je t'ai encore jamais vu comme ça.
You: Ouais !

<< advanceTime 0:15 >>

Fred s'apprête à vous répondre quand la sonnerie retentit.
<< fx Notification >>

<< hide >>
<< setBackground door class-screen >>
Les résultats du test viennent de tomber...

<< if visited("Rappel_libre") >>
-> Consulter les résultats
    <<jump Resultats_Good>>
<< else >>
-> Consulter les résultats
    <<jump Resultats_Bad_Again>>
<< endif >>
===
title: Resultats_Good
tags: 
colorID: 0
position: -529,1332
---
<< empty >>
<< music Victory >>
<< fx Success >>

Voilà... Les scores s'affichent sur l'écran de la porte principale... Vous avez remonté votre retard... Mais vous n'êtes pas encore dans le peleton de tête... Vous n'avez que <b>B</b>. Fred vous distance encore...

Fred_smiling: Hé, C'est vachement bien pour une matière que tu détestes.
You: Facile à dire pour toi. Tu as eu <b>A+</b>
Fred_happy: Et alors ? Tu feras mieux la prochaine fois.
<< hide >>

<< advanceTime 0:05 >>
===
title: Resultats_Bad_Again
tags:
colorID: 0
position: -476,1087
---
TaTaTaTa... Vous n'avez pas bien écouté les leçons des 4 Sages de la Métacognition.

Tentez à nouveau de passer ce premier examen...

<< setTime 8:15 >>

<< fadeIn >>
<< resetLevel >>
<<jump Phone_off_Rewind>>
===
