#gauges:learning

title: Start
tags:
colorID: 0
position: -245,126
---
<< empty >>
<< fadeIn 1 >>
<< showTitle Chapter 1 >>
<< setGauge learning 0 >>
<< setBackground bedroom >>
<< setTime 8:00>>
<< loopFX Chime_LOOP 2000 >>
<< fadeOut >>

Bip ! Bip ! Il est 8 heures, vous rappelle votre horloge connectée.  #line:0282b9b 

<< stopFX Chime_LOOP >>
<< music Neutral >>

<<if !visited("Chapitre1")>>
Quel rêve étrange ...
<<endif>>
Comme tous les matins, vous vous réveillez dans votre chambre d'étudiant de l'Académie d'Omniscience et de Polytechnologie Universelle. #line:00aae49 

<< loopFX PhoneRing_LOOP 5000 >>
Cette fois, c'est votre palmphone qui vibre : <b>Fred</b> veut vous parler. #line:0c10bcd 
-> Prendre l'appel #line:0c4a7b0 
    <<jump Appel_Fred_Rewind>>
===

title: Appel_Fred_Rewind
tags: 
colorID: 0
position: -570,57
---
<< stopFX PhoneRing_LOOP >>

<<if !visited("FredCall")>>
Fred@holo_excited: Salut ! c'est Fred ! #line:0fb4525 
You: Je sais, j'ai vu ton nom. #line:0d1a13b 
Fred@holo_smiling: Yo, je vois que t'es pas de bonne humeur ce matin... #line:039d35b 
You: Facile à dire pour toi, tu révises jamais et t'as tout le temps des bonnes notes !
Fred@holo_happy: Damned, tu t'es vraiment levé du mauvais pied aujourd'hui. Qu'est-ce qui ne va pas ?

-> C'est fini, je vais jamais passer mon année !
-> Tu le sais bien, tu as dû voir ma prestation d'hier...

You: À ton avis... J'ai foiré le Grand Oral ! C'est une note éliminatoire, tu sais ce que ça veut dire ! #line:09b04e6 
Fred@holo_smiling: Ah ouais, la bonne blague... Le Grand Oral, c'est dans une semaine ! #line:057105b 
You: Quoi ?! #line:080aa84 
Fred@holo_happy: Toi, t'as la tête dans le pâté. Aujourd'hui, on a le partiel d'Astronometric Data Management. T'as pas oublié, quand-même ?
You: Mais... C'était la semaine dernière ?? #line:0f2a881 
Fred@holo_happy: Ouais ouais, dans ta tête peut-être, coco. Mais dans la vraie vie, c'est aujourd'hui. #line:03aa008 
You: C'est dingue ! Je rêve ou quoi ? #line:0b5f597 
Fred@holo_smiling: Non non, je t'assure que c'est la réalité, tête de mule. #line:0c6db74 
You: Euh... #line:007c5ec 
Fred@holo_excited: Bon, je vais me détendre à la salle de gym, on se retrouve à 14 heures pour l'exam ? #line:03c1f6b 
You: Euh, ok... #line:03e0c76 
<<else>>
Fred@holo_excited: Hello ! c'est Fred ! #line:0fb4525 
You: Je sais, Fred, on a le partiel cet aprem ! On se retrouve à 14h devant la classe, ok ?
Fred@holo_smiling: Wow, t'es pressé, ce matin, à ce que je vois !
You: T'inquiète, on se verra à la fête de ce soir si tout va bien !
Fred@holo_excited: Cool, je savais même pas que t'étais au courant ! A plus tard, alors !
You: A plus !
<<endif>>


<< hide >>

Vous raccrochez sans vous en rendre compte. #line:077b761 

<< advanceTime 0:15 >>

<<jump Phone_off_Rewind>>
===

title: Phone_off_Rewind
tags:
colorID: 0
position: -316,223
---
<< setBackground bedroom >>
<< fadeOut >>

<<if !visited("FredCall")>>
Quoi ?! On dirait que vous avez fait un bond en arrière dans le temps ! Peut-être que ces <i>sages</i> y sont pour quelque chose... Vous allez pouvoir revivre cette semaine, sauver votre année. C'est la chance du siècle ! #line:0d751d6 
Vous poussez un puissant "BINGO" qui résonne contre les quatre murs de votre chambre ! #line:020bca4 
<<endif>>

<<showGauges>>
Il vous reste quelques heures avant le partiel... Aujourd'hui, vous allez montrer à Fred qui est le plus grand vainqueur de l'Académie ! #line:0cfadf9 

<<visitPermanent FredCall>>

<<jump Action_List_Rewind>>
===

title: Action_List_Rewind
tags: freechoice
colorID: 0
position: -1248,479
---
<< setBackground bedroom >>
<< fadeOut >>

<< if !visited("Action_List") >>
<< visit Action_List >>
Que faites-vous maintenant ? #line:03f3124 
<< endif >>

<< if isTimeOver("12:00") >>
Il est déjà l'heure de déjeuner. C'est tout ce que vous pouvez faire avant d'aller en classe. #line:0424ed0 
-> Manger (à midi) #line:0526b3b 
    <<jump Lunch_Rewind>>
<< else>>
-> Cuisine@cuisine #line:0b8d567 
    <<jump cuisine_subchoice>>
-> Bureau@bureau #line:0027a0c 
    <<jump bureau_subchoice>>
-> Salon@salon #line:021edd5 
    <<jump salon_subchoice>>
-> Porte@porte
    <<jump porte_subchoice>>
<< endif>>
=== 

title: porte_subchoice
tags: subchoice
position: -525,562
---
-> Aller à l'examen (passer jusqu'à 14h)
    <<jump Check_Results>>
-> back
    <<jump Action_List_Rewind>>
===

title: cuisine_subchoice
tags: subchoice
position: -525,562
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>

-> Prendre le petit déjeuner (30 min) <<if !visited("Petit_dej")>>#line:079d0ff 
    <<jump Petit_dej_Rewind>>
-> back #line:0fad580 
    << fadeIn >>
    <<jump Action_List_Rewind>>
===

title: bureau_subchoice
tags: subchoice
position: -993.6492929458618,-75.37680292129517
---
<< fadeIn >>
<< setBackground bedroom desk >>
<< fadeOut >>
-> Passer du temps sur le chat avec un pote (Au moins 1h...) <<if !visited("Chat")>> #line:026de4e 
    <<jump Chat_Rewind>>
-> Réviser cette fichue Astronometric Data Management (2h) <<if !visited("Revision")>> #line:070f359 
    <<jump Revision_Rewind>>
-> Essayer la technique du rappel libre (2h) <<if !visited("Rappel_libre")>> #line:0310da3 
    <<jump Rappel_libre>>
-> back #line:0a06fb6 
    << fadeIn >>
    <<jump Action_List_Rewind>>
===

title: salon_subchoice
tags: subchoice
position: -1427,217
---
<< fadeIn >>
<< setBackground bedroom chill >>
<< fadeOut >>
-> Regarder votre série TV préférée (Au moins 2h...) <<if !visited("BingeTV")>> #line:0f2e31c 
    <<jump BingeTV_Rewind>>
-> back #line:0341673 
    <<jump Action_List_Rewind>>
===

title: Lunch_Rewind
tags:
colorID: 7
position: -1405,986
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>
<< fx EatCook >>

<<if visited("LunchBefore")>>
Et ce midi pareil... Encore ce Burger Big Proteins Double Cheese et un verre Super Tall de Xeno Cola... #line:046846a 
Espérons que vous n'allez pas reproduire les mêmes erreurs qu'hier. En route ! #line:0f21e5a 
<<else>>
Miam... Délicieux, ce Burger Big Proteins Double Cheese !
Bien chargé en calories, mais vous allez en avoir besoin tout à l'heure pour l'examen ! Allez, une bonne gorgée de Xeno Cola pour accélérer la digestion... #line:00ba9c8 
<<endif>>

<<visitPermanent LunchBefore>>

<<jump Check_Results>>
===

title: Check_Results
tags:
colorID: 7
position: -1269,1157
---
<< if getGauge("learning") >= 50 >>
-> Il est temps de passer cet examen ! #line:02b7a89 
    <<jump Test_Rewind>>
<< else >>
-> Il est temps de passer cet examen ! #line:02b7a89 
    <<if visited("Assemblee_Sages")>>
        <<jump Resultats_Bad_Again>>
    <<else>>
        <<jump Test>>
    <<endif>>
<< endif >>
===

title: Petit_dej_Rewind
tags:
colorID: 0
position: -1040,831
---
<< fx EatCook >>

Encore un petit déjeuner chimicookies arrosé d'un synthécafé. C'est clair que ce matin dégage un étrange air de "déjà-vu"... #line:0ac0772 
Bon, ce n'est pas tout. #line:0d01fa8 

<<visit Petit_dej>>

<< advanceTime 0:30 >>

<< fadeIn >>
<<jump Action_List_Rewind>>
===

title: Chat_Rewind
tags:
colorID: 0
position: -907,703
---

Vous cliquez sur l'icône ChatApp de votre Palmphone. Vous êtes surpris de voir le nombre d'étudiants connectés à cette heure si matinale. C'est lundi, après tout... Vous êtes pingé par Alex, vous engagez la conversation. #line:0ecf0bf 

<< fx Notification >>

Ensuite, c'est Dom, puis Lou, puis Cam... #line:008f37e 

<< if visited("Chat_Before") >>
<bi>Étrange... Vous avez l'impression d'avoir déjà vécu ce moment de dilatation du temps...</bi> #line:0cd34ae 
<< endif >>

<< advanceTime 2:00 >>

Vous passez 2 heures à discuter de tout, mais surtout de rien. #line:06ab06b 

<<visit Chat>>
<<visitPermanent ChatBefore>>

<< fadeIn >>
<<jump Action_List_Rewind>>
===

title: Revision_Rewind
tags:
colorID: 0
position: -591,772
---
Vous ouvrez votre dossier Astrometric Data Management. Qu'est-ce que c'est barbant... Mais cette fois, vous ravalez votre bile. #line:0ca527e 
Aujourd'hui, vous voulez réussir votre journée. Vous aussi, vous aurez un <i>A+</i>, comme Fred ! #line:04765d7 

<<visit Revision>>

<< advanceTime 2:00 >>

<< addToGauge learning 30 >>

Vous vous infligez un supplice volontaire : passer les 2 heures suivantes à potasser ce long pensum ennuyeux ! Beuark ! #line:0cd60f9 

<< fadeIn >>
<<jump Action_List_Rewind>>
===

title: BingeTV_Rewind
tags:
colorID: 0
position: -744,455
---

Vous lancez votre application Screencast, l'écran d'accueil de Webflix s'affiche aussitôt sur le mur, dévoilant votre liste de programmes préférés. Vous pointez avec le doigt sur l'icône de votre série du moment : Star Mars - The Lone Lightsaber. #line:0ff050f 

<< show lightsaber@holo >>
<< loopFX TVStarwars_LOOP 10000 >>

Le générique démarre et déroule sur un fond scintillant d'étoiles un texte récapitulatif des épisodes précédents. L'infâme Jaba Fett s'est fait cryogénisé par l'impitoyable corsaire Yann Salto. #line:0dd0537 
Yann Salto doit désormais ramener sa capture sur la planète prison Jailhouse. Encore faut-il échapper aux attaques des pirates de l'espace, qui sont nombreux à sillonner autour de Jailhouse... #line:0335c72 

<< if visited("BingeTV_Before") >>
<bi>Étrange... Vous avez l'impression d'avoir déjà vécu ce moment de dilatation du temps...</bi> #line:0a04f28 
<< endif >>

En tous les cas, vous vous en doutiez, Yann Salto a beau être l'un des meilleurs pilotes de l'univers, il se fait amocher son vaisseau ! Il est obligé d'atterrir en catastrophe sur Tortuga. Le suspense est haletant... 

<< advanceTime 4:00 13:00 1:00 >>
<< stopFX TVStarwars_LOOP >>
<< hide >>

Au final, vous avez regardé plus d'épisodes que prévu...

<<visit BingeTV>>
<<visitPermanent BingeTV_Before>>

<< fadeIn >>
<<jump Action_List_Rewind>>
===

title: Rappel_libre
tags:
colorID: 0
position: -485,416
---
Vous tentez d'appliquer la méthode du rappel libre. #line:0b8f655 
Vous essayez de vous souvenir de ce que vous avez retenu en cours, sans aide. Ce n'est pas brillant. Très peu de choses vous reviennent. Pas étonnant, vous n'aimez pas cette matière. #line:07f35ff 
Mais lorsque vous replongez dans la lecture pour verifier vos réponses, vous constatez que ça vous aide vraiment à mettre de l'ordre dans votre esprit. Il y a encore du travail à faire, mais ça sort plus facilement de votre tête ! #line:080fdf4 

<< visit Rappel_libre >>
<< addToGauge learning 60 >>

Vous prenez encore le temps de relire votre cours pour combler les lacunes qui vous gênent encore. Résultat, vous vous sentez bien plus prêt à attaquer l'examen. #line:0988ada 

<< advanceTime 2:00 >>

<< fadeIn >>
<<jump Action_List_Rewind>>
===

title: Test_Rewind
tags:
colorID: 0
position: -1013,1248
---
<< fadeIn >>
<< setBackground class >>
<< setTime 14:00 >>
<< fadeOut >>
Vous voilà désormais dans la grande salle. Vous vous installez devant votre écran de travail. À peine vous cliquez sur le fichier <b>Sujet d'examen</b> que le compteur se met en route. #line:0352286 

<< advanceTime 4:00 >>

Vous passez les 4 heures suivantes à répondre aux questions à choix multiples qui s'enchaînent les unes derrière les autres. Ce coup-ci, vous êtes sûr de vous. Vous allez y arriver !! #line:006d985 

Euphorique, vous quittez la salle et retrouvez Fred, qui vous attend à l'entrée de la classe. #line:0b43c25 

<<jump Post_Test_Rewind>>
===

title: Test
tags:
position: -666.0570068359375,500.580322265625
---
<< fadeIn >>
<< setBackground class >>
<< setTime 14:00 >>
<< fadeOut >>
Vous voilà désormais dans la grande classe. Vous vous installez devant votre écran de travail. A peine vous cliquez sur le fichier <b>Sujet d'examen</b>, que le compteur se met en route. #line:06b2e70 
Vous passez les 4 heures suivantes à répondre aux questions à choix multiples qui s'enchaînent les unes derrière les autres. Les questions semblent bêtes et évidentes, vos réponses aussi. #line:0598ff5 

<< advanceTime 4:00 >>

Après avoir validé votre rendu digital, vous quittez la salle et retrouvez Fred, qui vous attend à l'entrée de la classe. #line:0be01e7 

<<jump Post_Test>>
===

title: Post_Test_Rewind
tags: 
colorID: 0
position: -830,1233
---
<< fadeIn >>
<< setBackground door class >>
<< fadeOut >>

Fred_excited: Alors, ça l'a fait ? #line:0cbca32 
You: Ouais ! Trop facile ! #line:0c9acae 
Fred_happy: Yes, je suis content pour toi. Toi qui n'aimais pas cette matière. #line:084e7f4 
You: Ouais, mais ce coup-ci, j'ai mis le paquet ! #line:0266bbe 
Fred_smiling: Ahhhh... Génial ! #line:014e2cf 
You: Je vais avoir <b>A</b> ! #line:00808f8 
Fred_happy: No... Pas moins ? #line:0f7880c 
You: Je te le dis ! #line:08f2afc 
Fred_smiling: C'est génial ! Je t'ai encore jamais vu comme ça. #line:072af68 
You: Ouais ! #line:01fabda 

<< advanceTime 0:15 >>

Fred s'apprête à vous répondre quand la sonnerie retentit. #line:02b4a63 
<< fx Notification >>

<< hide >>
<< setBackground door class-screen >>
Les résultats du test viennent de tomber... #line:0509cfb 

<<jump Resultats_Good>>
===

title: Resultats_Good
tags: 
colorID: 0
position: -801,1373
---
<< empty >>
<< music Victory >>
<< fx Success >>

Voilà... Les scores s'affichent sur l'écran de la porte principale... Vous avez remonté votre retard ! #line:0395b52 

<< completeLevel Astuce des sages : Pour augmenter votre score, pensez à vous reposer et à bien manger. >>

Mais vous n'êtes pas encore dans le peleton de tête... Vous n'avez que <b>B</b>. Fred vous distance encore... #line:0395b52 

Fred_smiling: Hé, C'est vachement bien pour une matière que tu détestes. #line:085368d 
You: Facile à dire pour toi. Tu as eu <b>A+</b>. #line:04e24fc 
Fred_happy: Et alors ? Tu feras mieux la prochaine fois.
You: C'est sûr !
Fred_smiling: Avec des scores pareil, y a plus qu'une chose à faire, célébrer ce jour victorieux ! Ça tombe bien, les docteurs font une fête ce soir. Joignons-nous à eux ! #line:0b027ac 
You: Ce serait sympa, mais... 
<< hide >>

<< advanceTime 0:05 >>

<<visit Resultats_Good>>

<<if !visited("Assemblee_Sages")>>
    <<jump Sages_Victoire>>
<<else>>
-> Réécrire dans le Journal mental
    << showJournal method >>
    << completeJournal >>
    << jump AfterJournal >>
-> Passer au chapitre suivant
<<endif>>
===
title: AfterJournal
tags:
colorID: 0
position: -476,1087
---
-> Passer au chapitre suivant
===
title: Resultats_Bad_Again
tags:
colorID: 0
position: -476,1087
---
<< fadeIn >>
<< setBackground class >>
<< setTime 14:00 >>
<< fadeOut >>

Hélas, vous avez encore une fois échoué votre examen ! L'onirique Miss Temdé apparaît soudainement alors que vous validez votre rendu décevant.

Temde_sad: Je vois que tu n'as pas écouté nos conseils à la lettre ! Tu veux les écouter à nouveau ?

-> Ecouter à nouveau les conseils des Sages
    <<jump Assemblee_Sages>>
-> Recommencer la journée
    << hide >>
    << resetLevel >>
    << jump Start>>

<< setTime 8:15 >>

<< fadeIn >>
<< resetLevel >>
<<jump Phone_off_Rewind>>
===

title: Post_Test
tags:
position: -270,525
---
<< fadeIn >>
<< setBackground door class >>
<< fadeOut >>

Fred_excited: Alors, ça l'a fait ? #line:0876127 
You: Je pense, oui. C'était basique. #line:01647f2 
Fred_smiling: Yes, je m'attendais à plus tordu que ça. #line:0804335 
You: Moi aussi. #line:042aa91 
Fred_smiling: Y avait quelques petits pièges, mais ils sont tellement gros, on ne pouvait pas les louper ! #line:0ce0a8b 
You: Ah bon ? #line:086ed91 
Fred_surprised: Ouais, t'en as pas trouvé ? #line:0d5781e 
You: Non. #line:0818e11 
Fred_smiling: C'est dommage. Tu sais, une mauvaise réponse t'enlève autant de points qu'une bonne réponse. #line:0bf1cd4 
You: Heuuuu... #line:0facc92 

<< advanceTime 0:15 >>

<< fx Notification >>
<< setBackground door class-screen>>
<< hide >>
Les résultats du test viennent de tomber... #line:0fd7dde 

-> Consulter les résultats #line:01e5c57 
    <<jump Resultats_Bad>>
===

title: Resultats_Bad
tags:
position: -88,657
---

<< music NegativeSad >>

Hélas... Fred n'avait pas tort... Les scores s'affichent sur l'écran de la porte principale. Vous êtes dans le peloton... de queue... La semaine de sprint commence mal... #line:0f1a807 

Fred_excited: Hé, ça va ? Tu as l'air tout blême d'un coup. #line:06878c2 
You: Ben... J'ai tout juste <b>D-</b>. #line:0d592fa 
Fred_happy: Hé alors ? Ce n'est pas la fin du monde. Ce n'est qu'une note parmi d'autres. Tu vas te rattraper. #line:0f8a7da 
You: Facile à dire pour toi. Tu as eu <b>A+</b> ! #line:0166adc 
Fred_smiling: Y a qu'une chose à faire dans ce cas ! #line:01bdf3a 
You: Quoi donc ? #line:0fc9d11 
Fred_happy: Se vider la tête ! Ça tombe bien, les docteurs font une fête ce soir. Joignons-nous à eux ! #line:0a6af63 


<< advanceTime 0:15 >>

-> Peut-être... #line:0bc651a 
-> Même pas en rêve #line:0eeda10 

You: J'ai pas trop la tête à ça... Je te dirai si je change d'avis, mais pour l'instant, je suis épuisé. Je rentre. #line:012855e 
<< hide >>
<<jump Sages_Defaite>>
===

title: Sages_Defaite
tags: noUi
position: -388,1264
---

<< fadeIn >>
<< setBackground bedroom night >>
<< fadeOut >>

Vous vous retrouvez une fois de plus seul, après une nouvelle défaite scolaire.

Sérieusement ! Même en l'ayant passé la semaine dernière, vous avez quand-même raté l'examen ! Frustré, vous vous allongez de tout votre mou sur le lit, vaincu, quand une silhouette familière se glisse dans votre champ de vision. 

<< music Mysterious >>

Temde_neutral: Allons, ne sois pas défaitiste, { $name }, l'apprentissage de la métacognition n'est pas évident au début, mais nous allons tout faire pour t'aider. Viens, suis-moi une fois de plus.
You: Où ça ? On va encore remonter le temps ?
Temde_cheerful: Patience, { $name }. Pour l'instant, cap vers l'Assemblée des Sages. Nous avons quelques astuces à t'apprendre.

<<jump Assemblee_Sages>>
===

title: Sages_Victoire
tags: noUi
position: -760,1476
---

Temde_neutral: { $name } ! Je vois que tu es déjà parvenu à t'améliorer par toi-même. C'est une capacité que je respecte.
You: Euh... J'étais avec mon pote là...
Temde_smiling: Pas d'inquiétude, nous te renverrons à cet instant précis dès que nous aurons terminé. Pour l'instant, viens, suis-moi une fois de plus.
You: Où ça ? On va encore remonter le temps ?
Temde_cheerful: Pas cette fois, { $name }. Pour l'instant, cap vers l'Assemblée des Sages. Même si tu es débrouillard, nous avons quelques astuces à t'apprendre.

<< fx Warp >>
<< fadeIn 1500 12ea12 >>
<< hide ! >>

<<jump Assemblee_Sages>>
===

title: Assemblee_Sages
tags: noUi
position: -571,1504
---

<< fadeIn >>
<< hide ! >>
<< setBackground circle >>
<< fadeOut >>

Ledai_explaining: Venons en donc au sujet de ce soir, <b>Master Sapiens</b>. #line:01eca65 
Sapiens_thinking: Oui, la mémorisation.

<< visitPermanent Assemblee_Sages >>

-> Qu'est-ce qu'elle a, ma mémorisation? #line:0d59581 
    <<jump memorisation>>
===

title: memorisation
tags: noUi
position: -366.2418212890625,1422.0413818359375
---
Ledai_explaining: La mémorisation est un art complexe et puissant qui ne s'apprend pas en une seule nuit. #line:0ea6147 
Temde_thinking: Pourtant, il le faudra bien. On n'a qu'une nuit à accorder à { $name }. #line:0d49277 
Sapiens_angry: Oui bien sûr. Mais dans ce cas, <b>Miss Temdé</b>, évite de m'interrompre sans arrêt... #line:0a57318 
Azul_cheerful: Vous savez quoi ? On va demander à { $name } ce qui l'intéresse, au lieu de se chamailler sans arrêt !
Sapiens_thinking: Mmh... Bien. Pour commencer, nous pouvons te parler des types de mémoires : la mémoire courte et la mémoire longue... #line:085e256 
Ledai_happy: Nous pouvons aussi t'expliquer comment passer de l'une à l'autre pour consolider tes connaissances !
<<jump Sages_FreeChoice>>
===

title: Sages_FreeChoice
tags: noUi
position: -366.2418212890625,1422.0413818359375
---
Azul_happy: Alors, que veux-tu approfondir maintenant ?

-> Mémoire courte et mémoire longue
    <<jump Types_Memoire>>
-> Comment consolider ses connaissances
    <<jump Routinisation>>
-> C'est bon pour moi ! (Quitter les sages)
    <<jump Quit_Sages>>
===

title: Types_Memoire
tags: noUi
position: -366.2418212890625,1422.0413818359375
---
<< fadeIn 200 >>
<< setBackground schema >>
<< fadeOut 200 >>

Sapiens_thinking: En bref, la cartographie de la mémoire dans le cerveau se découpe en deux zones. Comme tes deux hémisphères. #line:0853e70 

<< setBackground schema shelf-disorganized >>

Sapiens_thinking: Il y a tout d'abord <b>La mémoire courte</b>. Elle est utilisée quand tu travailles sur une tâche nouvelle, un nouveau savoir. Elle enregistre ce que tu regardes, ce que tu lis, ce que tu perçois, ce que tu interprètes, ce que tu comprends ou pas... Et elle stocke ça dans un énorme container. #line:0bc94b1 
Sapiens_thinking: Le souci, c'est qu'elle amasse toutes ces nouvelles informations sans ordre précis. Elle les empile en vrac, les unes sur les autres, à gauche à droite. En bref, elle range ça n'importe comment, comme un amateur, un dilettante ! #line:08ab121 

Ledai_explaining: Oui. Ce qui fait que quand tu es à la recherche d'une référence particulière, tu ne sais plus où elle se trouve, ni si tous les documents sont rangés au même endroit. #line:0991ef5 
Sapiens_thinking: Tout à fait, c'est le chaos ! La panique ! La catastrophe !.. Alors que <b>la mémoire longue</b>... #line:03bbbec 

-> C'est quoi la mémoire longue? #line:0617b83 
    <<jump Memoire_Longue>>
===

title: Memoire_Longue
tags: noUi
position: -67,1414
---
<< setBackground schema shelf-organized >>

Sapiens_thinking: <b>La mémoire longue</b> c'est le caviar de la connaissance. Là au contraire, tout y est ordonné, rangé, archivé, classifié. Comme dans une authentique bibliothèque. Dès qu'on a besoin de trouver quelque chose, on sait où le trouver. #line:0b78acd 

Ledai_explaining: Sans compter que chaque élément de <b>la mémoire longue</b> contient un index, une table des matières. Le cerveau n'a plus qu'à suivre le plan et ça roule sans sursaut. #line:0970ca1 
Sapiens_thinking: En bref, le secret pour bien apprendre réside dans ta capacité à consolider tes nouvelles connaissances, les stocker de manière logique et efficace. #line:041c990 

<< fadeIn 200 >>
<< setBackground circle >>
<< fadeOut 200 >>

Azul_happy: Tu as saisi ? La clé est de faire glisser tes nouveaux savoirs de la mémoire courte à la mémoire longue. #line:0ba2da0 

<<jump Sages_FreeChoice>>
===

title: Routinisation
tags: noUi
position: 243.36883544921875,1410.058349609375
---
Sapiens_thinking: Pour consolider ses connaissances, c'est simple. Pas de miracle sans effort. Pas de médaille sans sueur. Il faut travailler !
Ledai_explaining: Il faut surtout pratiquer ! Répéter et répéter et encore répéter ! À force de faire, on apprend à refaire. De plus en plus facilement, de plus en plus rapidement. #line:00317ce 
Azul_happy: Nos deux savants ont raison. Ta pratique finit par devenir une routine. Imagine que tu es cuisinier. À force de réaliser la même recette, tu finis pas la maîtriser. #line:050ca2d 
Ledai_explaining: Pour une fois que je suis d'accord avec <b>Azul</b>. Ainsi, tu mémorises la liste des ingrédients, les dosages, comme les étapes de préparation et les temps de cuisson. #line:0e3a8de 
You: Et comment je fais pour que ma pratique devienne une routine? #line:0439386 
Ledai_explaining: Tout simplement en répétant, en appliquant. #line:0832a45 

<< if visited("Rappel_libre") >>
You: C'est la méthode du rappel libre, pas vrai ?
Sapiens_happy: Exact ! Tu l'as déjà bien assimilé, on dirait !
<< else >>
Sapiens_thinking: C'est ce que l'on appelle, dans la vocabulaire de la métacognition : <b>les rappels libres</b>. #line:0318be0 
<< endif >>

Ledai_explaining: Le rappel libre, c'est tenter de restituer, par soi-même, et sans ordre précis, les notions que l'on a retenues. Cette méthode est moins connue, mais bien plus efficace qu'une relecture classique.
Sapiens_happy: La raison est très simple : en récupérant par toi-même les informations dans ta mémoire vive, tu indiques à ton cerveau qu'il doit consolider l'information, la faire passer dans la mémoire longue.
Azul_happy: Et c'est comme ça que tu réussis à conserver l'information le plus longtemps possible !
<<jump Sages_FreeChoice>>
===

title: Quit_Sages
tags: noUi
position: 243.36883544921875,1410.058349609375
---
You: C'est bon pour moi... Mais en attendant, va falloir que je me souvienne de tout ça à mon réveil. Comment faire ? #line:0d2186b 
Ledai_happy: En notant toutes les idées importantes dans ton <bi>Journal Mental</bi> ! #line:0838542 

<<if !visited("Resultats_Good")>>
Temde_smiling: Lorsque tu auras eu le temps de noter dans ton journal, nous te renverrons au début de cette journée. Bonne chance !
<<endif>>

<< completeSages >>
<<jump Journal_Rappel_libre>>
===

title: Journal_Rappel_libre
tags: noUi
position: 544,1399
---
<< fx Warp >>
<< fadeIn 100 ffffff >>
<< hide ! >>
<< setBackground bedroom night >>
<< fadeOut 400 >>

Le paradis verdoyant et rayonnant des 4 sages n'est plus là ! Vous voilà seul... Dans une bulle de noirceur, éclairée par un douce lumière tamisée.
Vous tenez entre vos mains un magnifique "carnet d'ingénieur tout-terrain". Garantie: waterproof, earthquakeproof, flameproof, acidproof... #line:0c1bf30 
Serait-ce le <bi>journal mental</bi> dont parlait Ledai ? #line:063d2d3 

<<visitPermanent Chapitre1>>

<<showJournal method>>
<< completeJournal >>

<<if !visited("Resultats_Good")>>
-> Recommencer la journée #line:0380a89 
    << resetLevel >>
    << jump Start>>
<<else>>
-> Revenir vers Fred
<< fx Warp >>
<< fadeIn 1500 12ea12 >>
<<endif>>
===
