#gauges:learning

title: Start
tags:
colorID: 0
position: -568,-53
---
<< empty >>
<< fadeIn 1 >>
<< setBackground bedroom >>
<< setTime 8:00>>
<< loopFX Chime_LOOP 2000 >>
<< showGauges >>
<< fadeOut >>

Bip ! Bip ! Il est 8 heures, vous rappelle votre horloge connectée.  #line:0282b9b 

<< stopFX Chime_LOOP >>
<< music Neutral >>

Quel rêve étrange ...
Comme tous les matins, vous vous réveillez dans votre chambre d'étudiant de l'Académie d'Omniscience et de Polytechnologie Universelle. #line:00aae49 

<< loopFX PhoneRing_LOOP 5000 >>
Ronronron... Cette fois, c'est votre palmphone qui vibre : FRED veut vous parler. #line:0c10bcd 
-> Prendre l'appel #line:0c4a7b0 
    <<jump Appel_Fred_Rewind>>
-> Rejeter l'appel #line:062f62e 
    <<jump Rejet_Fred_Rewind>>
===
title: Appel_Fred_Rewind
tags: 
colorID: 0
position: -570,57
---
<< stopFX PhoneRing_LOOP >>

Fred@holo_excited: Hello ! c'est Fred ! #line:0fb4525 
You: Je sais, j'ai vu ton nom. #line:0d1a13b 
Fred@holo_smiling: Yo, je vois que tu n'es pas de bonne humeur ce matin... #line:039d35b 
You: Facile à dire pour toi, tu révises jamais et tu as tout le temps des bonnes notes !
Fred@holo_happy: Damned, tu t'es vraiment levé du mauvais pied aujourd'hui. Qu'est-ce qui ne va pas ?
You: À ton avis ... J'ai foiré le Grand Oral ! C'est une note éliminatoire, tu sais ce que ça veut dire ! #line:09b04e6 
Fred@holo_smiling: Yes, la bonne blague ... Le Grand Oral, c'est dans une semaine ! #line:057105b 
You: Quoi ?! #line:080aa84 
Fred@holo_happy: Toi, tu as la tête dans le pâté. Aujourd'hui, on a le partiel d'Astronometric Data Management. T'as pas oublié, quand-même ?
You: Mais ... C'était la semaine dernière ?? #line:0f2a881 
Fred@holo_happy: Yes yes, dans ta tête peut-être, coco. Mais dans la vraie vie, c'est aujourd'hui. #line:03aa008 
You: C'est dingue ! Je rêve ou quoi ? #line:0b5f597 
Fred@holo_smiling: No no, je t'assure que c'est la réalité, coco. #line:0c6db74 
You: Euh... #line:007c5ec 
Fred@holo_excited: Bon, je vais me détendre à la salle de gym, on se retrouve à 14 heures pour l'exam ? #line:03c1f6b 
You: Euh, ok... #line:03e0c76 

<< hide >>

Vous raccrochez sans vous en rendre compte. #line:077b761 

<< advanceTime 0:15 >>

<<jump Phone_off_Rewind>>
===
title: Rejet_Fred_Rewind
tags:
colorID: 0
position: -326,-51
---
Décidément, vous ne voulez pas comprendre qui est Fred. Il ne s'avoue jamais vaincu. Il vous rappelle aussitôt ! #line:064cdc3 
-> Prendre l'appel #line:04b2664 
    <<jump Appel_Fred_Rewind>>
===
title: Phone_off_Rewind
tags:
colorID: 0
position: -316,223
---
<< setBackground bedroom >>
<< fadeOut >>
Mais c'est quoi ce truc ?! On dirait que vous avez fait un bon en arrière dans le temps ! Peut-être que ces <i>sages</i> y sont pour quelque chose... Vous allez pouvoir revivre cette semaine, sauver votre année. C'est la chance du siècle ! #line:0d751d6 
Vous poussez un puissant "BINGO" qui résonne contre les quatre murs de votre chambre ! #line:020bca4 

<<showGauges>>
Il vous reste 6 heures avant le partiel... Aujourd'hui, vous allez montrer à Fred qui est le Big Winner de l'Académie ! #line:0cfadf9 

<<jump Action_List_Rewind>>
===
title: Action_List_Rewind
tags: freechoice
colorID: 0
position: -1122,466
---
<< setBackground bedroom >>
<< fadeOut >>

<< if !visited("Action_List") >>
<< visit Action_List >>
Que faites-vous maintenant ? #line:03f3124 
<< endif >>

<< if isTimeOver("12:00") >>
Il est déjà l'heure de déjeuner. C'est tout ce que vous pouvez faire avant d'aller en classe. #line:0424ed0 
-> Manger (à midi) #line:0526b3b 
    <<jump Lunch_Rewind>>
<< else>>
-> Cuisine@cuisine #line:0b8d567 
    <<jump cuisine_subchoice>>
-> Bureau@bureau #line:0027a0c 
    <<jump bureau_subchoice>>
-> Salon@salon #line:021edd5 
    <<jump salon_subchoice>>
<< endif>>
===
title: cuisine_subchoice
tags: subchoice
position: -543,533
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>
-> Prendre le petit déjeuner #line:079d0ff 
    <<jump Petit_dej_Rewind>>
-> back #line:0fad580 
    << fadeIn >>
    <<jump Action_List_Rewind>>
===
title: bureau_subchoice
tags: subchoice
position: -993.6492929458618,-75.37680292129517
---
<< fadeIn >>
<< setBackground bedroom desk >>
<< fadeOut >>
-> Passer du temps sur le chat avec le pote d'une autre Université #line:026de4e 
    <<jump Chat_Rewind>>
-> Réviser cette fichue Astronometric Data Management #line:070f359 
    <<jump Revision_Rewind>>
-> Essayer la technique du rappel libre #line:0310da3 
    <<jump Rappel_libre>>
-> back #line:0a06fb6 
    << fadeIn >>
    <<jump Action_List_Rewind>>
===
title: salon_subchoice
tags: subchoice
position: -1427,217
---
<< fadeIn >>
<< setBackground bedroom chill >>
<< fadeOut >>
-> Binger votre série TV préférée #line:0f2e31c 
    <<jump BingeTV_Rewind>>
-> back #line:0341673 
    <<jump Action_List_Rewind>>
===
title: Lunch_Rewind
tags:
colorID: 7
position: -1284,1084
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>
<< fx EatCook >>

Et ce midi pareil... Encore ce Burger Big Proteins Double Cheese et un verre Super Tall de Xeno Cola... #line:046846a 
D'accord, ce repas est bien chargé en calories et en caféine, mais vous avez mangé le même hier. Ça commence à bien faire ces sensations de "déjà-vu"... #line:0e88c64 
Espérons que vous n'allez pas reproduire les mêmes erreurs qu'hier. En route ! #line:0f21e5a 

<< advanceTime 0:30 >>

-> Il est temps de battre votre précédent score! #line:02b7a89 
    <<jump Test_Rewind>>
===
title: Petit_dej_Rewind
tags:
colorID: 0
position: -1040,831
---
<< fx EatCook >>

Encore un petit déjeuner chimicookies arrosé d'un synthécafé. C'est clair que ce matin dégage un étrange air de "déjà-vu"... #line:0ac0772 
Bon, ce n'est pas tout. #line:0d01fa8 

<< advanceTime 0:30 >>

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: Chat_Rewind
tags:
colorID: 0
position: -907,703
---
<b>Pensez-vous vraiment que c'est ce que vous ont recommandé les 4 Sages de la Métacognition ?</b> #line:0349739 

Peu importe, il est trop tard maintenant, vous cliquez sur l'icône ChatApp de votre Palmphone. Vous êtes surpris de voir le nombre d'étudiants connectés à cette heure si matinale. Probablement que c'est parce que nous sommes lundi. Vous êtes pingé par Alex, vous engagez la conversation. #line:0ecf0bf 

<< fx Notification >>

Ensuite, c'est Dom, puis Lou, puis Cam... #line:008f37e 

<< if visited("Chat") >>
<bi>Étrange... Vous avez l'impression d'avoir déjà vécu ce moment de dilatation du temps...</bi> #line:0cd34ae 
<< endif >>

<< advanceTime 2:00 >>

Vous passez deux heures à discuter de tout, mais surtout de rien. #line:06ab06b 

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: Revision_Rewind
tags:
colorID: 0
position: -591,772
---
Vous ouvrez une nouvelle fois votre dossier Astrometric Data Management. Qu'est-ce que c'est barbant... Mais cette fois, vous ravalez votre bile. #line:0ca527e 
Aujourd'hui, vous voulez réussir votre journée. Vous voulez coiffer Fred au poteau ! Vous aussi, vous aurez un <i>A+</i> #line:04765d7 

<< advanceTime 2:00 >>

<< addToGauge learning 30 >>

Vous vous infligez un supplice volontaire : passer les 2 heures suivantes à potasser ce long pensum ennuyeux ! Beuark ! #line:0cd60f9 

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: BingeTV_Rewind
tags:
colorID: 0
position: -741,452
---

<bi>Pensez-vous vraiment que c'est ce que vous ont recommandé les 4 Sages de la Métacognition ?</bi> #line:04a07cf 
Vous lancez votre application Screencast, l'écran d'accueil de Webflix s'affiche aussitôt sur le mur, dévoilant votre liste de programmes préférés. Vous pointez avec le doigt sur l'icône de votre série du moment : Star Mars - The Lone Lightsaber. #line:0ff050f 

<< show lightsaber@holo >>
<< loopFX TVStarwars_LOOP 10000 >>

Le générique démarre et déroule sur un fond scintillant d'étoiles un texte récapitulatif des épisodes précédents. L'infâme Jaba Fett s'est fait cryogénisé par l'impitoyable corsaire Yann Salto. #line:0dd0537 
Yann Salto doit désormais ramener sa capture sur la planète prison Jailhouse. Encore faut-il échapper aux attaques des pirates de l'espace, qui sont nombreux à sillonner autour de Jailhouse... #line:0335c72 

<< if visited("BingeTV") >>
<bi>Étrange... Vous avez l'impression d'avoir déjà vécu ce moment de dilatation du temps...</bi> #line:0a04f28 
<< endif >>

En tous les cas, vous vous en doutiez, Yann Salto a beau être l'un des meilleurs pilotes de l'univers, il se fait amocher son vaisseau ! Il est obligé d'atterrir en catastrophe sur Tortuga. Le suspense est haletant, vous ne pouvez pas ne pas regarder l'épisode suivant. Et ainsi de suite. #line:0f4f9b7 

<< advanceTime 4:00 13:00 1:00 >>
<< stopFX TVStarwars_LOOP >>
<< hide >>

Au final, vous passez trop de temps à binger votre série préférée. #line:023c509 

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: Rappel_libre
tags:
colorID: 0
position: -485,416
---
Vous tentez d'appliquer la méthode du rappel libre. #line:0b8f655 
Vous essayez de vous souvenir de ce que vous avez retenu en cours. Ce n'est pas brillant. Vous vous ne que souvenez de très peu de choses. Pas étonnant, vous n'aimez pas cette matière. #line:07f35ff 
Mais lorsque vous replongez dans la lecture pour verifier vos réponses, vous constatez que ça vous aide vraiment à mettre de l'ordre dans votre esprit. Il y a encore du travail à faire, mais ça sort plus facilement de votre tête ! #line:080fdf4 

<< visit Rappel_libre >>
<< addToGauge learning 60 >>

Vous prenez encore le temps de relire votre cours pour combler les lacunes qui vous gênent encore. Résultat, vous vous sentez bien plus prêt à attaquer l'examen cette fois. #line:0988ada 

<< advanceTime 2:00 >>

<< fadeIn >>
<<jump Action_List_Rewind>>
===
title: Test_Rewind
tags:
colorID: 0
position: -1007,1085
---
<< fadeIn >>
<< setBackground class >>
<< setTime 14:00 >>
<< fadeOut >>

Vous voilà désormais dans la grande salle. Vous vous installez devant votre écran de travail. À peine vous cliquez sur le fichier <b>Sujet d'examen</b> que le compteur se met en route. #line:0352286 

<< advanceTime 4:00 >>

Vous passez les 4 heures suivantes à répondre aux questions à choix multiples qui s'enchaînent les unes derrière les autres. Ce coup-ci, vous êtes sûr de vous. Bingo ! Vous allez y arriver !! #line:006d985 

Euphorique, vous quittez la salle et retrouvez Fred, qui vous attend à l'entrée de la classe. #line:0b43c25 

<<jump Post_Test_Rewind>>
===
title: Post_Test_Rewind
tags: 
colorID: 0
position: -786,1085
---
<< fadeIn >>
<< setBackground door class >>
<< fadeOut >>

Fred_excited: Alors, ça l'a fait ? #line:0cbca32 
You: Ouais ! Trop facile ! #line:0c9acae 
Fred_happy: Yes, je suis content pour toi. Toi qui n'aimais pas cette matière. #line:084e7f4 
You: Ouais, mais ce coup-ci, j'ai mis le paquet ! #line:0266bbe 
Fred_smiling: Ahhhh... Génial ! #line:014e2cf 
You: Je vais avoir <b>A</b> ! #line:00808f8 
Fred_happy: No... Pas moins ? #line:0f7880c 
You: Je te le dis ! #line:08f2afc 
Fred_smiling: C'est génial ! Je t'ai encore jamais vu comme ça. #line:072af68 
You: Ouais ! #line:01fabda 

<< advanceTime 0:15 >>

Fred s'apprête à vous répondre quand la sonnerie retentit. #line:02b4a63 
<< fx Notification >>

<< hide >>
<< setBackground door class-screen >>
Les résultats du test viennent de tomber... #line:0509cfb 

<< if getGauge("learning") >= 66 >>
-> Consulter les résultats #line:09b16c0 
    <<jump Resultats_Good>>
<< else >>
-> Consulter les résultats #line:0524065 
    <<if visited("Assemblee_Sages")>>
        <<jump Resultats_Bad_Again>>
    <<else>>
        <<jump Sages_Defaite>>
    <<endif>>
<< endif >>
===
title: Resultats_Good
tags: 
colorID: 0
position: -529,1332
---
<< empty >>
<< music Victory >>
<< fx Success >>

Voilà... Les scores s'affichent sur l'écran de la porte principale... Vous avez remonté votre retard... Mais vous n'êtes pas encore dans le peleton de tête... Vous n'avez que <b>B</b>. Fred vous distance encore... #line:0395b52 

Fred_smiling: Hé, C'est vachement bien pour une matière que tu détestes. #line:085368d 
You: Facile à dire pour toi. Tu as eu <b>A+</b>. #line:04e24fc 
Fred_happy: Et alors ? Tu feras mieux la prochaine fois. #line:038278d 
<< hide >>

<< advanceTime 0:05 >>

<<if !visited("Assemblee_Sages")>>
    <<jump Sages_Victoire>>
<<endif>>

===
title: Resultats_Bad_Again
tags:
colorID: 0
position: -476,1087
---
TaTaTaTa... Vous n'avez pas bien écouté les leçons des 4 Sages de la Métacognition. #line:0da87fe 

Tentez à nouveau de passer ce premier examen... #line:0ac7005 

<< setTime 8:15 >>

<< fadeIn >>
<< resetLevel >>
<<jump Phone_off_Rewind>>
===

title: Sages_Defaite
tags: noUi
position: -730.5562744140625,1421.6199951171875
---

<< fadeIn >>
<< setBackground bedroom night >>
<< fadeOut >>

Vous vous retrouvez une fois de plus seul, après une nouvelle défaite scolaire.

Sérieusement ! Même en l'ayant passé la semaine dernière, vous avez quand-même raté l'examen ! Frustré, vous vous allongez de tout votre mou sur le lit, vaincu, quand une silhouette familière se glisse dans votre champ de vision. 

<< music Mysterious >>

Temde_neutral: Allons, ne sois pas défaitiste, { $name }, l'apprentissage de la métacognition n'est pas évident au début, mais nous allons tout faire pour t'aider. Viens, suis-moi une fois de plus.
You: Où ça ? On va encore remonter le temps ?
Temde_cheerful: Patience, { $name }. Pour l'instant, cap vers l'Assemblée des Sages. Nous avons quelques astuces à t'apprendre.

<<jump Assemblee_Sages>>

===
title: Sages_Victoire
tags: noUi
position: -730.5562744140625,1421.6199951171875
---

//transition chambre nuit
Vous vous retrouvez une fois de plus seul, fier d'avoir remonté votre note de la dernière fois. Décidément, ces sages vous ont déjà bien aidé !

Coup du hasard, c'est en pensant à eux que vous croisez à nouveau cette silhouette familière qui se glisse dans votre champ de vision.

Temde_neutral: { $name } ! Je vois que tu es déjà parvenu à t'améliorer par toi-même. C'est une capacité que je respecte. Viens, suis-moi une fois de plus.
You: Où ça ? On va encore remonter le temps ?
Temde_cheerful: Pas cette fois, { $name }. Pour l'instant, cap vers l'Assemblée des Sages. Même si tu es débrouillard, nous avons quelques astuces à t'apprendre.

<< fx Warp >>
<< fadeIn 1500 12ea12 >>
<< hide ! >>

<<jump Assemblee_Sages>>

===
title: Assemblee_Sages
tags: noUi
position: -730.5562744140625,1421.6199951171875
---
// transition assemblée


<< visitPermanent Assemblee_Sages >>

Ledai_explaining: Venons en donc au sujet de ce soir, <b>Master Sapiens</b>. #line:01eca65 
Sapiens_thinking: Oui, la mémorisation. #line:085e256 
-> Qu'est-ce qu'elle a, ma mémorisation? #line:0d59581 
    <<jump memorisation>>
===
title: memorisation
tags: noUi
position: -366.2418212890625,1422.0413818359375
---
Ledai_explaining: La mémorisation est un art complexe et puissant qui ne s'apprend pas en une seule nuit. #line:0ea6147 
Temde_thinking: Pourtant, il le faudra bien. On n'a qu'une nuit à accorder à { $name }. #line:0d49277 
Sapiens_angry: Oui bien sûr. Mais dans ce cas, <b>Miss Temdé</b>, évite de m'interrompre sans arrêt... #line:0a57318 

<< fadeIn 200 >>
<< setBackground schema >>
<< fadeOut 200 >>

Sapiens_thinking: En bref, la cartographie de la mémoire dans le cerveau se découpe en deux zones. Comme tes deux hémisphères. #line:0853e70 

<< setBackground schema shelf-disorganized >>

Sapiens_thinking: Il y a tout d'abord <b>La mémoire courte</b>. Elle est utilisée quand tu travailles sur une tâche nouvelle, un nouveau savoir. Elle enregistre ce que tu regardes, ce que tu lis, ce que tu perçois, ce que tu interprètes, ce que tu comprends ou pas... Et elle stocke ça dans un énorme container. #line:0bc94b1 
Sapiens_thinking: Le souci, c'est qu'elle amasse toutes ces nouvelles informations sans ordre précis. Elle les empile en vrac, les unes sur les autres, à gauche à droite. En bref, elle range ça n'importe comment, comme un amateur, un dilettante ! #line:08ab121 

Ledai_explaining: Oui. Ce qui fait que quand tu es à la recherche d'une référence particulière, tu ne sais plus où elle se trouve, ni si tous les documents sont rangés au même endroit. #line:0991ef5 
Sapiens_thinking: Tout à fait, c'est le chaos ! La panique ! La catastrophe !.. Alors que <b>la mémoire longue</b>... #line:03bbbec 

-> C'est quoi la mémoire longue? #line:0617b83 
    <<jump Memoire_Longue>>
===
title: Memoire_Longue
tags: noUi
position: -63.20210266113281,1418.4576416015625
---
<< setBackground schema shelf-organized >>

Sapiens_thinking: <b>La mémoire longue</b> c'est le caviar de la connaissance. Là au contraire, tout y est ordonné, rangé, archivé, classifié. Comme dans une authentique bibliothèque. Dès qu'on a besoin de trouver quelque chose, on sait où le trouver. #line:0b78acd 

Ledai_explaining: Sans compter que chaque élément de <b>la mémoire longue</b> contient un index, une table des matières. Le cerveau n'a plus qu'à suivre le plan et ça roule sans sursaut. #line:0970ca1 
Sapiens_thinking: En bref, le secret pour bien apprendre réside dans ta capacité à consolider tes nouvelles connaissances, les stocker de manière logique et efficace. #line:041c990 
Azul_happy: Tu as saisi ? Nos deux savants veulent te dire qu'ils vont t'aider à faire glisser tes nouveaux savoirs, de la mémoire courte à la mémoire longue. #line:0ba2da0 

<< fadeIn 200 >>
<< setBackground circle >>
<< fadeOut 200 >>

You: Génial, Azul ! Et je fais comment pour glisser et déposer mes révisions dans ma mémoire longue ? #line:02bf1b5 

<<jump Routinisation>>
===
title: Routinisation
tags: noUi
position: 243.36883544921875,1410.058349609375
---
Sapiens_thinking: Pas de miracle sans effort. Pas de médaille sans sueur. Il faut travailler ! #line:0c67f70 
Ledai_explaining: Il faut surtout pratiquer ! Répéter et répéter et encore répéter ! À force de faire, on apprend à refaire. De plus en plus facilement, de plus en plus rapidement. #line:00317ce 
Azul_happy: Nos deux savants ont raison. Ta pratique finit par devenir une routine. Imagine que tu es cuisinier. À force de réaliser la même recette, tu finis pas la maîtriser. #line:050ca2d 
Ledai_explaining: Pour une fois que je suis d'accord avec <b>Azul</b>. Ainsi, tu mémorises la liste des ingrédients, les dosages, comme les étapes de préparation et les temps de cuisson. #line:0e3a8de 
Temde_cheerful: Yep, et comme tu n'as plus besoin d'avoir le mode d'emploi, tu deviens un sage, tu es autonome ! #line:0df9941 
You: Et comment je fais pour que ma pratique devienne une routine? #line:0439386 
Ledai_explaining: Tout simplement en répétant, en appliquant. #line:0832a45 


<< if visited("Rappel_Libre") >>
    You: C'est la méthode du rappel libre, pas vrai ?
    Sapiens_happy: Exact ! Tu l'as déjà bien assimilé, on dirait !
<< else >>
    Sapiens_thinking: C'est ce que l'on appelle, dans la vocabulaire de la métacognition : <b>les rappels libres</b>. #line:0318be0 
<< endif >>

You: Ouaiiiis... Mais va falloir que je me souvienne de tout ça à mon réveil. Comment faire ? #line:0d2186b 
Ledai_happy: En notant toutes les idées importantes dans ton <bi>Journal Mental</bi> ! #line:0838542 

<<jump Journal_Rappel_libre>>

===
title: Journal_Rappel_libre
tags: noUi
position: 528.9749550819397,1406.2884521484375
---
<< fx Warp >>
<< fadeIn 100 ffffff >>
<< hide ! >>
<< setBackground bedroom night >>
<< fadeOut 400 >>

Le paradis verdoyant et rayonnant des 4 sages n'est plus là ! Les 4 sages non plus ! Vous voilà seul... Dans une bulle de noirceur, éclairée par un douce lumière tamisée. Curieusement, vous ne vous sentez pas inquiet. #line:00b6d70 
En outre, vous n'êtes pas totalement isolé. Vous tenez entre vos mains un magnifique "carnet d'ingénieur tout-terrain". Garantie: waterproof, earthquakeproof, flameproof, acidproof... #line:0c1bf30 
Serait-ce le <bi>journal mental</bi> dont parlait Ledai ? #line:063d2d3 
===
