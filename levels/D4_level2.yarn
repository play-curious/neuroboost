#gauges:learning,sleep,food,mentalLoad

title: Start
tags:
position: -2504.752685546875,43.17276573181152
colorID: 8
---
<< fadeIn 1 >>
<< setTime 8:00 3 >>
<< setBackground bedroom >>
<< setGauge learning 0 >>
<< setGauge sleep 100 >>
<< setGauge food 100 >>
<< setGauge mentalLoad 0 >>
<< showGauges >>
<< music Victory >>
<< fadeOut >>

Vous vous dans votre chambre. Il est jeudi matin, encore une fois. Vous mangez votre petit déjeuner tout en pensant à ce que vous allez bien pouvoir faire pour ce projet de mémoire sur la théorie du chaos en macrobiologie post-darwinienne. Motiver et guider Fred : c'est bien joli à dire, c'est plus difficile à faire.  #line:02a3388 
Fred... Vous attendez son appel d'ailleurs. #line:0fc5fb6 

<< loopFX PhoneRing_LOOP 5000 >>
Dring Dring. #line:0308746 

-> Vous décrochez #line:0042053 
    <<jump Appel_Fred>>
===
title: Appel_Fred
tags:
colorID: 0
position: -2287.7581787109375,43.895751953125
---
<< stopFX PhoneRing_LOOP >>

Fred@holo_excited: Hello, { $name }, la forme ? Motivé ?  #line:0a622c4 
You: Oui, je te rappelle qu'on doit finir notre mémoire sur la théorie du chaos dans la macrobiologie post-darwinienne. #line:01e1f78 
Fred@holo_smiling: Cool, tu me retrouves ? #line:0f00fce 
You: Non, viens plutôt me chercher. #line:0b4d03d 
Fred@holo_happy: Non, je suis à la maison, je viens juste de recevoir le dernier GTR : Grand Thrill Rodéo 66 ! Une tuerie ! #line:06aaf28 
You: Je te le répète, on doit finir notre mémoire ! Avant minuit ! #line:0320c2d 
Fred@holo_smiling: Yes yes. Viens d'abord chez moi ! #line:0dcf2cc 
You: Fred, je peux pas faire le boulot tout seul. J'ai besoin de toi. #line:04016b2 
Fred@holo_smiling: Yes yes, on verra après. Viens ! #line:05b6ac7 
You: Fred, sérieux... #line:0f99a58 
Fred@holo_smiling: Je te dis, viens ! #line:0cca79b 
<<hide>>
Tant pis, vous avez essayé de le raisonner par téléphone, ça ne marche pas. Il faut essayer autre chose : #line:041d168 

-> Vous allez à la bibliothèque et vous le harcelez jusqu'à ce qu'iel vous rejoigne. #line:0b30d3e 
    <<jump Bibli_soloAM>>
-> Vous allez chez lui et vous l'obligez à venir à la bibliothèque. #line:09ab66e 
    <<jump ChezFred>>
===
title: Bibli_soloAM
tags: 
colorID: 0
position: -2066.117431640625,-90.2164535522461
---
<< fadeIn >>
<< setBackground library >>
<< setTime 8:15 >>
<< fadeOut >>

Vous vous asseyez à une table de la grande bibliothèque. Quelle ambiance de concentration et de travail. Ça motive. Vous textez Fred pour lui préciser dans quelle zone du bâtiment vous l'attendez.  #line:0210c73 
Vous y ajoutez un paramètre. Et toutes les dix minutes, votre palmphone renvoie votre texto à Fred. Vous espérez ainsi le forcer à vous retrouver. Le temps passe, les minutes et les heures s'écoulent.  #line:09be2be 
<< advanceTime 4:45 >>
Mais que fait donc Fred ? Le voilà enfin. #line:097a9fb 
<< show fred >>

You: J'ai pas arrêté de te relancer, tu foutais quoi ? #line:0f1bfbc 
Fred_smiling: Je jouais à GTR 66 ! C'est trop de la bombe nucléaire ! #line:015d8ac 
You: Et mes textos ? Je t'en ai balancé des centaines ! Tu les as bien reçus ? #line:06d46cb 
Fred_smiling: Je les ai pas entendu, j'avais le casque sur la tête. #line:0a211cc 
You: T'as vu l'heure ? La bibliothèque ferme à 20h ! #line:045040f 
Fred_happy: No worry, j'ai tout dans la tête. #line:033461c 
You: J'espère bien. #line:02af4ec 
Fred_excited: On commence par quoi ? #line:02555ca 
You: Heu. #line:0aea5f3 
Fred_smiling: C'est toi le chef de projet, non ?.. #line:046c0db 

Vous vous rappelez des paroles d'Azul, c'est vous le capitaine de ce projet. C'est à vous de mener la barque. #line:01a6df8 

-> Se mettre au travail  #line:03aeea4 
    <<jump Liste_taches>>
===
title: Bibli_soloPM
tags: 
colorID: 0
position: -949.32763671875,1219.6046142578125
---
<< fadeIn >>
<< setBackground library >>
<< fadeOut >>

Vous retournez vous asseoir à la même table que ce matin. Vous textez Fred pour lui dire que vous l'attendez au même endroit. Vous ajoutez une condition de renvoi automatique du texto. S'iel ne répond pas dans les 10 minutes, le message lui est renvoyé de nouveau. Le temps passe, les minutes et les heures s'écoulent. Enfin, Fred revient en salle... #line:03f23dc 

<< show fred >>
<< advanceTime 2:00 >>

You: Tu te fous de moi ou quoi ? #line:08b782b 
Fred_smiling: C'est trop de la bombe nucléaire GTR 66 ! #line:05a2da8 
You: Je te rappelle que la bibliothèque ferme à 20h ! #line:0a2d4c0 
Fred_happy: No worry, j'ai tout dans la tête, faut juste que je me pose un moment. #line:0b7cfd4 
You: Il est grand temps. #line:0844380 
Fred_smiling: Je t'écoute. #line:0ed7142 

-> Se remettre au travail  #line:0d7cbe3 
    <<jump Liste_taches>>
===
title: Bibli_binome
tags: 
colorID: 0
position: -1840.7229614257812,139.0914535522461
---
<< fadeIn >>
<< setBackground library >>
<< fadeOut >>

Vous voilà à la bibliothèque avec Fred. #line:00123b2 

Fred_excited: Au fait, { $name }, félicitations ! #line:0bf05f2 
You: Merci. #line:03e555c 
Fred_smiling: T'as eu [b]B-[/b] à ta note de synthèse sur la PNLIAA #line:0901a2a 
You: Ah. #line:0aa44c4 
Fred_surprised: Tu t'en fiches ? #line:0aaabd3 
You: C'est bien. En attendant, mettons-nous au travail. #line:02ddbff 
Fred_happy: J'ai eu pareil. [b]B-[/b] #line:091b440 
You: Félicitations Fred ! Tu vois, quand tu travailles. #line:0c7d464 
Fred_smiling: Travailler ? Si tu savais la vitesse avec laquelle j'ai torché ma rédaction. #line:0323ce1 
You: Justement, on va éviter de faire la même co.... bêtise avec la théorie du chaos. D'accord ? #line:0da444f 
Fred_smiling: OK. On commence par quoi ? #line:01e1669 
You: Heu... #line:07a5945 
Fred_happy: C'est toi le chef de projet, non ? Tu es passé maître en théorie du chaos... #line:0a089d1 

Vous vous rappelez des paroles de Azul. C'est à vous de piloter ce travail, de motiver et guider Fred dans ce projet collaboratif. #line:03873cc 

-> Se mettre au travail  #line:0e68709 
    <<jump Liste_taches>>
===
title: Liste_taches
tags: 
position: -1617.8306884765625,24.821166694164276
colorID: 1
---
<< if visited("Notes_You", "Notes_Fred", "Redaction", "Plan") >>
<<advanceTime 5:00 20:00 0:05>>
<<jump Exit_Bibli>>
<< endif >>

<< if !visited("Liste_taches") >>
Fred_happy: Alors, on attaque quoi maintenant, boss ? #line:0c3407b 
<< visit Liste_taches >>
<< endif >>

<< if !isTimeOver("20:00") >>
-> Travailler avec Fred sur le mémoire #line:0620e87 
    <<empty>>
    -> Montrer vos notes à Fred#once #line:0611eb4 
        <<jump Notes_You>>
    -> Demander à Fred ses notes#once #line:0ead95b 
        <<jump Notes_Fred>>
    -> Reprendre la rédaction du mémoire#once #line:0c51ae2 
        <<jump Redaction>>
    -> Valider le plan du mémoire#once #line:08f7a73  
        <<jump Plan>>
-> Générer une bulle d'immersion studieuse#once #line:0bc0935 
    <<jump Zero_distraction>>
-> Faire la pause lunch#once #line:0eb4b51 
    <<jump lunch>>
<<else>>
    <<jump Exit_Bibli>>
<< endif>>

===
title: Redaction
tags: 
colorID: 0
position: -1391.3614501953125,446.9007568359375
---
You: On reprend la rédaction détaillée du mémoire ?  #line:0c284d3 
Fred: Yes. On se répartit la tâche comment ? #line:005280e 
You: Heu...  #line:01c46ea 

<<visit Redaction>>

-> Demander l'avis de Fred  #line:06e222c 
    << jump Redaction_Good >>
-> Donner les ordres à Fred #line:07279f3 
    << jump Redaction_Bad >>
===
title: lunch
tags: 
colorID: 0
position: -1389.5840454101562,1321.8181457519531
---
You: Tu n'as pas faim? #line:09efca7 
Fred_smiling: Si, je manque clairement de fuel là. #line:0fb3d6a 
You: On va manger à la cafeteria de la bibli? #line:01672ed 
Fred_smiling: Yes yes ! #line:0339b31 

-> Cafeteria de la bibliothèque  #line:0c416da 
    <<jump Lunch_bibli>>
===
title: Zero_distraction
tags: 
colorID: 0
position: -1396.09326171875,1672.4146423339844
---
Vous demandez au récepteur de l'IA de la bibliothèque de déployer une "Sphère de Travail Immersive". Aussitôt un dôme de lumière opaque se referme sur votre table et vous isole du reste de la bibliothèque et des étudiants qui y sont. Le son devient net et réverbéré, comme dans un studio d'enregistrement.  #line:0968648 
Fred_smiling: Great, ta bulle de silence. On va pouvoir parler tous les deux sans honte ! #line:032b9c9 
You: Tu as tout compris. Maintenant que nous sommes enfermés dans une bulle de travail, nous allons pouvoir échanger verbalement, toi et moi. C'est cool non ? #line:0d7834f 
Fred_happy: Yolo, c'est trop dingue ton truc ! J'adore ! #line:0dd2ca1 
You: Bon, on reste concentré, Fred. On doit finir la rédaction du mémoire aujourd'hui. #line:0f7cd07 
Fred_happy: Yes Yes...  Et si je te montrais ce qu'on peut faire dans le dernier GTR, y a déjà plein de playthrough sur Zootud ? #line:09a583b 
You: Fred, on reste concentré sur le sujet du mémoire. De toute manière, ça ne marcherait pas. Nos connexions sont limitées aux sites académiques. #line:0056a1d 
Fred_sad: Ah, c'est pas si fun ta boule... #line:09940ab 
You: C'est pas fun, c'est sérieux. #line:05d8166 
Fred_sad: Ah. #line:07c6912 
You: Rappelle-toi qu'on a un diplôme à décrocher. #line:06c7c8c 
Fred_smiling: C'est vrai. #line:0fc578f 

<< visit Zero_Distraction >>

->  Reprendre le travail  #line:0c0955f 
    <<jump Liste_taches>>
===
title: Lunch_bibli
tags:
colorID: 0
position: -1167.3054809570312,1321.2256164550781
---
<< hide >>
<< fadeIn >>
<< setBackground cafeteria >>
<< fadeOut >>

Vous commandez un lunch à la bibliothèque.  #line:09e9a0a 
<< fx EatCook >>

C'est jour de relâche, vous commandez un Burger Big Protéine Double Cheese. C'est pas équilibré mais vous avez assez d'effort à faire par ailleurs.  #line:053045f 

<< addToGauge food ?? >>
<< addToGauge sleep ?? >>

// quel impact dans la boucle de jeu de cette journée les effets sur jauge food et sleep? utile?

Vous rigolez quand vous observez Fred engloutir son Tacos Tex-Mex XXXL et liquider un Xeno Cola double-sugar ! Vous avez mal pour son estomac. Vous êtes obligé d'aller aux toilettes. Quand vous revenez, Fred est à nouveau en train de jouer avec un étudiant sur une Nanontedo à GTR 66 !  #line:073a69e 

<< show fred >>
You: Fred, on y retourne, j'ai besoin de toi.  #line:0df9445 
Fred_excited: Yes, yes, attends 5 minutes encore. #line:055c14d 
You: Fred, c'est toi le créatif du projet, moi je suis le chef d'ouvrage. #line:02324cc 
Fred_smiling: Please... #line:00297e6 
<<advanceTime 1:00>>
-> Retourner en salle et laisser Fred jouer quelques minutes #line:012b100 
    << jump Bibli_soloPM >>
-> Insister pour que Fred vienne avec vous  #line:071a93f 
    << jump Fred_goesback_towork >>
===
title: Notes_You
tags: 
colorID: 0
position: -1399.2734375,-394.30879974365234
---
<< advanceTime 1:00 >>

You: Je sais que tu as avancé depuis la dernière fois.  #line:002fcbd 
Fred_happy: Yes yes. On échange à nouveau nos commentaires et nos notes ? #line:0530a0b 
You: On commence par les miennes ? #line:0cd9556 
Fred_smiling: Yeeees. Je t'écoute ! #line:0a51cb6 

<<visit Notes_You>>

<< if !visited("Zero_Distraction") >>
You: Non, mais je ne peux pas te lire à haute voix, on est à la bibliothèque ! #line:08e64e8 
<< endif >> 

-> Vous lui lisez vos notes << if visited("Zero_Distraction") >> #line:0bfb9d2 
    << jump Notes_You_Good >>
-> C'est à lui de lire vos notes #line:01f3a4c 
    << jump Notes_You_Bad >>
===
title: Fred_goesback_towork
tags: 
colorID: 0
position: -949.209098815918,1430.5440979003906
---
Fred_sad: T'es vraiment pas drôle. #line:028d7bc 
You: J'ai besoin de toi.  #line:036ff77 
Fred_sad: Ça, j'ai bien entendu. Et si j'avais besoin de toi aussi ? #line:0c3d161 
You: Tu plaisantes, tu es le meilleur de la promo. Ceci dit, comme les Sages l'ont dit, sans moi, tu te disperserais et tu n'y arriverais pas. #line:0ea7b37 
Fred_surprised: Les Sages ?! #line:0b6426a 
You: Laisse tomber. #line:00a0389 

-> Vous retournez au travail à la bibliothèque. #line:005ba03 
    << fadeIn >>
    << setBackground library >>
    << fadeOut >>
    <<jump Liste_taches>>
===
title: Exit_Bibli
tags: 
position: -1396.7598876953125,1892.27099609375
colorID: 1
---
<< if getGauge("learning") > 66 >>

<< fx Notification >>
Ding Ding Dong. L'horloge officielle de la bibliothèque de l'Académie d'Omniscience et de Polytechnologie Universelle vient de sonner pour la dernière fois. Les robots vous font signe de remballer vos affaires et de quitter les lieux. C'est à regret que Fred et vous, sortez de l'établissement. #line:00f03b9 
You: Non, on a presque fini. #line:0e613d9 
Fred_happy: Cool, tu as tout dans les mains : le plan, les idées fortes. Tu n'as plus qu'à développer tout ce qu'on a dit. No worry, ça va le faire, je te fais confiance. #line:06a417f 
You: Et toi ? #line:0c6c678 
Fred_happy: Je m'occupe de la conclusion et de l'intro. #line:0e56f3e 
You: OK. #line:02bda36 
Fred_smiling: Keep faith. #line:031f90d 
<< hide >>
C'est drôle. C'est Fred désormais qui vous pilote et vous rassure sur votre capacité à finir dans les temps. Troublé, un peu hésitant, vous retournez dans votre chambre. #line:03fa023 

<<jump Bedroom>>

// Sinon, joueur n'a pas réussi toutes ses conditions de victoire
<< else >>
<< fx Notification >>

Ding Ding Dong. L'horloge officielle de la bibliothèque de l'Académie d'Omniscience et de Polytechnologie Universelle vient de sonner pour la dernière fois. Les robots vous font signe de remballer vos affaires et de quitter les lieux.  #line:07ecec4 
You: Non, on est loin d'avoir fini !! #line:0c62d43 
Vous criez, vous paniquez complètement, vous vous mettez à suer à grosses gouttes, votre vue se trouble, votre ouïe sature, parasité par un effet de larsen. Vous avez encore échoué !  #line:04492ae 
-> Vous allez devoir recommencer cette journée épuisante à la bibliothèque #line:07dace0 
    << resetLevel >>
    << hide >>
    << jump Start>>

<< endif >>
===
title: Notes_Fred
tags: 
colorID: 0
position: -1393.3150634765625,24.101524353027344
---
<< advanceTime 2:00 >>

You: Je sais que toi aussi, tu as avancé sur le travail de rédaction du mémoire. #line:01b1dcd 
Fred_smiling: Bien sûr. #line:0fed354 
You: Tu as tes notes avec toi ? #line:0bf6862 
Fred_happy: Tu veux pas plutôt qu'on en parle de vive voix ? #line:0a57c1c 

<< if !visited("Zero_Distraction") >>
You: Heu... C'est plus facile pour moi de les lire. Surtout qu'on est à la bibliothèque, il ne faut pas faire du bruit. #line:0f34e21 
<< endif >> 

<<visit Notes_Fred>>

-> Écouter Fred présenter ses notes <<if visited("Zero_Distraction")>> #line:031034a 
    << jump Notes_Fred_Good >>
-> Demander à Fred de vous envoyer ses notes écrites #line:0b165d9 
    << jump Notes_Fred_Bad >>
===
title: ChezFred
tags: 
colorID: 0
position: -2067.0577392578125,138.21179962158203
---
<< fadeIn >>
<< setBackground bedroom_fred >>
<< fadeOut >>

Fred vous fait entrer à la première sonnerie. #line:024dd08 

<< advanceTime 0:05 >>
 
Fred_excited: Tu vas voir, ce GTR, c'est de la giga-bombe ! #line:02fbd0b 
You: Fred, faut qu'on aille à la bibli, tu joueras ce week-end. #line:0847944 
Fred_smiling: T'es pas drôle. Viens, juste pour 5 minutes. #line:00f974b 
You: Non, Fred. On doit s'y mettre au plus vite, sinon, ça recommencera. #line:052b050 
Fred_surprised: Recommencera ?! #line:00bf353 
You: Rien, laisse tomber. #line:045b04a 
Fred_smiling: Alors on joue ? #line:07a065e 
You: Non. J'ai besoin de toi, Fred, s'il-te-plaît. #line:034a9a9 
Fred_sad: Haaaan, t'es relou parfois. #line:045699a 
You: Je veux l'avoir mon diplôme. #line:0a8ab2a 
Fred_sad: D'accord... #line:011dc57 

Bien joué, vous avez touché la corde sensible de Fred, iel accepte de vous suivre... À la bibliothèque.  #line:00a52f4 

<<jump Bibli_binome>>
===
title: Bedroom
tags: 
colorID: 0
position: -1178.4630126953125,1891.7039794921875
---
<< fadeIn >>
<< setBackground bedroom night >>
<< fadeOut >>

Vous demandez à l'IA de votre studio de passer en mode "Immersion Studieuse".   #line:0949681 
// y-a-t-il une animation son/visuelle? La jauge de la charge mentale est renforcée? Le joueur le voit? 
// C'est une proposition, pas indispensable dans cette quête, juste possibilité que le joueur réutilise ce qu'il a débloqué
Vous ne vous accordez qu'une pause dîner. Une soupe de nouilles aux légumes et un sorbet multi-fruits. Vous vous attaquez ensuite à la rédaction finale de votre mémoire. Les échanges avec Fred sont encore frais dans votre tête. Vous n'hésitez pas à convertir ses paroles en texte pour enrichir vos propres paragraphes.  #line:04d4a5b 
Heureusement que l'IA de la bibliothèque a enregistré tous les échanges que vous avez eu avec lui aujourd'hui : ça vous fait gagner un temps fou. #line:064a2e4 
<<setTime 21:30>>
<< addToGauge learning 20 >>
Une fois que vous estimez avoir fini, vous annulez le mode "Immersion Studieuse" et vous contactez Fred. #line:0f9806b 
Fred@holo_excited: T'as fini ? #line:0eb7fce 
You: Oui. Et toi ? #line:05c5e53 
Fred@holo_happy: Je t'envoie ma participation à mon tour. Désolé, c'est modeste. #line:01d28a8 
You: Je sais, l'écrit, c'est pas ton truc. Mais tu m'as énormément aidé sur ma partie. Avec tes idées et tes remarques. #line:0e6d6c9 
Fred@holo_smiling: Merci. On fait la paire. #line:03fb032 
You: Je sais pas. #line:0c35c9a 
Fred@holo_smiling: Yes yes, je te le dis. #line:0d43b47 
Fred raccroche. Vous recevez dans votre boîte mail sa contribution. Vous l'intégrez dans le texte final et vous déposez le tout dans le FTP de l'Académie. Bravo ! Mission accomplie... #line:0076812 
===
title: Plan
tags: 
colorID: 0
position: -1390.7283935546875,891.5607299804688
---
You: Tu te souviens du plan ? #line:0608a08 
Fred_smiling: Ton plan ?  #line:010b808 
You: Oui, je t'ai demandé d'en proposer un, t'as refusé. #line:0a49c05 
Fred_smiling: Le plan, ça vient avec les idées, les breakthrough. #line:02066f7 
You: Sans plan, on ne peut pas avancer dans la rédaction. Et on en est là, la rédaction, la dernière ligne droite. #line:0696f59 
Fred_sad: Mais pourquoi on doit suivre l'axe que tu as proposé ?  #line:091835b 
You: T'en vois d'autre, #line:0d08340 
Fred_happy: Oui, plein, y a l'embarras du choix. #line:0b977ec 
You: On n'a plus le temps. #line:004dc90 
Fred_smiling: On a toujours le temps. #line:0e52680 

<< visit Plan >>

-> Vous prenez le temps de discuter avec Fred de tous les plans possibles  #line:0d5fcf7 
    << jump Plan_Good >>
-> Vous présentez votre plan à Fred #line:08a8a75 
    << jump Plan_Bad >>
===
title: Notes_You_Good
tags: 
position: -1171.1915283203125,-520.5768127441406
colorID: 2
---
You: Tu sais, moi, j'échange par écrit. C'est plus simple. #line:0d67c8e 
Fred_surprised: Ah. Pour moi... #line:017912f 
You: Ouais, je sais. C'est le contraire. Exceptionnellement, je fais l'effort de te présenter oralement ce que j'ai accumulé de mon côté. #line:0bad325 
Fred_smiling: Cool. #line:09fbef3 
You: C'est une première pour moi. J'espère que je ne serai pas confus. #line:0b1d3d8 
Fred_happy: Pas grave. #line:0fd4f63 
You: Merci. #line:0fa3688 
Ne sachant pas par quoi commencer, vous dites la première remarque qui vous vient en tête. Et puis, de fil en aiguille, une remarque en amène une autre, qui en amène une autre. Fred est un auditeur très bienveillant. En outre, iel rebondit sur certaines de vos idées et vous incite à les clarifier, les critiquer, les nuancer. #line:0d62399 
Au final, ce n'est pas vous qui avez présenté votre travail à Fred. C'est Fred qui a revu vos notes et a corrigé ce qui n'allait pas. C'est génial. #line:0b450ec 

<<advanceTime 1:00>> 
<< addToGauge learning 20 >>

Enfin, comble de joie : l'IA vous informe qu'elle a enregistré toute la conversation et qu'elle vous transfère le fichier audio sur votre ordinateur ! #line:03a3fb9 

<<jump Liste_taches>>
===
title: Notes_You_Bad
tags: 
position: -1169.79443359375,-313.6395568847656
colorID: 2
---
You: On fait ça par écrit. #line:0e9165e 
Fred_surprised: Pourquoi ? #line:0cdd371 
You: Parce que ! T'as dit que c'est moi le chef, non ? #line:09a3070 
Fred_sad: Oui, chef... #line:0c5e7f6 

Vous envoyez vos documents de travail à Fred. Vous voyez votre ami grincer des dents. #line:0b9fc4e 
Iel peine à lire et relire vos docs. Iel n'arrête pas de faire des pauses, de vous interpeller, mais vous limitez les discussions pour vous concentrez sur votre travail. #line:096efcd 
<<advanceTime 1:00>>
<< addToGauge learning 10 >>
Avez-vous fait le bon choix ? #line:013daa2 

<<jump Liste_taches>>
===
title: Notes_Fred_Good
tags: 
position: -1169.795654296875,-90.88680267333984
colorID: 2
---
Fred_happy: Tout est dans ma tête. Tu veux que je commence par quoi ? #line:0ca8f0f 

<< advanceTime 1:00 >>
<< addToGauge learning 20 >>

Vous comprenez que c'est à vous de questionner Fred et, en quelque sorte, de lui tirer les vers du nez. Vous passez les deux heures suivantes à lui poser des questions, à écouter ses réponses, à rebondir sur ces idées, ces commentaires. #line:0e52cd5 
C'est vraiment super stimulant et super enrichissant. Fred a vu des trucs que vous n'aviez pas remarqué. Cette personne a un don, c'est certain. Après, iel ne correspond pas totalement aux standards académiques. C'est sa force et sa faiblesse.   #line:06c6070 
Quand vous pensez avoir fait le tour de la question avec lui, l'IA vous informe qu'elle a enregistré toute la conversation. Elle vous transfère le fichier audio sur votre ordinateur. Génial ! #line:0b03dfd 

<<jump Liste_taches>>
===
title: Notes_Fred_Bad
tags: 
position: -1169.442138671875,123.66140747070312
colorID: 2
---
Fred pianote sur son ordinateur, l'instant d'après, vous réceptionnez ses documents... Incroyable, il n'y a pas grand chose d'exploitable. Que des bouts de phrases, de mots et quelques commentaires en annexe. #line:0082f38 
You: Azul avait raison, tu es fâché avec l'écrit toi. #line:0f22648 
Fred: C'est qui Azul ? #line:0fb3b4a 
You: Laisse tomber. Tu veux pas faire un effort ? #line:0a11445 
Fred: Tsss. #line:0599f66 
You: Pour moi, Fred, s'il-te-plaît. Je sais que tu es intelligent, que tu en sais des choses. #line:0cb3a4f 
Fred: Bon, d'accord. Puisque tu insistes. C'est bien parce que c'est toi... #line:0b3e63f 
Vous voyez à son air pincé que Fred se fait violence pour vous faire plaisir. Après un bon moment, Fred, vous renvoie les fichiers mis à jour. #line:0d827ea 
Fred: J'ai fait un effort. J'ai écrit des vraies phrases. #line:058646a 
En effet, c'est mieux, mais ça reste laconique. Il reste encore beaucoup à faire mais vous n'obtiendrez pas plus de Fred de cette manière. #line:0057664 
Avez-vous fait le bon choix ? #line:0af7001 
<<advanceTime 1:00>>
<< addToGauge learning 10 >>

<<jump Liste_taches>>
===
title: Plan_Good
tags: 
position: -1171.308837890625,773.103515625
colorID: 2
---
You: C'est le dernier jour, on doit choisir un plan et s'y tenir.  #line:0774d64 
Fred_happy: Très bien. Dis-moi ce que tu as retenu et je te dirai ce que j'en pense. #line:0da186f 
You: OK.  #line:02303dd 
Vous farfouillez dans vos dossier et vous ouvrez le fichier qui contient le plan détaillé du mémoire sur la théorie du chaos en macrobiologie post-darwinienne. #line:0d3ee9f 
Vous commencez ensuite à lui présentez vos trois grandes parties, vos sections et sous-sections. Fred se révèle un infatigable représentant du jury. Iel ne manque pas d'arguments ni de pertinence dans ces remarques et ces questions. #line:0a18b2d 
En clair, iel vous déstabilise, iel vous secoue. Iel vous fait douter de votre problématique et de vous. Sauf que, c'est là le génie de Fred. Fred ne se contente pas de vous perturber, iel vous propose des pistes alternatives, des questions et des arguments auxquels vous n'aviez pas pensés. #line:07d69b4 
En fait, iel secoue l'arbre de votre argumentation pour vous aider à cueillir tous les fruits qu'il a produit. Grâce à la contribution de Fred, vous comprenez que vous avez un plan en béton armé. #line:03502f1 
<< advanceTime 2:00 >>
<< addToGauge learning 20 >>

<< jump Liste_taches >>
===
title: Plan_Bad
tags: 
position: -1172.7930908203125,995.4922485351562
colorID: 2
---
You: Ben c'est le problème. On n'a plus le temps d'hésiter. On doit choisir un plan et s'y tenir.  #line:04260cf 
Fred_sad: C'est vraiment dommage. #line:0c92611 
You: Je te rappelle qu'on doit le rendre ce soir ce mémoire. C'est maintenant trop tard pour changer d'avis. Je te renvoie le plan et on le valide. Un point, c'est tout. #line:0fe17ad 
Vous sentez que vous n'avez peut-être pas pris la bonne décision. Fred se révèle un infatigable avocat de l'accusation. Iel ne manque pas d'arguments ni de pertinence dans ces remarques et ces questions. Commentaires qui ne font que fragiliser votre problématique et douter de vous. #line:0531726 
Si Fred avait raison... Vous allez devoir revivre encore une fois cette journée de jeudi. Pourvu que non... #line:0264a4e 

<< advanceTime 1:30 >>
<< addToGauge learning 10 >>

<<jump Liste_taches>>
===
title: Redaction_Good
tags: 
position: -1168.8485107421875,337.4625244140625
colorID: 2
---
Vous vous rappelez des conseils d'Azul. Il faut solliciter Fred, sur ses connaissances et ses opinions. De manière verbale.  #line:027a1aa 
Vous prenez le temps de lui résumer oralement ce que vous avez déjà sur la question des modèles chaotiques, déterministes dont le degré d'erreur croît de manière exponentielle. Votre présentation n'ennuie pas Fred, au contraire, votre pote se trouve être une source inépuisable d'idées et de référence pour étayer cette partie. #line:0d6c894 
Vous enchaînez avec la portée de l'effet papillon et l'imprévisibilité des systèmes dynamiques à long terme. Là encore, échanger avec Fred vous permet de consolider vos arguments et de donner plus de profondeur à cette question. #line:0863687 
Enfin, vous énumérez brièvement les éléments que vous avez déjà intégrés dans la troisième partie sur la macrobiologie et la remise en question du postulat darwinien de l'évolution comme un schéma linéaire et donc prévisible. Fred se montre encore généreux et vous propose des réflexions, des idées, des hypothèses auxquelles vous n'aviez pas pensées. #line:0a38763 
Au final, vous constatez qu'avec le concours de Fred, votre rédaction s'est considérablement étoffée. Elle est vraiment solide et bien charpentée. Il ne vous reste plus qu'à arranger votre démonstration de manière plus académique et plus littéraire et le tour sera joué.  #line:0d7870c 
Alors que vous terminez de noter un truc dans votre agenda, l'IA vous informe qu'elle a enregistré toute la conversation. Elle vous expédie le fichier audio sur votre ordinateur. Parfait ! Si vous avez oublié un truc, vous n'aurez qu'à réécouter l'enregistrement... #line:0399372 
<< advanceTime 3:30 >>
<< addToGauge learning 20 >>

<<jump Liste_taches>>
===
title: Redaction_Bad
tags: 
position: -1169.8704833984375,554.6487426757812
colorID: 2
---
You: Je m'occupe du problème des modèles déterministes dont le degré d'erreur croît de manière exponentielle. Je prends également en charge l'effet papillon et l'imprévisibilité des systèmes dynamiques à long terme. Je te laisse réfléchir sur la révolution du paradigme post-darwinien en macrobiologie pour la conclusion ? #line:0a59d4a 
Fred_smiling: OK. Et qui fait l'introduction ? #line:03a4d80 
You: Toi, par exemple. N'oublie pas le problème à 3 corps de Poincaré dans l'introduction. Je te demande pas d'écrire beaucoup, non ? #line:0fff9aa 
Fred_sad: Non, c'est vrai. #line:0699c86 
You: Cool. #line:0da4c78 
Vous reprenez vos brouillons et vos notes et vous commencez à mettre au propre vos idées. Vous vous voulez bien faire les choses. Vous prenez le temps de contrôler régulièrement vos sources et de réutiliser dans votre argumentation les données que vous avez glanées.  #line:0ce4d55 

<< hide >>
<< fadeIn >>
<< advanceTime 3:00 >>
<< addToGauge learning 10 >>
<< fadeOut >>

Quand vous relevez la tête, vous voyez Fred, qui vous sourit. #line:0256ae6 
Fred_smiling: J'ai plein d'idée pour la conclusion. Et pour l'introduction aussi. #line:0bf97db 
You: Noon ! #line:0feaaae 
Manifestement, ce n'était pas le meilleur choix possible. #line:0583471 
<<jump Liste_taches>>
===