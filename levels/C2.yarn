#gauges:learning,sleep,food

title: Start
tags: noUi
position: -1627,-493
colorID: 8
---
<< fadeIn  >>
<< setBackground door class >>
<< fadeOut >>

Fred_smiling: Allô ? Tu m'entends pas ou quoi ?
You: Pardon, Fred, j'étais ailleurs pendant un instant... Tu disais ?
Fred_happy: La fête organisée par les docteurs, tu viens ou quoi ? Je leur ai promis que je relèverais leur défi du roi de la soirée. Je peux pas me défiler !
You: Ben, le truc c'est que... Demain matin, on a un exercice de simulation. Un test de pilotage de drone d'ingénierie ! #line:03a1bce 
Fred_happy: Et alors ? On l'a déjà fait 100 fois, c'est pas si dur. #line:06de838 
You: Oui, mais mon drone est pas prêt ! La coque est trop lourde et mon système de navigation est encore bancal. 
Fred_smiling: Tranquille, le mien fonctionne comme du tonnerre, je te passerai mes plans !
You: Même si j'ai tes plans, il me faudra au moins deux heures pour faire les changements et les tester...
Fred_happy: Ok, je vois que t'es difficile en affaires... Si les docteurs m'ont donné ce défi, c'est pas pour rien, mais je vais être contre le champion des teufeurs, j'ai nommé : BB Rex. J'aurai besoin de toi si je veux être sûr de gagner.
Fred_excited: Si on gagne le défi des docteurs, tu peux être sûr que je prendrai du temps pour t'aider avec ton drone demain matin. T'en dis quoi ?
Fred serait prêt à vous donner ses plans ET son aide personnelle pour réussir ce défi ?! Il tient vraiment à sa réputation, celui-là... En tout cas, vous savez qu'avec ses talents d'ingénierie, vous pourriez avoir une très bonne note, voire une excellente !
You: Bon... Tu me laisses pas le choix, là ! On se retrouve vers 21h30 ?
Fred_smiling: Haha, ça, c'est l'attitude que je préfère ! Allez, à plus tard !

-> Rentrer
    << hide >>
    <<jump Back_Home>>


===

title: Back_Home
tags: 
position: -1367,-540
---
<< setGauge learning 0 >>
<< setGauge food 60 >>
<< setGauge sleep 60 >>

<< fadeIn >>
<< setBackground bedroom >>
<< setTime 18:30 >>
<< fadeOut >>

<< showGauges >>
Vous vous retrouvez seul. Vous et Fred avez rendez-vous à 21h30, mais entre temps vous pouvez faire ce que vous voulez avant la soirée ! Voyons voir...
<<jump Actions_Before_Party>>
===

title: Actions_Before_Party
tags: freechoice
position: -1210,-459
---
<< setBackground bedroom >>
<< fadeOut >>

-> Cuisine@cuisine #line:0717775 
    <<jump Cuisine_S >>
-> Porte@porte #line:0d21566 
    <<jump Porte_S>>
-> Bureau@bureau #line:048f567 
    <<jump Bureau_S>>
-> Lit@lit #line:03dbf5c 
    <<jump Lit_S>>
-> Salon@salon #line:0645318 
    <<jump Salon_S>>
===

title: Cuisine_S
tags: subchoice
position: -1290,-229
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>
-> Manger un bout avant la fête << if !visited("Dinner") >>
    <<jump Eat_Before_Party>>
-> back
    <<jump Actions_Before_Party>>
===

title: Eat_Before_Party
tags:
position: -1480,-308
---
Vous préparez un repas rapide, ni trop léger, ni trop lourd, pour éviter d'être tout engourdi sur la piste de danse. Cela vous permettra de tenir un peu avant de devoir vous jeter sur les gourmandises !
<< visit Dinner >>
<< addToGauge food 50>>
<<jump Actions_Before_Party>>
===

title: Bureau_S
tags: subchoice
position: -829,-486
---
<< fadeIn >>
<< setBackground bedroom desk >>
<< fadeOut >>
-> Revoir les plans du drone << if !visited("Drone_Review") >>
    <<jump Drone_Review>>
-> back
    <<jump Actions_Before_Party>>
===

title: Drone_Review
tags: 
position: -673,-592
---
Vous profitez de ce moment au calme pour tenter d'améliorer votre drone comme vous le pouvez. Vous repassez d'abord sur le module d'assistance à la trajectoire, mais il y a encore du boulot avant d'avoir un système acceptable.
<< addToGauge learning 20>>
Décidément, vous aurez bien besoin de ce coup de pouce de Fred !
<< visit Drone_Review >>
<<jump Actions_Before_Party>>
===

title: Lit_S
tags: subchoice
position: -878,-289
---
<< fadeIn >>
<< setBackground bedroom bed >>
<< fadeOut >>
-> Faire une sieste << if !visited("Sieste") >>
    <<jump Sieste>>
-> back
    <<jump Actions_Before_Party>>
===

title: Sieste
tags: 
position: -816,-411
---
Vous profitez du calme avant la fête pour reprendre vos forces en faisant un somme. En vous réveillant, vous vous sentez débordant d'énergie !
<< visit Sieste >>
<< addToGauge sleep 50>>
<<jump Actions_Before_Party>>
===

title: Salon_S
tags: subchoice
position: -1032,-604
---
<< fadeIn >>
<< setBackground bedroom chill >>
<< fadeOut >>

-> Pratiquer ses gammes à la guitare << if !visited("Guitar_Training") >>
    <<jump Guitar_Training>>
-> S'entraîner au Jungle Body Beat << if !visited("Danse_Training") >>
    <<jump Danse_Training>>
-> Regarder Star Mars << if !visited("Star_Mars_Incursion")>>
    <<jump Star_Mars_Incursion>>
-> back
    <<jump Actions_Before_Party>>
===

title: Guitar_Training
tags: 
position: -1082,-816
---
Pas de soirée qui se respecte sans un guitariste amateur grattant les accords de WanderHall... Vous, vous préférez le bon vieux néo-shredding, ou encore le galactic jazz. Dans tous les cas, vous vous entraînez un peu pour amuser la galerie tout à l'heure.
<< visit Guitar_Training >>
<<jump Actions_Before_Party>>
===

title: Danse_Training
tags: 
position: -849,-789
---
Vous allumez votre stéréo uni-directionnelle pour pratiquer vos meilleurs pas de danse sans déranger vos voisins. C'est sûr que vous allez impressionner tout le monde en vous déhanchant comme ça !
<< visit Danse_Training >>
<<jump Actions_Before_Party>>
===

title: Star_Mars_Incursion
tags: 
position: -1349,-851
---
<< show lightsaber@holo >>
<< loopFX TVStarwars_LOOP 10000 >>
Allez, un petit épisode pour se calmer les nerfs avant de partir se défouler. Pas de nouvel épisode aujourd'hui, mais vous en profitez pour revisiter un classique : sur la planète Métopsia, nos héros doivent déjouer les plans du Collectionneur, une créature polymorphe désirant absorber l'ADN de tous les individus.
L'histoire est si passionnante que vous restez jusqu'à la fin de l'épisode, le "cliffhanger", même si vous l'avez déjà vu : le méchant était en réalité une créature bienveillante, désirant empêcher une incursion extra-terrestre. Quel suspense !
<< visit Star_Mars_Incursion >>
<< stopFX TVStarwars_LOOP >>
<< hide >>
<<jump Actions_Before_Party>>
===

title: Porte_S
tags: subchoice
position: -1102,-281
---
-> Retrouver Fred (passe le temps jusqu'à 21h30)
    <<jump Debut_Soiree>>
-> back
    <<jump Actions_Before_Party>>
===

title: Debut_Soiree
tags: 
position: -939,-153
---
<< setBackground party >>
<< music DanseQuiet >>
<< fadeOut >>
<<set $partyPoints to 0>>
Fred_excited: Yo, te voilà enfin ! Alors, pas mal, cette fête, non ?
You: Carrément ! Alors, on le commence, ce défi ?
Fred_excited: Ouais, mais avant ça, il faut trouver notre adversaire, BB-Rex !
You: Et il est où, ce... BB-Rex ?
Fred_happy: Euh... Ca, je sais pas vraiment... Je l'ai jamais vu avant. Mais il paraît que c'est le plus grand fêtard de l'université, alors il devrait pas être difficile à trouver !
You: Euh... Ok... On fait quoi en attendant ?
Fred_smiling: En attendant, allons nous chercher de quoi nous remplir le gosier ! Tiens, un BarBot !
BarBot: Qu'est-ce que je peux vous offrir, les losers ?
Fred_smiling: Yo BarBot, mets-nous deux verres de...
Fred_surprised: Attends... Comment tu viens de m'appeler ?
BarBot: Ben quoi, loser, y a quelque chose qui va pas dans ton programme de décodage, ou quoi ? Allez, dégage d'ici et laisse-moi servir.
You: Euh... Ils sont censés être aussi violents ?
Barbot: Non, seulement moi. C'est moi qui écrit les règles ici, amigo.
Fred_excited: OH !! JE SAIS !! C'est pas un Barbot !! C'EST LUI, BB-REX !!
You: Quoi ??
Barbot: Et ouais, amigo, en fer et en os.
Fred_excited: Le vieux robot de la cafétéria ! Les docteurs se sont amusés à le reprogrammer pour le transformer en caïd !
You: C'est... lui, notre adversaire ?
Barbot: Et comment... Bon, les losers, pour l'instant, vous avez les poches vides, hein ? Vous feriez-mieux de vous y mettre si vous voulez amasser les 4 Party Points et pouvoir m'affronter en duel !
You: De quoi il parle, là ?
Fred_excited: A chaque fois qu'on arrive à impressionner BB, il nous donne un point ! Quand on en a récupéré 4, on peut l'affronter pour voir qui obtiendra le titre de champion de la fête !
Barbot: T'oublies un détail, amigo. Personne a réussi à battre BB-Rex. En tout cas, aucun humain. Tout simplement car BB-Rex est le plus grand teufeur de l'histoire. Allez, au boulot les losers !
Fred_surprised: T'as entendu le tas de ferraille, { $name }, il faut qu'on se fasse remarquer ! Alors, on fait quoi ?

<<jump Actions_Party>>
===

title: Actions_Party
tags:
position: -433,-420
---
<< hide >>
<< music Danse >>
<<if $partyPoints < 4>>
-> Hall de la cafétéria
    <<jump Hall_S>>
-> Bar
    <<jump Bar_S>>
-> Canapé
    <<jump Sofa_S>>
-> Porte
    <<jump Porte_Fete_S>>
<<else>>
-> Confronter BB-Rex
    <<jump RapBattle>>
<<endif>>
===

title: RapBattle
tags:
position: -170,-251
---
Fred_excited: Yo, BB-Rex ! On a obtenu 4 Party Points, alors maintenant on accepte ton défi !... Quel qu'il soit, d'ailleurs.
BarBot: Ah ouais... On m'avait dit que vous étiez costauds pour des premières années... Bien, si vous y tenez... C'est l'heure...
BarBot: ...de la destruction.
BarBot: ...de l'annihilation.
BarBot: ...de...
BarBot: ...la bataille de rap !!
Fred_surprised: Quoi ?! Mais j'ai jamais fait ça, moi ?
BarBot: C'est parti...
BarBot: Mon flow est titanesque, un peu comme ma cuirasse...
BarBot: Avant j'étais Barbot, maintenant j'suis le ClashBot qui tabasse !
Fred_surprised: Mince, il est trop fort... Ok, je me lance... Suis ma rime, { $name } !
Fred_excited: Nous on est étudiants, on a pas beaucoup d'argent...

-> Mais quand on rappe on a des punchlines d'argent !
    <<jump Bad_Rap1>>
-> Et pourtant nos rimes sont plus dures que du diamant !
    <<jump Good_Rap1>>
-> Prendre le contrôle du BarBot <<if visited("PiratedPerfect")>>
    <<jump Control_BarBot>>
===

title: Good_Rap1
tags:
position: -174,-126
---
You: Et pourtant nos rimes sont plus dures que du diamant !
BarBot: Mmmh... Peut-être, mais pour des premières années...
BarBot: Je suis étonné... que vous ayiez appris à rapper !
<< visit Good_Rap >>

-> On a beau être jeune, toi tu n'es pas de taille...
    <<jump Good_Rap2>>
-> Toi t'es qu'un robot, tes rimes sont tellement pauvres...
    <<jump Bad_Rap2>>
-> Prendre le contrôle du BarBot <<if visited ("PiratedPerfect")>>
    <<jump Control_BarBot>>
===

title: Bad_Rap1
tags:
position: 44,-190
---
You: Mais quand on rappe on a des punchlines d'argent !
BarBot: Pfff... Sérieusement ? Faire rimer Argent et Argent...
BarBot: Venant de la part d'enfants, ça n'est pas très étonnant !

-> On a beau être jeune, toi tu n'es pas de taille...
    <<jump Good_Rap2>>
-> Toi t'es qu'un robot, tes rimes sont tellement pauvres...
    <<jump Bad_Rap2>>
-> Prendre le contrôle du BarBot <<if visited ("PiratedPerfect")>>
    <<jump Control_BarBot>>
===

title: Good_Rap2
tags:
position: 153,-54
---
You: On a beau être jeune, toi tu n'es pas de taille...
Fred_excited: Alors mords la poussière, espèce de tas de ferraille !
<< visit Good_Rap >>
<<jump End_Rap>>
===

title: Bad_Rap2
tags:
position: -89,-10
---
You: Toi t'es qu'un robot, tes rimes sont tellement pauvres...
Fred_surprised: Euh... En plus t'es... T'as la tête... Tu...
Fred_angry: Sérieusement, { $name }, t'as pas des rimes encore plus difficiles ?!

<<jump End_Rap>>
===

title: Control_BarBot
tags:
position: -323,32
---
BarBot: Désactivation du protocole "rappeur"...
BarBot: Je... Erreur... Vous voulez grignoter quelque chose ?
You: Euh... Yo, Fred je crois que BB-Rex a un problème...
Fred_excited: Même pour un robot, c'est bizarre, il est tout blême !
BarBot: Hahaha, bien joué, le piratage...
<< visit Good_Rap >>
<<jump End_Rap>>
===

title: End_Rap
tags:
position: -36,90
---
BarBot: Bon, les amis, on va arrêter là, je crois...
BarBot: Maintenant, je vous demande de quitter la salle...
<< visit End_Rap >>
<<if visited("Good_Rap")>>
    <<jump WinCompetition>>
<<else>>
    <<jump LoseCompetition>>
<<endif>>
===

title: WinCompetition
tags:
position: 173,158
---
BarBot: ...Avec le titre de champions incontestés de la fête !!!
Tous les élèves vous applaudissent à l'unisson. Vous êtes les véritables rois de la fête.
Fred_smiling: On a gagné !! On est les premiers à avoir battu BB-Rex !
Fred_excited: C'est grâce à toi, { $name } ! Allez, on fête ça ?
You: Je suis un peu fatigué, là...
Fred_happy: Tu as raison, on ferait mieux de rentrer. Demain, je dois t'aider avec ton drone ! A plus, tout le monde !
===

title: LoseCompetition
tags:
position: -73,158
---
Barbot: ...Et de plus ramener vos faces de losers !
Tous les étudiants vous regardent d'un air déçu, puis reviennent à leurs occupations.
Fred_sad: Mince... On a pas réussi... Mais au moins on s'est bien amusés. Merci d'être resté jusqu'au bout, { $name }.
You: Je suis un peu fatigué, là
Fred_happy: Tu as raison, on ferait mieux de rentrer. Demain, je dois t'aider avec ton drone ! A plus, tout le monde !
<<jump TransitionMatin>>
===

title: TransitionMatin
tags:
position: -73,158
---
<< fadeIn >>
<< setBackground bedroom night >>
<< fadeOut >>
A peine rentré, vous vous effondrez dans votre lit, songeant à la journée suivante...
<<if visited("End_Rap")>>
    <<jump MatinWin>>
<<else>>
    <<jump MatinLose>>
<<endif>>
===

title: MatinWin
tags:
position: -73,158
---
<< fadeIn >>
<< setBackground bedroom >>
<< fadeOut >>
Bip !! Vous êtes réveillé par votre Palmphone. C'est Fred qui appelle. Il veut sûrement aider pour le TP de drones !
Fred@holo_excited: Yo, { $name } ! Bien dormi ? Pas moi ! Je t'ai envoyé les plans de mon drone, t'as vu, je tiens mes promesses !
You: Merci, Fred !
Fred@holo_happy: T'inquiète, c'est pour être resté jusqu'au bout hier soir ! Alors... Pour ce qui est de l'allègement des matériaux, j'ai penché pour une coque en méta-plastique...
Fred et vous prenez 1h pour travailler sur le drone. Au final, celui-ci semble fin prêt pour le TP de pilotage ! Va-t-il tenir jusqu'au bout ?
-> Aller au TP
    <<jump TpWin>>
===

title: MatinLose
tags:
position: -73,158
---
<< fadeIn >>
<< setBackground bedroom >>
<< fadeOut >>
Bip !! Votre réveil sonne en vous faisant bourdonner les oreilles.
Vous avez bien dormi, trop dormi ! Il ne vous reste qu'une heure pour améliorer votre drone.
Fred@holo_happy: Yo, { $name } ! Bien dormi ? Pas moi ! Je t'ai envoyé les plans de mon drone, t'as vu, je tiens mes promesses !
You: Merci, Fred ! T'étais pas obligé, tu sais...
Fred@holo_sarcasme: Je sais, et crois pas que je vais passer 1h à t'expliquer comment ça marche alors que t'es parti super tôt hier !
Au final, vous parvenez à faire des améliorations légères sur votre drone, mais sans les explications de Fred, le résultat n'est pas fameux...
<< addToGauge learning 10 >>
-> Aller au TP
    <<jump TpLose>>
===

title: Hall_S
tags: subchoice
position: 335,-307
---
-> Jouer un solo de guitare <<if !visited("Guitar_Solo")>>
    <<jump Solo_Guitare>>
-> Se déhancher sur la piste de danse <<if !visited("Danse")>>
    <<jump Danse>>
-> back
    <<jump Actions_Party>>
===

title: Solo_Guitare
tags:
position: 470,-242
---
Vous vous approchez de l'instrument, une cyber-guitare améliorée par un groupe de docteurs. Celle-ci est posée contre un énorme amplificateur d'où sortent une ribambelle de cables et de pédales d'effets bricolées maison.
Vous allumez l'amplificateur et grattez la première corde. Un son puissant et dissonnant jaillit du caisson d'amplificateur. Vous allez devoir apprendre à dompter la bête.
Le temps d'accorder l'instrument et de le prendre en main, un petit groupe s'est déjà amassé autour de vous, prêt à entendre vos mélodies.
Le robot BarBot modifié, BB-Rex, vous entend et coupe sa sono intégrée pour vous écouter.
BarBot: Yo la rockstar, on va voir si ton groove est à la hauteur de BB-Rex !
<< visit Guitar_Solo >>

<<if !visited("Guitar_Training")>>
    <<jump Solo_Bad>>
<<else>>
    <<jump Solo_Good>>
<<endif>>
===

title: Solo_Bad
tags:
position: 585,-117
---
Stressé par la tension et la foule, vous entamez votre solo. Vous aimeriez avoir un peu révisé avant de venir, mais c'est trop tard désormais.
Heureusement, Fred est là pour vous accompagner. Muni de sa Rythm-Box portable M47, il vous crée un beat du tonnerre.
Vous parvenez tout de même à tenir une belle performance, qui ne laisse pas les étudiants indifférents.
BarBot: Bon, ça, c'est ce que j'aime voir ! Bon, c'était un peu basique pour BB-Rex, mais vous avez réussi à ravir le public. Allez, pour la peine, voilà 1 PARTY POINT !
Le barbot vous lance un gâteau apéritif teinté d'or que Fred attrape en sautant.
Fred_excited: Génial !
<< music Solo1 >>
<<set $partyPoints to $partyPoints+1>>
<<jump Actions_Party>>
===

title: Solo_Good
tags:
position: 719,-183
---
Confiant de vos compétences et fier d'avoir mené le groupe de Galactic Jazz au temps du lycée, vous entamez un shredding digne des plus grands.
Heureusement, Fred est là pour vous accompagner. Muni de sa Rythm-Box portable M47, il vous crée un beat du tonnerre.
Les cordes rugissent au rythme de vos ongles, taillés pour l'occasion, se tendent et s'étouffent pour laisser la distortion s'exprimer.
Le public est en extase. Vous êtes parvenus à créer un spectacle digne de ce nom.
BarBot: Ok, je vous laisse ma couronne, les amis, vous l'avez mérité. J'ai pas entendu de licks pareils depuis les seventies !
Fred_surprised: Qu'est-ce qu'il raconte ?
Le barbot éjecte 2 gâteaux apéritifs teintés d'or que vous et Fred attrapez au vol.
Fred_excited: Deux Party Points ?? Trop bien ! On avance deux fois plus vite !
<< music Solo2 >>
<<set $partyPoints to $partyPoints+2>>
<<jump Actions_Party>>
===

title: Danse
tags:
position: 534,-351
---
Cette fois, vous vous dirigez au milieu de la piste de danse. Vous commencez par vous échauffer doucement en vous laissant guider par le beat de la chanson. Puis, quand le morceau suivant débute, là, vous enclenchez la vitesse supérieure et vous libérez toute l'énergie et le rythme qui bat en vous. #line:0ff5e35 
BarBot: Alors, l'ami, on groove ? On va voir comment tu te débrouilles en dehors de ta zone de confort. MODE DISCO ACTIVE !
<< music JungleBodyBeat >>
Un rythme endiablé tout droit sorti d'une époque bien révolue vous prend aux tripes.
<< visit Danse >>

<<if !visited("Danse_Training")>>
    <<jump Danse_Bad>>
<<else>>
    <<jump Danse_Good>>
<<endif>>
===

title: Danse_Good
tags:
position: 679,-392
---
C'est comme si vous vous étiez calé sur l'orbite de la Planète Disco. Vous vous trémoussez, vous vous déhanchez, vous sautez, vous virevoltez, vous tournoyez comme un globe scintillant ou une boule disco radieuse ! #line:01c4444 
Vous vous retournez pour constater que Fred vous a rejoint sur la piste de danse !
Les danseurs s'écartent et vous abandonnent la piste ! Tous les spots et les regards sont fixés sur vous. Vous voilà au centre de l'attention. Au lieu de vous refroidir, cela vous emplit d'énergie et d'enthousiasme. Vous donnez libre cours à votre inspiration, vous entrez en résonance avec le rythme et la joie du public ! #line:03f5ea5 
Puis, vous estimez qu'il est temps de faire une pause. Vous décrochez progressivement de l'orbite disco, vous saluez trois fois la foule avant de quitter la piste de danse. Votre sortie est suivie par un tonnerre d'applaudissements ! #line:0fa106d 
Barbot: Wow, je vois que vous êtes plus que familiers avec le monde magique de la disco ! C'était puissant, les amis. Voilà 2 PARTY POINTS pour la peine !
Le barbot éjecte 2 gâteaux apéritifs teintés d'or que vous et Fred attrapez au vol.
Fred_excited: Deux Party Points ?? Trop bien ! On avance deux fois plus vite !

<<set $partyPoints to $partyPoints+2>>
<<jump Actions_Party>>
===

title: Danse_Bad
tags:
position: 711,-282
---
Vous vous déhanchez en vous donnant un maximum, tentant de vous rappeler de vos leçons de Jungle Body Beat pour l'appliquer à l'ambiance disco.
Vous aimeriez avoir eu le temps de vous entraîner, mais heureusement Fred est là pour vous guider et vous faire groover un maximum !
BarBot: Ok, ça suffit. Vous êtes bons, les gars, mais vous pourriez être encore meilleurs. Allez, un Party Point est de mise !
Le barbot vous lance un gâteau apéritif teinté d'or que Fred attrape en sautant.

<<set $partyPoints to $partyPoints+1>>
<<jump Actions_Party>>
===

title: Bar_S
tags: subchoice
position: -105,-630
---
-> Grignoter un snack <<if !visited("Grignoter")>>
    <<jump Grignoter>>
-> Se rafraîchir au bar
    <<jump Drink>>
-> Tenter de pirater BB-Rex <<if !visited("Pirater")>>
    <<jump Pirater>>
-> Modifier le comportement de BB-Rex <<if visited("Pirater") && !visited("PiratedPerfect") && visited("Drone_Review")>>
    <<jump Piratage_Good>>
-> back
    <<jump Actions_Party>>
===

title: Grignoter
tags:
position: -163,-939
---
BarBot: Alors, amigo, on a déjà le ventre creux ? Besoin d'un petit casse-croûte avant de revenir affronter BB-Rex ?
You: Euh... Ouais. T'as quoi au menu ?
BarBot: Que de la bombe, frérot ! Tiens, goûte ça.
You: Y a rien d'illégal là-dedans, pas vrai ?
BarBot: Bahahaha, bien sûr que non ! On va pas gaspiller les bonnes choses sur les premières années, p'tit génie !
Vous croquez dans le biscuit apéritif. Ce n'est ni sain, ni très bon, mais ça devrait vous faire tenir le reste de la soirée.
<< visit Grignoter >>
<< addToGauge food 30 >>
<<jump Actions_Party>>
===

title: Drink
tags: subchoice
position: 128,-897
---
Barbot: Alors, on commence enfin à se décoincer, je vois ? Allez, un petit cocktail, ça te mettra bien.

-> Mets-moi un truc bien corsé <<if !visited("Alcohol")>>
    <<jump Drink_Alcohol>>
-> D'accord, mais sans alcool <<if !visited("Soft")>>
    <<jump Drink_Soft>>
-> back
    <<jump Actions_Party>>
===

title: Drink_Alcohol
tags: 
position: 128,-897
---
Barbot: C'est ce qe je veux entendre. Allez, bois-moi ça comme un vrai homme.
BB-Rex vous sert un grand verre contenant un liquide semi-translucide. A l'intérieur, les couleurs virevoltent et se croisent pour former des mélanges.
Vous avalez le cocktail d'un coup. Vous sentez votre gorge frémir et vous manquez de tout rejetez, tant le mélange est corsé.
Barbot: Haha ! C'est ce que j'aime !! Allez, pour la bravoure, voilà 1 Party Point !
Désinhibé mais nauséeux, vous ne savez pas si vous avez eu une bonne idée de boire ce cocktail. Ce qui est sûr, c'est qu'il vous a donné un coup au système.
<< removeFromGauge sleep 20 >>
<< removeFromGauge food 20 >>

<< visit Alcohol >>

<<jump Actions_Party>>
===

title: Drink_Soft
tags: 
position: 128,-897
---
BarBot: C'est bien ce que je pensais... Allez, voilà un truc tout soft, à ton image... Haha !
Vous vous sentez revigoré par la boisson caféinée. Maintenant, votre coeur bat à fond. Pas idéal pour faire une sieste, mais au moins, vous pourrez tenir un peu.
<< visit Soft >>
<<jump Actions_Party>>
===

title: Pirater
tags: 
position: 190,-589
---
Vous contemplez BB-Rex, le Barbot reprogrammé par les docteurs pour devenir le roi de la fête. Hey, si ce robot a déjà été piraté une fois, vous pourriez tenter de pénétrer son système, vous aussi !
Vous vous glissez derrière l'androïde et connectez votre tablette portable sur son boîtier principal.
BINGO ! Devant vous, une interface de code vous montre les comportements basiques du robot.
-> LIBERER_PARTY_POINT
    <<jump Reward_Piratage>>
===

title: Reward_Piratage
tags: 
position: 190,-589
---
Vous lancez la fonction et une trappe s'ouvre sur la tête du robot. Un biscuit doré en jaillit ! Un Party Point !
<<set $partyPoints to $partyPoints+1>>
Peut-être que vous pourriez influencer encore plus le comportement du droïde...
Vous remarquez que son architecture de conscience est similaire à un système de pilotage. Le robot n'est pas reprogrammé... Il est piloté !!
<< visit Pirater >>
<<if !visited("Drone_Review")>>
    <<jump Piratage_Bad>>
<<else>>
    <<jump Piratage_Good>>
<<endif>>

<<jump Actions_Party>>
===

title: Piratage_Bad
tags: 
position: 434,-558
---
Si seulement vous aviez compris les rouages des Systèmes d'Assistance au Pilotage de drone ! Les plans de Fred vous auraient bien aidé...
BarBot: Hé ! Pas cool, ça, amigo. On essaye pas de pirater BB-Rex, ok ?! J'ai passé... Euh, je veux dire, les étudiants qui m'ont reprogrammé ont passé beaucoup de temps à le faire !
You: Euh... Désolé...

<<jump Actions_Party>>
===

title: Piratage_Good
tags: 
position: 369,-742
---
Vous utilisez vos connaissances fraîchement acquises en Systèmes d'Assistance au Pilotage pour prendre le contrôle du droïde ! Désormais, vous pourrez activer son contrôle à distance à l'aide de votre tablette.
Fred_surprised: T'as fait QUOI ??
Fred_excited: TROP BIEN !! Passe-moi les commandes !
Barbot: Yo !! Fred est vraiment la meilleure personne que je connaisse, je l'adore ! Tiens, un PARTY POINT, rien que pour toi !!
Fred_excited: Hahahaha !
<<set $partyPoints to $partyPoints+1>>

<< visit PiratedPerfect >>

<<jump Actions_Party>>
===

title: Sofa_S
tags: subchoice
position: -604,-48
---
-> Se reposer sur le canapé
    <<jump Repos_Fete>>
-> Demander à Fred d'expliquer le Système d'Assistance au Pilotage <<if !visited("Drone_Review")>>
    <<jump Ask_Fred_Drone>>
-> back
    <<jump Actions_Party>>
===

title: Ask_Fred_Drone
tags:
position: -787,56
---
Vous invitez Fred à s'installer sur le canapé, proche de vous.
You: Dis, Fred, tu pourrais m'expliquer ton système de pilotage assisté pour le TP de demain ?
Fred_surprised: Là, maintenant ?! On est dans le coeur de la fête !
You: Je serais un peu moins stressé pour demain si je savais comment ça marchait...
Fred_smiling: Bon, ok... Je vais faire vite, alors. Tu vois, les systèmes de correction de trajectoire par advanced learning...
Fred semble, au final, plutôt passionné par le sujet, et vous en ressortez avec quelques notions, mais rien qui puisse changer la donne pour demain...
Vous vous demandez à quoi cela pourrait bien servir dans le contexte actuel...
<< visit Drone_Review >>
<< addToGauge learning 20>>
<<jump Actions_Party>>
===

title: Repos_Fete
tags:
position: -625,119
---
Vous vous allongez sur le canapé de la cafétéria. Le tissu en nanofibre se laisse facilement détendre sous votre poids et vous vous endormez vite malgré la musique et la fête alentours.
<< addToGauge sleep 40 >>
Une fois révéillé, vous vous sentez revigoré, d'attaque.
<<jump Actions_Party>>
===

title: Porte_Fete_S
tags: subchoice
position: 188,-445
---
-> Rentrer chez soi avant d'être trop fatigué
    <<jump Rentrer>>
-> back
    <<jump Actions_Party>>
===

title: Rentrer
tags:
position: 188,-445
---
Vous vous dirigez vers l'entrée, épuisé et priant de ne pas être trop épuisé pour demain. Vous êtes en train de reprendre vos affaires quand les
===

title: Tp_Win
tags: 
position: 375.0788269042969,1111.1724853515625
---
<< fadeIn >>
<< setBackground door class >>
<< setTime 9:00 >>
<< fadeOut >>

Vous retrouvez votre ami Fred, qui vous attend avec le sourire. #line:0bd159f 

Fred_excited: Hey, tu avais la fièvre hier soir ! Je suis content que tu sois resté aussi longtemps... #line:028f8a8 
You: Ouais... #line:0ddab6e 
Fred_smiling: Je vois ça. J'espère que tu as bien dormi. #line:0bf6f69 
You: Ouais. #line:083d267 
Fred_excited: Moi aussi. On se revoit tout à l'heure. #line:00d7d95 
<< hide >>

<< fadeIn >>
<< setBackground class >>
<< fadeOut >>

Vous vous rendez à votre poste. Vous vous calez sur votre siège et prenez connaissance des instructions qui défilent sur l'écran. Vous savez comment piloter un drone d'ingénierie. Vous êtes même doué à ce jeu. #line:0086d5b 
Votre drone est complet et répond parfaitement aux commandes. À part une erreur ou deux, vous guidez votre appareil à bon port. #line:05f29c8 
Puis, vous sortez de la salle de classe. #line:09197a3 

<<jump Sortir>>
===

title: Sortir
tags: 
position: 591.0426635742188,1112.3233642578125
---

<< fadeIn >>
<< setBackground door class >>
<< fadeOut >>

Dehors, vous rejoignez Fred. #line:075dcd1 
Fred_smiling: C'était easy, non? #line:072f2fb 
You: Ouais. #line:0b8eac9 
Fred_happy: Je pense avoir cartonné. Et toi ? #line:05c06e6 
You: Ça devrait aller. #line:0b67da4 

C'est à ce moment que les résultats tombent.  #line:0c34794 
<< setBackground door class-screen >>
<< fx Notification >>
Fred décroche un <b>A</b> et vous un <b>B</b> #line:05a2fca 
<< fx Success >>
<< music Victory >>
Fred_smiling: Hey ! Attention, hein ! Tu serais pas en train de vouloir me dépasser ?... #line:0981b12 
You: Bien entendu. #line:0187a34 
Fred_smiling: Je t'adore, tu as toujours la phrase qui tue. Allez, on va manger un petit truc et on file au cinoche. On est libre maintenant jusqu'à demain. #line:09dcf0b 
You: Yes. #line:0b3e43f 
Fred_smiling: Yes yes! #line:0f08c3f 
<< advanceTime 0:15 >>
===

title: Replay
tags:
position: -1624.1019287109375,775.4595947265625
---
<< fadeIn >>
<< setBackground circle >>
<< fadeOut >>
Nonononoon... Vous n'avez pas bien écouté les leçons des 4 Sages de la Métacognition. #line:07bd3dd 
Vous devez respecter votre cycle de sommeil et d'alimentation ! #line:0ebe6d4 

Vous allez devoir retourner à cette soirée festive... #line:0e44199 

-> Retourner à la fête #line:05385cd 
    << resetLevel >>
    <<jump Start>>
===

