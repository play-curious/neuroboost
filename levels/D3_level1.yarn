# gauges: learning sleep food mentalLoad

title: Start
tags:
colorID: 0
position: -941,-697
---
<< fadeIn 1 >>
<< setGauge learning 0 >>
<< setGauge sleep 100 >>
<< setGauge food 100 >>
<< setGauge mentalLoad 0 >>
<< showGauges >>
<< setBackground bedroom >>
<< setTime 8:00 2 >>
<< fadeOut >>
<< loopFX Chime_LOOP 2000 >>
Bip ! Bip ! Il est 8 heures vous rappelle votre horloge connectée. 
<< stopFX Chime_LOOP >>
<< music Neutral >>

Comme tous les matins, vous vous réveillez dans votre chambre. Mercredi, c'est jour d'autonomie à l'Académie d'Omniscience et de Polytechnologie Universelle. Attention, ça ne veut pas dire jour de repos !

<< loopFX PhoneRing_LOOP 5000 >>
Ronronron... Votre palmphone qui vibre. Sans suprise, vous voyez que l'appel vient de Fred.

-> Prendre l'appel
    <<jump Appel_Fred>>
===
title: Appel_Fred
tags:
colorID: 0
position: -631,-697
---
<< stopFX PhoneRing_LOOP >>

Fred@holo_excited: Hello ! Je te dérange pas ?
You: Naaan...
Fred@holo_smiling: Tu fais quoi là, maintenant ?
You: Je finis mon petit déjeuner.
Fred@holo_smiling: Yes yes. Cool. Tu viens avec moi à la salle de gym ?
You: Naan... Je dois travailler en autonomie.
Fred@holo_smiling: Tu veux parler de la note de synthèse sur la PNLIAA ?
You: Ouais. La Programmation Neuro Linguistique des Intelligences Artifielles Analogiques.
Fred@holo_excited: No worry. On a juste qu'à minuit pour la déposer sur le FTP de l'Académie.
You: C'est vrai. C'est assez touffu ce truc de PNLIAA.
Fred@holo_smiling: Pfuuu. Tu prends trop les choses au sérieux. Alors tu viens ?

<< hide >>
<< advanceTime 1:00 >>

-> Rejoindre Fred
    <<jump Salle_Gym>>
-> Rester travailler
    <<jump Liste_Taches>>
===
title: Salle_Gym
tags:
colorID: 0
position: -23,-776
---
<< fadeIn >>
<< setBackground sport >>
<< advanceTime 0:20 >>
<< fadeOut >>

Vous retrouvez Fred à la salle de gym. Il sourit quand il vous voit.
Fred_excited: Je savais que tu allais venir.
You: La preuve.
Fred_smiling: Yes. Après notre séance de gym, tu vas voir, tu vas torcher cette note de synthèse !
You: On verra, je l'espère.
Fred_excited: No worry ! Allez viens !
<< hide >>

<< loopFX Sports_LOOP 16000 >>

Vous emboîtez le pas de Fred et vous éclatez tous les deux aux différentes activités proposées: cybersprint, electroskate, eBall, eJump...

Le moins que l'on puisse dire, c'est que vous y avez mis du vôtre. Au final, vous vous sentez épuisé. Il est temps de retourner travailler.

<< stopFX Sports_LOOP >>

<< advanceTime 2:00 >>
<< removeFromGauge sleep 30 >>
<< removeFromGauge food 30 >>
<< addToGauge mentalLoad 30 >>

Fred_smiling: Tu pars déjà, lâcheur ?
You: J'en peux plus.
Fred_sad: Tant pis.
<< hide >>

-> Retourner dans votre chambre
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Liste_Taches
tags:
colorID: 1
position: -328,-637
---
<< setBackground bedroom >>
<< fadeOut >>

<<if !visited("Liste_Taches")>>
Que faites-vous maintenant ?
<<endif>>

<< if getGauge("mentalLoad") >= 100 >>
-> Faire un somme
    <<jump Surcharge_Cognitive>>
<< endif >>

<< if !isTimeOver("14:00") >>
<< jump Liste_Taches_AM >>
<< endif >>

<< jump Liste_Taches_PM >>

===
title: Liste_Taches_AM
tags: freechoice
colorID: 1
position: -328,-637
---
-> Lit@lit
    <<jump lit_subchoice_am>>
-> Bureau@bureau
    <<jump bureau_subchoice_am>>
-> Salon@salon
    <<jump salon_subchoice>>
===
title: lit_subchoice_am
tags: subchoice
---
<< fadeIn >>
<< setBackground bedroom bed >>
<< fadeOut >>

-> Faire une sieste#once
    <<jump Sleep>>
-> back
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: bureau_subchoice_am
tags: subchoice
---
<< fadeIn >>
<< setBackground bedroom desk >>
<< fadeOut >>

-> Préparer son bureau avant de se mettre à travailler#once
    <<jump Ranger>>
-> Ouvrir le chat histoire de se détendre#once
    <<jump Chat>>
-> Débuter la rédaction de la note de synthèse#once
    <<jump Redaction_Note>>
-> back
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: salon_subchoice
tags: subchoice
---
<< fadeIn >>
<< setBackground bedroom chill >>
<< fadeOut >>

-> Binger votre série TV préférée#once
    <<jump BingeTV>>
-> back
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Liste_Taches_PM
tags: freechoice
colorID: 1
position: -328,-637
---
-> Lit@lit
    <<jump lit_subchoice_pm>>
-> Bureau@bureau
    <<jump bureau_subchoice_pm>>
-> Salon@salon
    <<jump salon_subchoice>>
-> Cuisine@cuisine
    <<jump cuisine_subchoice_pm>>
===
title: lit_subchoice_pm
tags: subchoice
---
<< fadeIn >>
<< setBackground bedroom bed >>
<< fadeOut >>

-> Faire une sieste#once
    <<jump Sleep>>
-> back
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: bureau_subchoice_pm
tags: subchoice
---
<< fadeIn >>
<< setBackground bedroom desk >>
<< fadeOut >>

-> Se détendre sur le chat#once
    <<jump Chat>>
-> Continuer la rédaction de la note de synthèse#once
    <<jump Redaction_Note>>
-> back
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: cuisine_subchoice_pm
tags: subchoice
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>

-> Manger#once
    <<jump Diner>>
-> back
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Ranger
tags:
colorID: 0
position: -958,-443
---
Vous débarrassez votre bureau de tous les objets inutiles et encombrants. Vous étalez ensuite votre le paper print de votre brief, les documents de référence que vous avez déjà en possession.
Avant de commencer à vous jeter dans la gueule du loup, vous relisez bien le brief et les notes prises sur le document. Avant de commencer la rédaction de votre note, vous prenez le temps d'établir un plan de réflexion et de travail. Certes, cela vous prend du temps, mais ça va vous faciliter le processus de rédaction.
<< advanceTime 0:20 >>
<< removeFromGauge mentalLoad 10 >>

-> Il ne reste plus qu'à s'y mettre maintenant
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Chat
tags:
colorID: 0
position: -388,-306
---
<< fx Notification >>
Vous cliquez sur l'icône ChatApp de votre Palmphone. Il y a encore plus de monde que lundi ! 
<< fx Notification >>
Vous êtes pingé par Fred ! Il vous demande si vous ne voulez pas le rejoindre à la salle de gym !
<< fx Notification >>
Vous lui répondez bien entendu non, mais il vous provoque en duel sur AlphaMégaRing, l'arène virtuelle du Métaverse de la fraternité étudiante de l'Académie. Vous ne pouvez ignorer son injonction !
Toute la communauté va apprendre que vous avez refusé un duel virtuel, que vous êtes un lâche...
Vexé et décidé à régler l'affaire au plus vite, vous vous connectez au site d'AlphaMégaRing ! Vous êtes meilleur que Fred à ce petit jeu. Vous le savez, il le sait.
Cependant, Fred se montre un adversaire particulièrement retord et surprenant. Le combat s'avère plus long et difficile que prévu. Heureusement, c'est vous qui finissez encore par l'emporter.
<< advanceTime 2:00 >>
<< addToGauge mentalLoad 30 >>

-> Et maintenant, il faut peut-être travailler sur cette note de synthèse ?
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: BingeTV
tags:
colorID: 0
position: 326,-411
---
Vous lancez votre application Screencast, l'écran d'accueil de Webflix s'affiche aussitôt sur le mur, vous désignez votre série culte : Star Mars - The Lone Lightsaber.
<< loopFX TVStarwars_LOOP 10000 >>
Le générique démarre en déroulant le récapitulatif des épisodes précédents. L'infâme Jaba Fett, emprisonné par Yann Salto, a été libéré de la planète Jailhouse par les Dark Ops, les commandos de choc de l'Impérium Noir !
Ils sont parvenu à décryogéniser Jaba Fett. Ils en besoin car lui seul sait où se cache Dude Highwalker, le Chevalier Laser... Sur Dunooine, la planète de Sable, la seule réserve de de Mana de toute la galaxie !
Quand les commandos Dark débarquent sur Dunooine, vous avez les yeux scotchés à l'écran ! Vont-ils parvenir à arrêter Dude ? L'empêcher de libérer la galaxie du joug de l'Imperium Noir ?
Vous tremblez à chaque fois que les Dark Ops débusquent la cache de Dude. Heureusement que ses disciples: les Freewalkers, parviennent à retenir les commandos le temps que le Chevalier Laser atteigne une nouvelle planque..
<< stopFX TVStarwars_LOOP >>
<< advanceTime 3:00 >>
<< addToGauge food 30>>
<< addToGauge mentalLoad 40>>

Le temps file... Meuha... Il faudrait peut-être terminer cette fichue note de synthèse...

-> Vite, au travail !
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Sleep
tags:
colorID: 0
position: -706,-331
---
Vous vous rappelez ce que vous ont dit les 4 Sages. Dormir régulièrement, tous les jours. Toutefois, si le temps manque, un petit somme, ça fait du bien. En effet, vous vous allongez sur votre lit, vous fermez les yeux.

<< loopFX Sleep_LOOP 5000 >>
Vous sentez que vous glissez rapidement dans un cocon chaud et doux, un sieste méritée et réparatrice. 
<< stopFX Sleep >>

Quand vous ouvrez les yeux, vous vous sentez de nouveau frais et joyeux.

<< removeFromGauge sleep 20 >>
<< removeFromGauge mentalLoad 10 >>

-> Maintenant, il faut s'y remettre
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Redaction_Note
tags:
colorID: 0
position: -22,-299
---
<< if visited("Redaction_Note") >>
Vous vous retroussez les manches et vous attelez à la tâche. Mettre au propre cette note de synthèse, fruit de plusieurs jours de lecture, de prises de notes, de réflexions diverses et variées.
Vous avez tellement de choses à dire et à coucher sur votre document, vous ne manquez pas d'idées, mais de concentration et productivité.
<< addToGauge learning 30 >>
<< else >>
Allez, encore une fois, vous respirez un grand coup avant de replonger dans le grand bain. Vous êtes loin d'avoir fini et les heures ne cessent de filer.
Il faut terminer cette note de synthèse coûte que coûte avant la deadline...

<< addToGauge learning 20 >>

<<endif>>

<< advanceTime 1:00 >>
<< loopFX PhoneRing_LOOP 5000 >>

Alors que vous vous accordez une pause Xeno Cola, votre palmphone vibre: Fred veut encore vous parler...

-> Prendre l'appel
    <<jump Fred_again>>
===
title: Fred_again
tags:
colorID: 0
position: 33,47
---
<< stopFX PhoneRing_LOOP >>

Fred@holo_excited: Alors ? Je t'attends toujours ? Quand viens-tu ?
You: Heu... Je sais pas.
Fred@holo_happy: Beware ! Je te défie IRL à la salle de gym, ce sera notre AlphaMégaRing. Et si tu ne viens pas, je le dis à toute la communauté du Métaverse.
<< hide >>
Nooon ! Si la communauté apprend que vous avez décliné le défi de Fred. Fred, le membre le plus populaire du Métaverse de la fraternité étudiante.
Vous êtes grillé aux yeux de tous les camarades de l'Académie d'Omniscience et de Polytechnologie Universelle... Vous êtes piégé. quand faut y aller, faut y aller...

-> Retrouver Fred
    <<jump Salle_Gym2>>
===
title: Salle_Gym2
tags:
colorID: 0
position: 317,29
---
<< fadeIn >>
<< setBackground sport >>
<< fadeOut >>
<< loopFX Sports_LOOP 16000 >>

Vous relevez le défi lancé par Fred. En fait, vous vous éclatez tous les deux comme des enfants; cybersprint, electroskate, eBall, eJump...
Le moins que l'on puisse dire, c'est que vous êtes donné à fond. Au final, vous vous sentez épuisé. Il est temps de retourner travailler.
<< stopFX Sports_LOOP  >>

<< advanceTime 2:00 >>
<< addToGauge sleep 30 >>
<< addToGauge food 30 >>
<< addToGauge mentalLoad 30 >>

Fred_smiling: Tu pars déjà, tu déclares forfait ?
You: J'en peux plus.
Fred_smiling: Alors, j'ai gagné.
You: Pour une fois.
<< hide >>

-> Retourner dans votre chambre
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Surcharge_Cognitive
tags:
colorID: 0
position: -427,-43
---
<< fadeIn >>
<< setBackground bedroom >>
<< fadeOut >>
<< music NegativeSad >>
Pfuuuu... Vous avez soudainement la tête lourde, la vision floue, vous ne sentez plus votre corps... Vous avez l'impression d'avoir bu une bouteille de whisky cul-sec.
Vous n'êtes pourtant pas ivre. Vous n'avez pas abusé de l'alcool. Vous n'avez pas bu une goutte.
Vous auriez peut-être abusé de votre temps de travail. Ce n'est pas possible, vous n'avez pas fini la rédaction de votre note !...
N'empêche, vous sentez votre tête penchez en avant. Vous tentez de la redresser, mais la fatigue, la charge est trop forte. Vous finissez par céder. Vous sombrer dans le noir...
<< fadeIn >>

Puis, vous entendez une voix familière:

???: Tout va bien, { $name } ?
You: Tais-toi, Miss Temdé !
<< fadeOut >>
Azul_proud: Raté, c'est moi, Azul.
You: Zut...
Azul_happy: Pas grave, viens, on a une nouveau truc à t'apprendre.

-> Suivre Azul 
    << hide >>
    <<jump Assemblee_3>>
===
title: Assemblee_3
tags: noUi
colorID: 1
position: -1091,21
---
<< fx Warp >>
<< fadeIn >>
<< setBackground circle >>
<< fadeOut >>

Hop, la seconde d'après, vous retrouvez Azul et ses collègues dans cet étrange et mystérieux jardin exotique.
Ce petit nid de verdure que vos hôtes nomment l'assemblée des Sages. C'est de la téléportation ou de la magie ? Et où se trouve ce fichu décor si criant de vérité... On dirait d'authentiques plantes exotiques...
Temde_smiling: Yep, elles sont réelles, ce ne sont pas des végétaux synthétiques. C'est moi qui m'en occupe.
Sapiens_concerned: Super Miss Temdé. Je ne crois pas que { $name } soit venu pour échanger sur cette question.
Ledai_explaining: Tout à fait. Cette nouvelle session aura pour objet la surcharge cognitive.
<< hide >>
You: Quoi ?!
Azul_proud: On t'as appris à ménager ton corps.
Temde_cheerful: Bien dormir et manger équilibré.
Sapiens_thinking: C'est la même chose avec tes capacités mentales. Elles ont besoin d'énergie et de recharger les batteries. Je te l'ai dit. Mais elles peuvent aussi tomber en panne.
You: Comment ça ?
Sapiens_concerned: Si tu les surcharges.
Ledai_explaining: Si tu les fais surchauffer. Comme ton ordinateur, il peut se bloquer, ne plus répondre.
You: Ah bon ?
Sapiens_happy: Il faut utiliser la technique <i>pomodoro</i>
<<jump pomodoro>>
===
title: pomodoro
tags:  noUi
colorID: 0
position: -750,149
---
<< music Neutral >>

You: Et ça consiste en quoi ?
Ledai_explaining: C'est très simple. Il faut fractionner ta tâche en plusieurs petites tâches.
You: Un exemple.
Ledai_explaining: Imagine un fruit aussi gros que toi. Tu ne vas pas l'engloutir en une seule fois. Tu vas le couper en plus petit bouts que tu vas du premier au dernier morceau.
You: Ouais... Et si je veux le dévorer ?
Sapiens_concerned: Ah, jeunesse insouciante... Tu vas t'étouffer, tu vas faire exploser ton estomac jusqu'à régurgiter ce que tu auras péniblement avalé.
Ledai_doubtful: Beuha... C'est dégoûtant.
Azul_happy: L'important n'est pas de s'élancer, mais de franchir le kilomètre suivant, et ainsi de suite jusqu'à la ligne d'arrivée.
Temde_smiling: L'important est de tenir l'effort sur la durée, pas de sprinter.
You: Et c'est tout ?
Sapiens_thinking: Non. Il y a autre chose. Le plus important... Éviter les distractions.
-> Les distractions  ?
    <<jump Les_distractions>>
===
title: Les_distractions
tags:  noUi
colorID: 0
position: -370,280
---
Sapiens_thinking: Les perturbations extérieures qui parasitent ta charge mentale et saturent tes processus de calcul cérébral.
You: Hé, je suis pas une machine !
Ledai_happy: Tout à fait, l'erreur est humaine, pas informatique.
Azul_proud: Je la fais court. Plus on est concentré, moins on peut disperser son attention. C'est comme le jonglage...

Temde_smiling: Exactement. Regarde-moi ! J'ai quatre bras ! Toi, tu n'en as que deux, je peux jongler avec plus de balles que toi ! 
You: C'est ça. Je te défie !
<< hide >>
Miss Temdé se lève tranquillement et étend ses mains. Des balles apparues de nulle part se matérialisent dans ses paumes. Miss Temdé jongle ainsi avec quatre, cinq, puis six balles...
Temde_smiling: À ton tour, montre-nous ce que tu peux faire.
<<jump Jongler>>
===
title: Jongler
tags:  noUi
colorID: 0
position: -184,504
---
<< empty >> 
<< hideDialog >>
<< music Tense >>
<< minigame Juggling >>
<< music Neutral >>
<< showDialog >>
// Necessary so that Yarn evaluates the variable correctly
<< empty >> 

<< if $ballsJuggled < 5 >>
Temde_smiling: Tu vois, tu ne peux pas jongler autant de balles que moi.
Temde_thinking: Mais, moi aussi, j'ai mes limites. J'ai beau jonglé 6 balles, mais je ne pourrais pas jonger 7, ou 8, ou 12 !  
Azul_happy: Nous en avons tous des limites...
<< else >>
Temde_smiling: Bravo ! Tu a réussi à jongler autant de balles que moi.
Temde_thinking: Mais est-ce que tu aurais pu jongler le double ? Le triple ? 
Azul_happy: Nous avons tous des limites...
<< endif>>

<<jump Quit_Mini_Game>>
===
title: Le_Quizz_des_Sages
tags:  noUi
colorID: 0
position: 342,320
---
Sapiens_angry: Je suis content de voir que tu te prends au jeu.
Ledai_doubtful: En effet, tout ce travail n'est pas mené en vain.
You: J'espère bien. C'est parfois barbant votre baratin...
Ledai_doubtful: Notre baratin ? Mais, nous essayons de t'améliorer...
You: Mouais...
Sapiens_thinking: Commençons par cette question. La technique pomodoro consiste à fractionner une tâche en plusieurs tâches.
-> Pour pouvoir jongler avec chacune dans l'ordre que l'on veut, voire en même temps
    <<jump Pomodoro_No>>
-> Pour pouvoir avancer méthodiquement, en respectant des étapes de concentration intenses mais courtes
    <<jump Pomodoro_Yes>>
===
title: Pomodoro_No
tags:  noUi
colorID: 0
position: 701,260
---
<< fx Failure >>

Sapiens_angry: N'importe quoi ! Tu n'as rien compris de ce qu'on a dit.
Sapiens_concerned: Je suis attristé de voir tout le mal que Miss Temdé s'est donné avec la parabole des balles de jonglage.
Sapiens_concerned: Non, on ne peut pas gérer n'importe quelle tâche dans n'importe quel ordre ! Et surtout pas tout en même temps ! Ta charge cognitive encaisse, encaisse. Et elle finit par saturer.
Ledai_doubtful: Hélas. Le "mode multitâche" a la vie dure.
Azul_sad: Cessons de regarder le verre à moitié vide. Je pense que { $name } a compris la leçon cette fois.
Ledai_explaining: Bien parlé Azul.
Sapiens_thinking: J'espère. Continuons avec la deuxième affirmation test, laquelle est la bonne ?
Sapiens_happy: Maintenant que tu as accepté l'idée que ton processeur cérébral ne pourra supporter un effort continu trop long... Tu décides de ménager ton cerveau en passant la matinée à pratiquer des activités agréables ou ludiques.
Sapiens_happy: Tu travailleras <b>uniquement l'après-midi</b> ou tu préfères travailler <b>matin et après-midi</b> afin de te ménager des pauses régulières et laisser ton processeur cérébral refroidir régulièrement ?
-> Me ménager
    <<jump GestionTemps_No>>
-> Travailler d'arrache-pied
    <<jump GestionTemps_Yes>>
===
title: Pomodoro_Yes
tags:  noUi
colorID: 0
position: 698,531
---
<< fx Success >>

Sapiens_happy: Exactement ! On doit apprendre les tâches les unes après les autres de manière logique.
Ledai_explaining: Que ce soit de manière linéaire ou itérative. On n'entame jamais une tâche importante si on ne sait pas d'où on vient ni on veut aller.
Temde_smiling: Oui, enfin, grosso modo...
Azul_sad: Chut ! Ne commençons pas à nous chicaner. Comment voulez-vous que { $name } y comprenne quelque chose...
Sapiens_concerned: Merci, Azul. Continuons avec la deuxième affirmation test, laquelle est la bonne ?
Sapiens_happy: Maintenant que tu as accepté l'idée que ton processeur cérébral ne pourra supporter un effort continu trop long... Tu décides de ménager ton cerveau en passant la matinée à pratiquer des activités agréables ou ludiques.
Sapiens_happy: Tu travailleras <b>uniquement l'après-midi</b> ou tu préfères travailler <b>matin et après-midi</b> afin de te ménager des pauses régulières et laisser ton processeur cérébral refroidir régulièrement ?
-> Me ménager
    <<jump GestionTemps_No>>
-> Travailler d'arrache-pied
    <<jump GestionTemps_Yes>>
===
title: GestionTemps_No
tags:  noUi
colorID: 0
position: 1042,297
---
<< fx Failure >>

Sapiens_angry: J'en perds ma barbe ! Quelle réponse idiote ! Tu as compris ce qu'est la surcharge cognitive ?
You: C'est quand le cerveau fatigue, il surchauffe.
Ledai_doubtful: Alors pourquoi as-tu cru cette affirmation ?
You: Parce que mon pote, Fred, il bosse à la dernière minute et ça marche !
Temde_thinking: Tu n'es pas ton pote.
Azul_sad: Rappelle-toi. Je te l'ai dit. C'est comme pour le sommeil : Il faut travailler régulièrement, sans excès.
Temde_thinking: Surtout ne pas tout reporter au dernier moment.
Ledai_explaining: Sinon, tu risques le court-circuit, le "system failure"...
Sapiens_concerned: C'est enfin compris ?
You: Oui.
Sapiens_thinking: Alors voici la dernière proposition test. J'ai insisté sur le rôle des distracteurs dans la surcharge cognitive... Choisis bien cette fois ta réponse.
-> Les distracteurs sont comme des bonbons qui récompensent les pauses entre chaque session de concentration intense
    <<jump Distracteurs_No>>
-> Les distracteurs sont des tentations extérieures qui menacent et dérangent l'effort de concentration du cerveau
    <<jump Distracteurs_Yes>>
===
title: GestionTemps_Yes
tags:  noUi
colorID: 0
position: 1048,579
---
<< fx Success >>

Sapiens_happy: Tout à fait. Chaque chose en son temps. Comme tes ordinateurs, il faut les mettre en veille pour qu'ils cessent de computer sans fin, qu'ils puissent refroidir et récupérer en capacités de calcul.
Azul_proud: Rappelle-toi. Je te l'ai dit. C'est comme pour le sommeil: Il faut travailler régulièrement, sans excès.
Temde_smiling: Surtout ne pas tout reporter au dernier moment.
Ledai_explaining: Sinon, tu risques le court-circuit, le "system failure"...
Sapiens_happy: C'est enfin compris ?
You: Oui.
Sapiens: Alors voici la dernière proposition test. J'ai insisté sur le rôle des distracteurs dans la surcharge cognitive... Choisis bien ta réponse.
-> Les distracteurs sont comme des bonbons qui récompensent les pauses entre chaque session de concentration intense
    <<jump Distracteurs_No>>
-> Les distracteurs sont des tentations extérieures qui menacent et dérangent l'effort de concentration du cerveau
    <<jump Distracteurs_Yes>>
===
title: Distracteurs_No
tags:  noUi
colorID: 0
position: 1281,310
---
<< fx Failure >>

Sapiens_angry: Quelle horreur ! Quel non-sens ! Les distracteurs...
Sapiens_angry: Réseaux sociaux, notifications automatiques, connexion web ou TV, la pollution sonore, échanges avec amis, activité sans rapport direct avec la tâche présente... Ne sont pas des facilitateurs, ni même des récompenses !
Sapiens_angry: Comme leur nom l'indique, ce sont des nuisances ! Ils t'empêchent d'être 100% concentré sur la tâche présente. Les distracteurs distraient ton attention, la dispersent dans toute les directions ! Par conséquent les distracteurs affaiblissent ta capacité cognitive...
You: Ah... zut...
Sapiens_angry: C'est tout ce que tu as à me dire ?
You: Ben... Ouais...
Sapiens_concerned: Quelle misère.
Azul_sad: Je pense que { $name } a compris cette fois-ci. N'est-ce pas ?
You: Oui.
Sapiens_concerned: Les distracteurs doivent être réduits au maximum. Surtout en période de concentration intense. Pourquoi ?
You: Parce qu'il réduisent mon attention.
Ledai_explaining: Tout à fait. Pour atteindre un niveau de concentration élevé, il faut se déconnecter de ces ondes parasites !
Azul_happy: Il faut se ménager un espace de calme et de paix, une bulle d'attention.
Sapiens_thinking: Pas d'écran, pas de réseau, pas de bruit désagréable, pas de présence indésirable...
You: OK.
Ledai_happy: Maintenant, il ne te reste plus qu'à noter tout ça dans ton journal mental !
<< hide >>
-> Journal mental 
    <<jump JournalMental>>
===
title: Distracteurs_Yes
tags:  noUi
colorID: 0
position: 1384,604
---
<< fx Success >>

Sapiens_happy: Bien entendu. Les Réseaux sociaux, les notifications automatiques, la connexion web ou TV, la pollution sonore, échanges avec amis, activité sans rapport immédiat avec la tâche présente... Ne sont pas des facilitateurs, ni même des récompenses ! Comme leur nom l'indique, ce sont des nuisances !
Sapiens_happy: Les distracteurs distraient ton attention, la dispersent dans toute les directions ! Les distracteurs affaiblissent ta capacité cognitive. Tu dois être 100% concentré sur la tâche présente, pas être distrait par d'autre choses ou d'autres personnes..
You: Oui.
Sapiens_happy: Les distracteurs doivent être réduits au maximum. Surtout en période de concentration intense. Pourquoi ?
You: Parce qu'il réduisent mon attention.
Ledai_happy: Tout à fait. Pour atteindre un niveau de concentration élevé, il faut se déconnecter de ces ondes parasites !
Azul_happy: Il faut se ménager un espace de calme de paix une bulle d'attention.
Sapiens_happy: Pas d'écran, pas de réseau, pas de bruit désagréable, pas de présence indésirable...
You: OK.
Ledai_happy: Maintenant, il ne te reste plus qu'à noter tout ça dans ton journal mental !
<< hide >>
-> Journal mental 
    <<jump JournalMental>>
===
title: JournalMental
tags:  noUi
colorID: 1
position: 1209,900
---
<< fx Warp >>
<< fadeIn 200 0xFFFFFF >>
<< setBackground bedroom night >>
<< fadeOut 200 >>

Vous revoilà seul... Dans cet instant figé, dans cette parenthèse intime entre l'ombre et la lumière, le jour et la nuit. Votre "carnet d'ingénieur tout-terrain" est entre vos mains...
Qu'allez-vous noter dessus ?
===
title: Lunch
tags:
colorID: 0
position: 477,-702
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>

Yum Yum ! Il est temps de se faire une pause "manger équilibré", comme le recommande les Sages. 
<< fx EatCook >>
Vous vous cuisinez un Green Burger Full Vegie. Franchement, c'est étrange, tant pour le goût que pour l'aspect.
On dirait un burger à la viande et il n'y en a aucune. En outre, c'est vraiment copieux. Et les frites à base de riz croustillant... Dingue. C'est meilleur que les frites de chez Naz Do...

<< advanceTime 0:30 >>
<< removeFromGauge sleep 20 >>
<< addToGauge food 20 >>

-> C'est pas tout, il faut s'y remettre
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Diner
tags:
colorID: 0
position: 684,-454
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>

À cette heure-ci, vous n'avez pas trop envie de cuire quelque chose de trop lourd. Un mix de ratatouilles de légumes du soleil d'Aldébaran, y a rien de mieux... 
<< fx EatCook >>
Vous ouvrez votre boîte de plat-rouge-bio. Vous servez le tout avec une assiette de céréales complètes. C'est simple, chaud, nourrissant et franchement bon.

<< advanceTime 0:30 >>
<< removeFromGauge sleep 20 >>
<< addToGauge food 20 >>

-> La journée n'est pas encore finie !
    << fadeIn >>
    <<jump Liste_Taches>>
===
title: Quit_Mini_Game
tags: noUi
colorID: 0
position: 182,578
---
Sapiens_concerned: On peut aussi apprendre à les repousser.
Temde_smiling: Ou à les contourner. C'est selon.
Ledai_explaining: Exactement, et c'est l'objet de cette session.
Sapiens_thinking: Gérer ta charge cognitive. Ne surtout pas saturer ton processeur cérébral.
Ledai_happy: S'isoler des éléments distracteurs. Ne pas abuser du "mode multitâches".
You: C'est quoi le "mode multitâches" ?
Temde_cheerful: En "mode multitâches", tu commence une action, tu passes à une autre, puis à une troisième...
Azul_proud: Ou tu en mènes plusieurs de front.
Ledai_explaining: Au lieu de focaliser ton processeur sur un calcul stratégique, tu fragmente ton attention.
Sapiens_thinking: Cela réduit ta capacité cognitive. Et à un moment donnée, cette dernière sature ! Elle bloque ! Elle freeze !
Ledai_explaining: Elle ne répond plus, c'est l'écran bleu.
You: Ah.
Sapiens_concerned: Un petit questionnaire pour conclure ?

<<jump Le_Quizz_des_Sages>>
===
