#gauges:learning,sleep,food,mentalLoad

title: Start
tags:
position: -1656,-460
colorID: 8
---
<< fadeIn >>
<< showTitle Chapitre 4 >>
<< setTime 8:00 3 >>
<< set $silence to false >>
<< setBackground bedroom >>
<< setGauge learning 0 >>
<< setGauge sleep 100 >>
<< setGauge food 100 >>
<< setGauge mentalLoad 0 >>
<< showGauges >>
<< music Neutral >>
<< fadeOut >>

<<if !visited("NoFredCall")>>
<< visitPermanent NoFredCall >>
Après vous être réveillé en douceur, vous prenez votre petit déjeuner tranquillement en attendant l'appel de Fred.
Aujourd'hui, vous devez travailler en binôme pour rédiger un mémoire… Sur quoi, déjà ?
Ah, oui ! La théorie du chaos dans la macro-biologie post-darwinienne. C'est compliqué, comme sujet, il vaut mieux être à deux pour l'aborder !
Quelque chose cloche… Fred ne vous a toujours pas appelé ! Ça ne lui ressemble pas…
-> Appeler Fred sur son Palmphone
    << visit TryToCallFred >>
    << loopFX PhoneRing_LOOP 5000 >>
    << screenshake 10 600 >>
    …
    << stopFX PhoneRing_LOOP 5000 >>
    Fred@holo_excited: Yooo, c'est Fred !
    You: Ah, Fred, tu es là ! Pourquoi tu m'as pas…
    Fred@holo_smiling: Je suis pas dispo pour le moment, mais si vous voulez, laissez-moi un message…
    Fred@holo_happy: …Je le lirai sûrement pas, mais rien ne vous empêche d'essayer !
    You: Super… Ça m'avance bien, tout ça !
    << advanceTimeBy 0:15 >>
-> Faire autre chose

Vous vous étiez mis d'accord pour aller travailler sur le mémoire à la bibliothèque de l'Académie… Tout seul, qu'est-ce que vous allez faire ?
<<else>>
Vous vous levez une nouvelle fois. Pas besoin d'appeler Fred, vous savez qu'iel est occupé à poncer le dernier GTR…
<<endif>>
<< hide >>
<<jump Actions_List>>
===

title: Actions_List
tags: freechoice
position: -1210,-459
---
<< music Neutral2 >>
<< setBackground bedroom >>
<< fadeOut >>

-> Cuisine@cuisine #line:0717775 
    <<jump cuisine_subchoice>>
-> Porte@porte #line:0d21566 
    <<jump porte_subchoice>>
-> Bureau@bureau #line:048f567 
    <<jump bureau_subchoice>>
-> Lit@lit #line:03dbf5c 
    <<jump lit_subchoice>>
-> Salon@salon #line:0645318 
    <<jump salon_subchoice>>
===

title: lit_subchoice
tags: subchoice
position: -1220,-672
---
<< fadeIn >>
<< setBackground bedroom bed >>
<< fadeOut >>
-> Se recoucher (1h30) #once
    <<jump Sleep_Morning>>
-> back
    <<jump Actions_List>>
===

title: porte_subchoice
tags: subchoice
position: -829,-353
---
-> Aller chez Fred
    <<jump ChezFred>>
-> Aller seul à la bibliothèque
    <<jump Bibli_soloAM>>
-> back
    <<jump Actions_List>>
===

title: cuisine_subchoice
tags: subchoice
position: -1358,-576
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>
-> Casser la croûte (30 min) #once
    <<jump Lunch>>
-> back #line:0fad580 
    <<jump Actions_List>>
===

title: bureau_subchoice
tags: subchoice
position: -972,-619
---
<< fadeIn >>
<< setBackground bedroom desk >>
<< fadeOut >>
-> Contacter Fred sur son compte Disboard #once
    <<jump Fred_Disboard>>
-> Travailler sur le mémoire tout seul #once
    <<jump Memoire_Home>>
-> back #line:0a06fb6 
    << fadeIn >>
    <<jump Actions_List>>
===

title: salon_subchoice
tags: subchoice
position: -874,-487
---
<< fadeIn >>
<< setBackground bedroom chill >>
<< fadeOut >>
-> Regarder Star Mars #once
    <<jump TV>>
-> back #line:0341673 
    <<jump Actions_List>>
===

title: Fred_Disboard
tags: 
colorID: 0
position: -816,-724
---
<< visit FredCall >>
Vous vous installez sur votre poste et mettez en route l'application Disboard. Vous constatez avec surprise que Fred est connecté, et vous lancez sans tarder un appel holo.
Fred@holo_surprised: { $name }… Il faut que je te dise quelque chose…
You: Quoi ?! Qu'est-ce qu'il y a, Fred ? Tu vas bien ??
Fred@holo_surprised: Il s'est passé un truc fou ce matin…
You: Accouche !!! QU'EST-CE QU'IL S'EST PASSÉ, FRED ??
Fred@holo_excited: J'AI REÇU… LE DERNIER OPUS… DE GRAND THRILL RODEO !!! JE SAIS, C'EST INCROYABLE !
-> FRED !! Tu m'as fait peur !
    You: Ça va pas, la tête ?! Tu m'as fait peur !
    <<if visited("TryToCallFred")>>
    Fred@holo_happy: Quoi ? Ahhh, t'as essayé de m'appeler… J'étais en plein dans le jeu ! J'en suis au chapitre 4 !
    You: Ouais, super. J'en ai pas grand-chose à faire, en fait.
    <<endif>>
-> Ça nous aidera pas à finir le mémoire…
    Fred@holo_happy: Ah ouais… C'est vrai, tu m'as appelé… J'étais en plein dans le jeu ! J'en suis au chapitre 4 !
    You: Ouais, super. J'en ai pas grand-chose à faire, en fait.
Fred@holo_excited: Tu te rends pas compte ! Le dernier opus était  il y a 36 ans, { $name } ! Ça se jouait encore à la MANETTE à l'époque ! Tu te souviens des "manettes" ??
You: Euh… Ouais, je crois. Bref, on a rendez-vous à la bibliothèque, tu te souviens ?
Fred@holo_happy: Euh… Ouais, on fera ça plus tard ! Pour l'instant, il FAUT que tu viennes chez moi, on va tester le mode multi !
You: On finira jamais à temps si on fait comme ça !
Fred@holo_happy: C'est bon, viens ! On a le temps !
-> Bon, je veux bien venir chez toi, mais pas pour jouer !
    You: Bon, je viens te chercher, mais pas pour traîner sur GTR !
    Fred@holo_bored: Ouais… Comme tu veux…
-> Non, on se retrouve à la bibliothèque !
    You: Non, je vais à la bibliothèque pour bosser, on se rejoint là-bas directement !
    Fred@holo_bored: Pfff… Comme tu veux… Mais je finis le chapitre d'abord…
<< advanceTimeBy 0:15 >>
<< hide >>
<<jump Actions_List>>
===

title: Sleep_Morning
tags: 
colorID: 0
position: -1244,-900
---
<< loopFX Sleep_LOOP >>
<< addToGauge sleep 40 >>
Pas grave… Si Fred ne veut pas travailler, alors c'est autant son problème que c'est le vôtre. Vous vous recouchez. Au moins, vous serez plus frais pour la journée !
<< stopFX Sleep_LOOP >>
<< advanceTimeBy 1:30 sleep food >>
<<jump Actions_List>>
===

title: Lunch
tags: 
colorID: 0
position: -1423,-748
---
<< fx EatCook >>
Un peu de gelée vitaminée en complément du petit déjeuner de ce matin ne vous ferait pas de mal…
<< addToGauge food 40 >>
…Cet en-cas supplémentaire a ravi votre estomac.
<< stopFX EatCook >>
<< advanceTimeBy 0:30 food sleep >>
<<jump Actions_List>>
===

title: TV
tags: 
colorID: 0
position: -675,-613
---
Vous allumez votre télé holographique en attendant que Fred se bouge… Après tout, vous n'avez pas à porter toute la responsabilité tout seul…
<< show lightsaber@holo >>
<< loopFX TVStarwars_LOOP 10000 >>
Aujourd'hui, pas d'épisode de votre show favori, mais à la place un dessin animé dérivé… Kids Mars…
C'est à peu près la même chose que le matériel original, mais sans violence ni une trace de sang.
<< advanceTimeBy 2:00 >>
Pas très intéressant, donc. Mais au moins, cela vous vide la tête.
<< removeFromGauge mentalLoad 30 >>
<< stopFX TVStarwars_LOOP >>
<< hide >>
<<jump Actions_List>>
===

title: Memoire_Home
tags: 
colorID: 0
position: -1069,-766
---
<< visit WorkWithoutFred >>
Convaincu que Fred vous a lâché, vous entamez le premier jet du mémoire. Le sujet est incroyablement complexe et vous avez du mal à structurer votre pensée sans l'aide des notes de Fred.
<< addToGauge learning 10 >>
<< addToGauge mentalLoad 20 >>
Peu satisfait, vous pliez bagage et vous posez à nouveau votre regard sur votre chambre d'étudiant.
<< advanceTimeBy 2:00>>
<<jump Actions_List>>
===

title: ChezFred
tags: 
colorID: 0
position: -603,-422
---
<< fadeIn >>
<< setBackground bedroom_fred >>
<< music Neutral3 >>
<< fadeOut >>

<< advanceTimeBy 0:15 >>

Fred vous fait entrer à la première sonnerie. #line:024dd08 

Fred_excited: Aaaah, { $name } ! Juste à temps pour tester le nouveau GTR !!
You: Je suis pas venu jouer, Fred… On a le mémoire, rappelle-toi…
Fred_happy: Ouaiiiiis, je sais… T'inquiète, j'ai quasiment tout le plan en tête ! C'est comme si c'était déjà rédigé, en fait !
You: On doit se rendre à la bibliothèque, elle ferme à 20h…
Fred_smiling: Ouais, ouais, mais avant ça, j'ai un chapitre 4 à finir !
You: Moi aussi…
-> Rester jouer avec Fred
    You: Bon, ok… T'as gagné, je te rejoins en multi…
    Fred_excited: Yeeeesssss !! Allez, regarde, là, on doit démanteler le cartel du terrible Dr. Stronghold…
    << advanceTimeBy 1:30 >>
    Vous passez un bon moment à descendre des gangsters avec votre pote, mais le temps file…
    << removeFromGauge mentalLoad 30 >>
    Fred_happy: C'était génial, non ? Allez, on fera le boss une prochaine fois, si tu veux.
    You: Maintenant, on doit finir ce satané mémoire !
    <<jump Bibli_binome>>
-> Insister pour que Fred vienne
    You: Non, Fred, trop, c'est trop ! On va à la bibliothèque, maintenant.
    << addToGauge mentalLoad 10 >>
    Fred_bored: Bon, t'as gagné… On va le faire vite fait, bien fait, ce devoir.
    << advanceTimeBy 0:15>>
    <<jump Bibli_binome>>
===

title: Bibli_soloAM
tags: 
colorID: 0
position: -705,-208
---
<< fadeIn >>
<< music Neutral3 >>
<< setBackground library >>
<< advanceTimeBy 0:15 >>
<< setDeadline Fermeture 20:00 >>
<< fadeOut >>

Au lieu d'attendre bêtement Fred, vous vous rendez dans cette immense et riche bibliothèque de l'Académie.
Vous lui précisez par message dans quelle partie du bâtiment vous vous trouvez, puis déballez vos affaires.
Cet immeuble très ancien modernisé vous inspire autant qu'il vous accable.
Que faire en attendant ?
-> Commencer à travailler sur le mémoire << if !visited("WorkWithoutFred") >>
    << visit WorkWithoutFred >>
    Tant pis pour Fred, iel n'avait qu'à être là ! Vous entamez le premier jet du mémoire.
    << advanceTimeBy 2:00 >>
    Le sujet est incroyablement complexe et vous avez du mal à structurer votre pensée sans l'aide des notes de Fred.
    << addToGauge learning 10 >>
    << addToGauge mentalLoad 20 >>
-> Attendre Fred en écoutant de la musique
    Vous décidez d'attendre Fred pour commencer, et de vous détendre en écoutant un peu de musique.
    You: Hey, BibliMate ! Crée une bulle sonore autour du bureau.
    RoomMate: Bien entendu. Une requête musicale ?
    -> Un bon vieux morceau rock des Frosty Mankees !
        You: Un petit Frosty Mankees est de mise !
        RoomMate: Entendu !
        << music Solo1 >>
        << advanceTimeBy 2:00 >>
        Le morceau mêle des motifs de basse et guitare qui se répètent sans cesse, accompagnant avec merveille la voix d'Alec Burner. Un classique !
        << removeFromGauge mentalLoad 20 >>
        Vous vous sentez détendu et revigoré par cet interlude musical.
        << music Neutral3 >>
    -> Un mix Xéno-Techno Bass-Boosted de EarsKiller !
        RoomMate: Entendu !
        << music DanceQuiet >>
        << advanceTimeBy 2:00 >>
        Les basses frénétiques et hypnotiques de EarsKiller vous prennent aux tripes et vous mettent dans un état de transe hypnotique.
        << addToGauge sleep 20 >>
        Vous vous sentez plein d'énergie et prêt à tout donner.
        << music Neutral3 >>
Vous levez les yeux pour enfin voir Fred !
<< show fred >>
Fred_happy: Yo !! Bon… Je peux officiellement dire que le nouveau GTR est encore mieux que l'ancien !!
You: Euh… Super… Maintenant que tu es enfin là, on pourrait se mettre au travail !… La bibliothèque ferme à 20h.
Fred_smiling: T'inquiète, j'ai tout dans la tête, ça va aller super vite.
You: On va voir ça…
<<jump Liste_Taches>>
===

title: Bibli_binome
tags: 
colorID: 0
position: -442,-200
---
<< fadeIn >>
<< setBackground library >>
<< setDeadline Fermeture 20:00 >>
<< fadeOut >>

Fred_happy: Hey, quand on a fini, on pourra se faire un petit GTR, tu crois pas ?
-> Si ça te fait plaisir…
    You: Oui, si on a encore le temps. Au pire, on pourra faire ça un autre jour !
-> Arrête avec ton GTR !
    You: Bon sang mais Fred, calme-toi avec GTR ! Tu pourras y jouer plus tard !
    Fred_bored: Pffff… Ouais, t'as sûrement raison…
Fred_smiling: Bon d'accord, on s'y met, alors ? C'est toi le chef de projet, non ? Alors, on fait quoi ?
<< hide >>
<<jump Liste_Taches>>
===

title: Liste_Taches
tags:
position: -870,-52
colorID: 1
---
<< if getGauge("food") <= 0>>
<<jump TooHungry>>
<<endif>>

<<if getGauge("sleep") <= 0>>
<<jump TooTired>>
<<endif>>

<<if getGauge("mentalLoad") >= 100>>
<<jump Surcharge_Cognitive>>
<<endif>>

<< if !isTimeOver("20:00") >>
-> Travailler sur le mémoire…
    <<jump Travail_Memoire_subchoice>>
-> Faire une pause… <<if $silence == false>>
    <<jump Pause_subchoice>>
-> Générer une bulle d'immersion studieuse <<if $silence == false>>
    <<jump Zero_Distraction>>
-> Désactiver le mode immersion studieuse <<if $silence == true>>
    <<jump Desactiver_Zero_Distraction>>
<<else>>
    <<jump Exit_Bibli>>
<< endif>>
===

title: TooHungry
tags:
colorID: 0
position: -580,-274
---
<< hide >>
<< music NegativeSad >>
Pfuuuu… Vous avez soudainement la tête lourde, la vision floue, vous ne sentez plus votre corps…
Vous avez le ventre creux, c'en est sûrement la raison. Vous voulez vous faire un en-cas, mais votre corps en a décidé autrement.
<< fadeIn >>
Vous êtes affamé !
-> Recommencer la journée
    << fx Warp >>
    << fadeIn 1500 12ea12 >>
    << resetLevel >>
    <<jump Start>>
===

title: TooTired
tags:
colorID: 0
position: -581,-196
---
<< hide >>
<< music NegativeSad >>
Pfuuuu… Vous avez soudainement la tête lourde, la vision floue, vous ne sentez plus votre corps…
<< fadeIn >>
Vous êtes épuisé !
-> Recommencer la journée
    << fx Warp >>
    << fadeIn 1500 12ea12 >>
    << resetLevel >>
    <<jump Start>>
===

title: Surcharge_Cognitive
tags:
colorID: 0
position: -390,-150
---
<< hide >>
<< music NegativeSad >>
Pfuuuu… Vous avez soudainement la tête lourde, la vision floue, vous ne sentez plus votre corps…
<< fadeIn >>
Vous êtes à bout !
-> Recommencer la journée
    << fx Warp >>
    << fadeIn 1500 12ea12 >>
    << resetLevel >>
    <<jump Start>>
<<jump Failed>>
===

title: Travail_Memoire_subchoice
tags: subchoice
position: -914,77
colorID: 1
---
-> Montrer vos notes à Fred (2h) #once #line:0611eb4 
    <<jump Notes_You>>
-> Demander à Fred ses notes (2h) #once 
    <<jump Notes_Fred>>
-> Rédiger la première partie (1h) <<if visited("Plan")>> #once
    <<jump Redaction>>
-> Rédiger la deuxième partie (1h) <<if visited("Plan")>> #once
    <<jump Redaction>>
-> Rédiger la conclusion (1h) <<if visited("Plan")>> #once
    <<jump Redaction>>
-> Valider le plan du mémoire (2h) <<if visited("Notes_You") && visited("Notes_Fred")>> #once
    <<jump Plan>>
-> Relire le mémoire (Passe le temps jusqu'à 20h) <<if visited("Redaction")>> #once
    Vous relisez le mémoire, tentant de corriger les erreurs de logique et de grammaire, jusqu'à ce que votre temps s'écoule.
    << addToGauge learning 10 >>
    << advanceTimeUntil 20:00 >>
    <<jump Exit_Bibli>>
-> back
    <<jump Liste_Taches>>
===

title: Pause_subchoice
tags: subchoice
position: -934,-147
colorID: 1
---
-> Pause Casse-croûte (1h)
    <<jump Pause_Lunch>>
-> Pause Café (30 min)
    <<jump Pause_Coffee>>
-> back
    <<jump Liste_Taches>>
===

title: Pause_Lunch
tags: 
colorID: 0
position: -958,-283
---
You: T'as pas le ventre creux, par hasard ?
Fred_excited: Ah, enfin ! Tu peux pas savoir à quel point j'attendais que tu dises ça.
You: Bon, et bien dans ce cas, cap sur la cafétéria !
Fred_happy: Ça nous fera une pause !

<< hide >>
<< fadeIn >>
<< setBackground cafeteria >>
<< fadeOut >>

Vous commandez un déjeuner à la cafétéria. Autour de vous, les discussions des autres élèves portent soit sur les cours, soit sur le boss final du nouveau GTR…
<< fx EatCook >>

<< addToGauge food 40 >>

Fred_angry: Hé ! NO SPOIL !! On n'a pas fini le jeu, nous autres !
Vous riez quand vous observez Fred engloutir son repas en une bouchée et tourner le dos furieusement au groupe d'étudiants qui parlaient du jeu pour allumer sa console portable.
<< removeFromGauge mentalLoad 20 >>
<< advanceTimeBy 1:00 food sleep >>

<<if !visited("Leave_Pause")>>
<<jump Leave_Pause>>
<<else>>
Bon… C'est reparti pour le travail…
<< fadeIn >>
<< setBackground library >>
<< fadeOut >>
<<jump Liste_Taches>>
<<endif>>
===

title: Pause_Coffee
tags: 
colorID: 0
position: -1021,-218
---
You: Un petit café pour se remonter ?
Fred_surprised: Je savais pas que tu buvais du café…
You: Pas d'habitude, mais là, je suis crevé. On y va ?
Fred_happy: Allez !

<< hide >>
<< fadeIn >>
<< setBackground cafeteria >>
<< fadeOut >>

<< removeFromGauge mentalLoad 20 >>
<< addToGauge sleep 20 >>
<< fx EatCook >>
Vous prenez votre café synthétique, pendant que Fred joue à GTR sur sa console portable. La boisson pleine de caféine vous remplit d'énergie pour la suite.
Pas une solution à long terme, certes, mais au moins, ça vous donne de l'avance sur la journée !
<< advanceTimeBy 0:30 sleep food >>

<<if !visited("Leave_Pause")>>
<<jump Leave_Pause>>
<<else>>
Bon… C'est reparti pour le travail…
<< fadeIn >>
<< setBackground library >>
<< fadeOut >>
<<jump Liste_Taches>>
<<endif>>
===

title: Leave_Pause
tags: 
colorID: 0
position: -1131,-310
---
<< visit Leave_Pause >>
You: Fred, on y retourne, j'ai besoin de toi.
Fred_excited: Yes, yes, attends 5 minutes, encore.

-> Bon, ok. Passe, on joue en multi.
    You: Bon, dans ce cas, passe au moins un module mental, que je joue avec toi !
    Fred sourit et vous tend ce petit dispositif coloré permettant de contrôler le personnage, que vous accrochez sur votre tempe.
    << removeFromGauge mentalLoad 20 >>
    C'est sympa de jouer les gangsters mais le temps passe ! Vous retournez à la bibliothèque pour travailler.
    << advanceTimeBy 1:00 food >>
-> Insister pour que Fred vienne avec vous  #line:071a93f 
    You: Fred, en tant que chef de projet, j'ai besoin de ton esprit créatif ! Viens !
    Fred_smiling: Bon… Ok… J'imagine que le boss attendra.

<< fadeIn >>
<< setBackground library >>
<< fadeOut >>
<<jump Liste_Taches>>
===

title: Zero_Distraction
tags: 
colorID: 0
position: -413,49
---
You: Hey, BibliMate, active le mode Immersion Studieuse !
RoomMate: C'est parti ! Une sphère de travail immersive, une !
<<if !visited("Zero_Distraction")>>
Aussitôt, un dôme de lumière opaque se referme sur votre table et vous isole du reste de la bibliothèque et des étudiants. Le son devient net et réverbéré, comme dans un studio d'enregistrement.
Fred_smiling: Bonne idée, la bulle de silence. On va pouvoir parler comme on veut tous les deux !
You: Bon, on reste concentrés, Fred. On doit finir la rédaction du mémoire aujourd'hui. #line:0f7cd07 
Fred_happy: Ouais…  Et si je te montrais ce qu'on peut faire dans le dernier GTR…
You: Fred, on reste concentrés sur le sujet du mémoire. De toute manière, ça ne marcherait pas. Nos connexions sont limitées aux sites académiques. #line:0056a1d 
<<endif>>

<< set $silence to true >>
<< visit Zero_Distraction >>
<<jump Liste_Taches>>
===

title: Desactiver_Zero_Distraction
tags: 
colorID: 0
position: -413,-37
---
You: Hey, BibliMate, désactive le mode immersif, s'il te plaît !
RoomMate: Pas de souci ! 
<< set $silence to false >>
<<jump Liste_Taches>>
===

title: Notes_You
tags: 
colorID: 0
position: -634,250
---
You: Bon, on passe mes notes en revue ?
Fred_smiling: Yeeees. Je t'écoute ! #line:0a51cb6 

<<visit Notes_You>>

<< if $silence == false >>
You: Non, mais je ne peux pas te lire à haute voix, on est à la bibliothèque ! #line:08e64e8 
<< endif >> 

-> Vous lui lisez vos notes #line:0bfb9d2 
    << jump Notes_You_Good >>
-> Vous le laissez lire vos notes
    << jump Notes_You_Bad >>
===

title: Notes_You_Good
tags: 
position: -383,341
colorID: 2
---
You: Grâce à la bulle immersive, on va pouvoir faire ça à l'oral ! Cool, non ?
Fred_smiling: Super ! Vas-y, je t'écoute !

<< if !$silence >>
Alors que vous parlez haut et fort, d'autres étudiants à proximité se tournent vers vous d'un air désapprobateur.
<< fx Failure >>
???: Hé, on est à la bibliothèque, là ! Un peu de silence, s'il vous plaît ? Activez une bulle d'immersion, qu'on ne vous entende pas dans toute l'Académie…
<<jump Notes_You_Bad>>
<<endif>>

Ne sachant pas par quoi commencer, vous dites la première remarque qui vous vient en tête. Et puis, de fil en aiguille, une remarque en amène une autre, et ainsi de suite.
Fred est un auditeur très bienveillant, il rebondit sur certaines de vos idées et vous incite à les clarifier, les critiquer, les nuancer.

<< advanceTimeBy 2:00 >> 
<< addToGauge learning 20 >>
<< addToGauge mentalLoad 10 >>

RoomMate: Voulez-vous que je conserve le fichier audio de la conversation pour en garder la trace ?
Fred_happy: Vas-y, BibliMate !
<<jump Liste_Taches>>
===

title: Notes_You_Bad
tags: 
position: -541,384
colorID: 2
---
You: On va faire ça par écrit, ok ?
Fred_surprised: Euh… Je sais pas si c'est la meilleure façon de faire…
You: Je suis le chef de projet, ou pas ?
Fred_sad: Oui, chef… #line:0c5e7f6 

Vous envoyez vos documents de travail à Fred. Vous voyez votre ami grincer des dents. #line:0b9fc4e 
Iel peine à lire et relire vos docs, et n'arrête pas de faire des pauses, de vous interpeller, mais vous limitez les discussions pour vous concentrer sur le travail.
<< advanceTimeBy 3:00 >>
<< addToGauge learning 10 >>
<< addToGauge mentalLoad 20 >>
<<jump Liste_Taches>>
===

title: Notes_Fred
tags: 
colorID: 0
position: -1192,159
---
You: Je sais que toi aussi, tu as avancé sur le travail de rédaction du mémoire. Tu as tes notes avec toi ?
Fred_happy: Tu veux pas plutôt qu'on en parle de vive voix ? #line:0a57c1c 

<<visit Notes_Fred>>

-> Écouter Fred présenter ses notes #line:031034a 
    << jump Notes_Fred_Good >>
-> Demander à Fred de vous envoyer ses notes écrites #line:0b165d9 
    << jump Notes_Fred_Bad >>
===

title: Notes_Fred_Good
tags: 
position: -1300,62
colorID: 2
---
Fred_happy: Tout est dans ma tête. Tu veux que je commence par quoi ? #line:0ca8f0f 

<< if !$silence >>
Alors que vous parlez haut et fort, d'autres étudiants à proximité se tournent vers vous d'un air désapprobateur.
<< fx Failure >>
???: Hé, on est à la bibliothèque, là ! Un peu de silence, s'il vous plaît ? Activez une bulle d'immersion, qu'on ne vous entende pas dans toute l'Académie…
<<jump Notes_Fred_Bad>>
<<endif>>



<< advanceTimeBy 2:00 >>
<< addToGauge learning 20 >>
<< addToGauge mentalLoad 10 >>

Vous comprenez que c'est à vous de questionner Fred et, en quelque sorte, de lui tirer les vers du nez. Vous passez les deux heures suivantes à lui poser des questions, à écouter ses réponses, à rebondir sur ses idées, ses commentaires. 
Fred a beau ne pas répondre aux standards académiques, iel a un don, c'est certain, iel a remarqué plein de choses que vous n'auriez pas vues.
RoomMate: Voulez-vous que je conserve le fichier audio de la conversation ?
You: Vas-y BibliMate !

<<jump Liste_Taches>>
===

title: Notes_Fred_Bad
tags: 
position: -1349,229
colorID: 2
---
Vous décidez de plutôt faire ça à l'écrit.
Fred pianote sur son ordinateur, l'instant d'après, vous réceptionnez ses documents… Il n'y a pas grand-chose d'exploitable… Que des bouts de phrases, de mots et quelques commentaires en annexe.
You: Je sais pas trop quoi faire de ça, Fred… Tu peux reprendre ton travail, s'il te plaît ?
Fred_sad: Bon, d'accord. Puisque tu insistes. C'est bien parce que c'est toi… #line:0b3e63f 
Vous voyez à son air pincé que Fred se fait violence pour vous faire plaisir. Après un bon moment, Fred, vous renvoie les fichiers mis à jour. #line:0d827ea 
Fred_sad: J'ai fait un effort. J'ai écrit des vraies phrases. #line:058646a 
En effet, c'est mieux, mais ça reste laconique. Il reste encore beaucoup à faire mais vous n'obtiendrez pas plus de Fred de cette manière. #line:0057664 
<< advanceTimeBy 3:00>>
<< addToGauge learning 10 >>
<< addToGauge mentalLoad 20 >>

<<jump Liste_Taches>>
===

title: Plan
tags: 
colorID: 0
position: -1139,276
---
You: Tu te souviens du plan ? #line:0608a08 
Fred_smiling: Ton plan ?  #line:010b808 
You: Oui, je t'ai demandé d'en proposer un, t'as refusé. #line:0a49c05 
Fred_sad: Mais pourquoi on doit suivre l'axe que tu as proposé ?  #line:091835b 
You: Tu en vois d'autres ?
Fred_happy: Oui, plein, y a l'embarras du choix. #line:0b977ec 

<< visit Plan >>

-> Prendre le temps de discuter avec Fred de tous les plans possibles
    <<jump Plan_Good>>
-> Envoyer votre plan à Fred
    <<jump Plan_Bad>>
===

title: Plan_Good
tags: 
position: -1358,349
colorID: 2
---
You: C'est le dernier jour, on doit choisir un plan et s'y tenir.  #line:0774d64 
Fred_happy: Très bien. Donne-moi les parties et je te dirai ce que j'en pense.
You: Ok.  #line:02303dd 

<< if !$silence >>
Alors que vous parlez haut et fort, d'autres étudiants à proximité se tournent vers vous d'un air désapprobateur.
<< fx Failure >>
???: Hé, on est à la bibliothèque, là ! Un peu de silence, s'il vous plaît ? Activez une bulle d'immersion, qu'on ne vous entende pas dans toute l'Académie…
<<jump Plan_Bad>>
<<endif>>

Vous commencez ensuite à lui présenter vos trois grandes parties, vos sections et sous-sections. Fred se met à la place du jury pour vous faire les meilleures remarques et vous proposer de meilleures options.
Iel réussit à étoffer votre argumentaire et ajoute plein de nouvelles idées.
<< advanceTimeBy 2:00 >>
<< addToGauge learning 20 >>
<< addToGauge mentalLoad 10 >>

<< jump Liste_Taches >>
===

title: Plan_Bad
tags: 
position: -1268,409
colorID: 2
---
Vous décidez alors d'envoyer directement le plan à Fred.
You: Bon. On n'a plus le temps d'hésiter. On doit choisir un plan et s'y tenir.
Fred_sad: C'est vraiment dommage. #line:0c92611 
You: Je te rappelle qu'on doit le rendre ce soir, ce mémoire. Je te renvoie le plan et on le valide. Un point, c'est tout.
Aussitôt, Fred se met dans une position d'avocat de l'accusation, vous fait douter de vos parties et de la structure que vous avez choisie à mesure que vous la lui présentez.
Pourvu qu'iel se trompe…

<< advanceTimeBy 3:00 >>
<< addToGauge learning 10 >>
<< addToGauge mentalLoad 20 >>

<<jump Liste_Taches>>
===

title: Redaction
tags: 
colorID: 0
position: -893,277
---
<<if !visited("Redaction")>>
You: On commence la rédaction du mémoire ?
Fred_happy: Yes. On se répartit la tâche comment ? #line:005280e 
<<visit Redaction>>
-> Demander l'avis de Fred <<if !visited("WorkWithoutFred")>>
    <<jump Redaction_Good>>
-> Dire à Fred quoi faire
    <<jump Redaction_Bad>>

<<elseif !visited("Redaction2")>>

<<if !visited("Redaction_Bad")>>
<< visit Redaction2 >>
You: On reprend la rédaction ?
Fred_happy: Ouais, c'est reparti !
Vous entamez le reste du mémoire. Comme à son habitude, Fred rebondit en permanence sur vos arguments et vous fournit assez de matière à développer.
Après une heure, l'essentiel de la partie est terminé !
<< addToGauge mentalLoad 10 >>
<< addToGauge learning 10 >>
<< advanceTimeBy 1:00 >>
<<else>>
You: On reprend la rédaction ?
Fred_bored: Ouais…
Vous entamez la partie 2 du mémoire. Fred ne semble toujours pas emballé par la production, iel paraît très distrait et peu investi.
Après une heure, l'essentiel de la partie est terminé !
<< addToGauge mentalLoad 15 >>
<< addToGauge learning 5 >>
<< advanceTimeBy 1:00 >>
<<endif>>

<<else>>

<<if !visited("Redaction_Bad")>>
Fred_smiling: Dernière ligne droite ! On la reprend, cette rédaction ?
You: Ouais ! c'est parti !
L'heure suivante, vous vous efforcez de tenir pour la dernière ligne droite de la rédaction. Vous et Fred échangez beaucoup, et avec son aide, vous la terminez avec brio.
<< addToGauge mentalLoad 10 >>
<< addToGauge learning 10 >>
<< advanceTimeBy 1:00 >>
<<else>>
Fred_bored: Bon… J'imagine qu'on doit reprendre la rédaction, maintenant ?
You: Ouais ! On a pas le choix !
L'heure suivante, vous vous efforcez de tenir pour la dernière ligne droite de la rédaction. Une heure pénible et peu productive… Peut-être que si Fred était plus motivé, cela se serait passé autrement…
<< addToGauge mentalLoad 15 >>
<< addToGauge learning 5 >>
<< advanceTimeBy 1:00 >>
<<endif>>
<<endif>>
<<jump Liste_Taches>>
===

title: Redaction_Good
tags: 
position: -781,408
colorID: 2
---
You: Tu as une idée ?
Fred_smiling: J'ai plein d'idées. Tu sais quoi, je me sers du plan pour arranger les idées, et tu mets tout au clair en rédigeant.

<< if !$silence >>
Alors que vous parlez haut et fort, d'autres étudiants à proximité se tournent vers vous d'un air désapprobateur.
<< fx Failure >>
???: Hé, on est à la bibliothèque, là ! Un peu de silence, s'il vous plaît ? Activez une bulle d'immersion, qu'on ne vous entende pas dans toute l'Académie…
<<jump Redaction_Bad>>
<<endif>>

You: Ouais… Je vais tout rédiger, quoi…
Fred_happy: T'inquiète, { $name }… Je rédigerai au moins l'intro, promis !
Vous travaillez d'arrache-pied pour fournir un mémoire dont vous serez tous deux satisfaits. Fred vous donne ses idées, et vous les mettez au propre, formant une chaîne de production efficace.
<< addToGauge mentalLoad 10 >>
<< addToGauge learning 10 >>
<< advanceTimeBy 1:00 >>
<<jump Liste_Taches>>
===

title: Redaction_Bad
tags: 
position: -1001,420
colorID: 2
---
<<if visited("WorkWithoutFred")>>
You: Bon… Alors… J'ai déjà commencé à rédiger nos parties au propre, on va l'utiliser en se servant de notre plan…
Fred_surprised: Attends… T'as commencé la rédaction sans moi ?
You: Ben… Ouais. T'étais pas là !
Fred_sad: Ah bon ?
Fred vous regarde avec une mine désappointée. Iel ne s'attendait sûrement pas à ce que vous travailliez sur le mémoire sans lui.
<<endif>>
You: Bon… On ne va pas en parler trop longtemps. Je vais répartir les tâches en suivant le plan et en m'aidant de nos recherches…
You: Je m'occupe du problème du degré d'erreur des modèles déterministes… et je te laisse réfléchir sur la révolution du paradigme post-darwinien en macro-biologie pour la conclusion ?
Fred_surprised: Je suppose que je fais l'introduction, aussi ?
You: Oui, tu peux ! Je vérifierai ton travail une fois qu'il sera fait. Pour l'instant, à la rédaction !
Fred_sad: Ok, ça marche…

<< hide >>
<< fadeIn >>
<< addToGauge mentalLoad 15 >>
<< addToGauge learning 5 >>
<< advanceTimeBy 1:00 >>
<< fadeOut >>

Au final, vous n'avez pas avancé si vite que cela… Votre partie ressemble à un simple brouillon. Vous allez devoir travailler un peu plus pour les autres…

<<jump Liste_Taches>>
===

title: Exit_Bibli
tags: 
position: -1294,-46
colorID: 1
---
<< fx Notification >>
<< removeDeadline >>
RoomMate: Ding-dong ! Allez, on remballe ses affaires ! La bibliothèque va fermer !

<< if getGauge("learning") >= 50 >>
You: Bon, c'est cool, ça ! On a presque fini !
Fred_happy: Oui, on n'a qu'à relire et peaufiner un peu la conclusion et on est bons !
You: On se voit sur Disboard plus tard ?
Fred_smiling: Ouaip. Allez, courage !
<< hide >>

<<jump Win>>

<< else >>

You: Non, on est loin d'avoir fini !! #line:0c62d43 
Fred_smiling: Mais, non, t'inquiète, ça va aller ! Je m'occupe de l'intro et de la conclusion, comme on a dit. Je te fais confiance pour le reste.
You: Bon… On échange nos rendus sur Disboard, on compile et on valide.
Satisfait de vos résolutions, et ayant confiance en les capacités de Fred, habitué à livrer ses rendus à la dernière minute, vous retournez dans votre chambre.
<< hide >>
<<jump Bedroom_Lose>>
<< endif >>
===

title: Bedroom_Lose
tags: 
colorID: 0
position: -1519,-189
---
<< fadeIn >>
<< setBackground bedroom night >>
<< fadeOut >>

De retour chez vous, vous tentez tant bien que mal de terminer l'édition de votre mémoire.
<< advanceTimeBy 0:30 sleep food >>
Déjà l'heure de compiler votre travail… L'heure de contacter Fred pour mettre en commun vos parties.
Fred@holo_bored: Quel ennui, ce truc… Bon, j'ai à peu près terminé, si tu veux voir.
You: A peu près ?
A votre grand désarroi, c'était un euphémisme de la part de Fred. Son texte n'est pas beau à voir.
Vous faites de votre mieux pour arranger les deux parties entre elles… C'est un peu mieux, mais vous n'êtes vraiment pas satisfait du résultat.
-> Rendre le mémoire
<<if !visited("Assemblee_Sages")>>
Vous auriez pu faire tellement mieux ! Vous envoyez le fichier sur la base de données de l'Académie, quand Fred semble se figer sur place derrière son holo-écran.
<<jump Transition_Assemblee>>
<<else>>
Sapiens_thinking: Fichtre ! Tu n'as pas dû écouter nos conseils pour te planter autant… Souhaites-tu les réécouter ?
-> Réécouter les conseils des Sages
    <<jump Assemblee_Sages>>
-> Recommencer la journée
    << fx Warp >>
    << fadeIn 1500 12ea12 >>
    << resetLevel >>
    <<jump Start>>
<<endif>>
===

title: Win
tags: 
colorID: 0
position: -1864,35
---
<< fadeIn >>
<< setBackground bedroom night >>
<< fadeOut >>

<< visit Win >>

Une fois chez vous, vous prenez un peu de temps pour éditer le mémoire, puis le rendez le mémoire sur le serveur de l'Académie, convaincu de la qualité de votre travail.
<< completeLevel >>
Fred@holo_smiling: C'est pas mal, ce qu'on a fait, non ? Ça mérite bien un petit GTR…
You: Carrément !
<<if !visited("Assemblee_Sages")>>
Tout d'un coup, Fred semble se figer sur place derrière son holo-écran.
<<jump Transition_Assemblee>>
<<else>>
-> Passer au chapitre suivant
-> Réécrire dans le journal
    << showJournal profiles >>
<<endif>>
===

title: Transition_Assemblee
tags: 
colorID: 0
position: -1704,-114
---
<< visit Chapitre4 >>
<< music Mysterious >>
Fred@holo_surprised: Euh… QU'EST CE QUE ??! { $name }, Y A UN VIEUX PERVERS AVEC UN CHAPEAU BIZARRE DANS TA CHAMBRE !!
Vous tournez la tête, terrifié, et constatez avec soulagement que derrière vous se tient…
Sapiens_angry: Il m'a traité de vieux quoi, l'autre morveux ??
You: Heyyy, Maître Sapiens ! Faut pas lui en vouloir, iel pouvait pas s'attendre à vous voir ici !
Sapiens_concerned: Mmoui, j'ai su faire plus discret dans mon temps. Suis-moi, nous avons encore une leçon pour toi !
-> Vous allez lui faire oublier ça, pas vrai ?
    Sapiens_thinking: Non, nous te ferons remonter assez tôt dans le passé pour que cela ne se soit jamais produit.
    You: Mais alors… Comment je peux m'en souvenir, moi ?
    Sapiens_happy: Réserve tes questions pour l'Assemblée, tu veux bien ? En parlant de ça, je t'embarque ! Nous avons encore des choses à t'apprendre.
    <<jump Assemblee_Sages>>
-> Suivre Sapiens
    <<jump Assemblee_Sages>>
-> (Passer la séquence des Sages)
===

title: Assemblee_Sages
tags: noUi
colorID: 1
position: -1913,850
---
<< fx Warp >>
<< fadeIn 1500 12ea12 >>
<< setBackground circle >>
<< music Mysterious >>
<< fadeOut >>
<< visit Assemblee_Sages>>

Sapiens_happy: Tu vois, nous étions impatients de te revoir. #line:02c6d14 
Azul_proud: C'est que derrière ses airs de maître inflexible se cache un cœur tendre. N'est-ce pas, Master Sapiens ? #line:0fc9524 
Sapiens_angry: Tu ne crois pas exagérer, Azul ? #line:0896e67 
Temde_thinking: À peine. Tiens-toi bien, { $name }, aujourd'hui, on attaque un gros morceau : les styles d'apprentissage !
Ledai_happy: Une leçon indispensable pour tout chef de projet en herbe !
-> C'est compliqué, d'être chef de projet…
    You: Ma journée était plutôt éprouvante…
    Sapiens_thinking: C'est normal, { $name }.
-> Les styles d'apprentissage ?
Ledai_explaining: Un bon chef de projet doit connaître ses propres dysfonctionnements ainsi que ceux des autres. Ce qu'ils savent bien faire, ce qu'ils ne savent pas faire.
Sapiens_thinking: Et c'est justement l'objet de cette session : déterminer le profil cognitif de ton partenaire parmi les nôtres.
Azul_happy: Ce qui t'aidera à mieux comprendre le tien. #line:0f445b0 
-> Le profil cognitif ?
    Sapiens_happy: Regarde-nous. Nous sommes tous des Sages, des experts de la connaissance. Nous ne sommes pas pour autant semblables !
    Sapiens_happy: La sagesse et la connaissance ont plusieurs visages. Certes, j'estime que certains sages sont plus experts que… #line:02f5fc7 
    Temde_thinking: Tu vas trop loin là ! #line:03c8108 
    Sapiens_thinking: Disons que nos cerveaux ne sont pas rigoureusement identiques. En bref, nous avons des profils différents.
-> Trouver le profil de Fred
You: D'accord, et comment je trouve le profil de Fred ?
Temde_cheerful: L'un d'entre nous correspond au profil de ton ami Fred. Il ne te reste plus qu'à trouver qui, et surtout pourquoi. #line:0b2b81f  
Azul_proud: C'est ça, le travail collaboratif : comprendre et valoriser les forces vives de chacun.

-> Interroger les Sages  #line:0f7fa48 
    <<jump Questions_Sages>>
===

title: Questions_Sages
tags: noUi
colorID: 0
position: -940,907
---
<< hide >>

-> Interroger Miss Temdé #line:0cb5a6e 
    You: Miss Temdé…
    <<jump QuestionsTemde>>
-> Interroger Master Sapiens #line:00f2b31
    You: Master Sapiens…
    <<jump QuestionsSapiens>>
-> Interroger Ledai #line:041c420 
    You: Ledai…
    <<jump QuestionsLedai>>
-> Interroger Azul #line:09f651a 
    You: Azul…
    <<jump QuestionsAzul>>
-> Révéler quel Sage ressemble à Fred  #line:0319aa8 
    You: Je crois que j'ai trouvé à qui ressemble Fred !
    Sapiens_happy: Nous t'écoutons. #line:04dced3 
    <<jump Trouver_Fred>>
===

title: QuestionsTemde
tags: noUi
colorID: 0
position: -1895,1126
---
<< show Temde >>
-> Demander ce que Temdé aime faire
    You: Qu'est-ce que tu aimes faire le plus ? #line:0d80347 
    Temde_smiling: Agir dans le feu de l'action, se trouver face à un problème concret et devoir trouver une solution. #line:0a8c4db 
    <<jump QuestionsTemde>>
-> Demander ce que Temdé n'aime pas faire
    You: Qu'est-ce que tu n'aimes pas du tout faire ? #line:066fd74 
    Temde_thinking: La théorie, la routine, suivre les consignes de quelqu'un d'autre.  #line:07ded1f 
    <<jump QuestionsTemde>>
-> Demander comment Temdé se définirait
    You: Comment tu te définirais ? #line:09897ab 
    Temde_cheerful: J'aime prendre des risques et mes erreurs m'aident à progresser.
    <<jump QuestionsTemde>>
-> Interroger un autre Sage
    <<jump Questions_Sages>>
===

title: QuestionsSapiens
tags: noUi
colorID: 0
position: -1525,1148
---
<< show Sapiens >>
-> Demander ce que Sapiens aime faire
    You: Qu'est-ce que tu aimes faire le plus ?
    Sapiens_happy: Accumuler des données, des théories, des concepts. #line:0897191 
    <<jump QuestionsSapiens>>
-> Demander ce que Sapiens n'aime pas faire
    You: Qu'est-ce que tu n'aimes pas du tout faire ? #line:066fd74 
    Sapiens_concerned: Qu'on ne me laisse pas le temps de raisonner avant de donner ma réponse. Qu'on me harcèle de questions sans préavis. #line:088bf63 
    <<jump QuestionsSapiens>>
-> Demander comment Sapiens se définirait
    You: Comment tu te définirais ? #line:09897ab 
    Sapiens_thinking: J'ai soif de savoir et d'informations. Plus je sais et plus je maîtrise la moindre situation, le moindre imprévu.  #line:0153123 
    <<jump QuestionsSapiens>>
-> Interroger un autre Sage
    <<jump Questions_Sages>>
===

title: QuestionsLedai
tags: noUi
colorID: 0
position: -1201,1184
---
<< show Ledai >>
-> Demander ce que Ledai aime faire
    You: Qu'est-ce que tu aimes faire le plus ?
    Ledai_happy: Appliquer un process qu'on m'a confié, étape par étape, avec rigueur et discernement. #line:0e39ad0
    <<jump QuestionsLedai>> 
-> Demander ce que Ledai n'aime pas faire
    You: Qu'est-ce que tu n'aimes pas du tout faire ? #line:066fd74 
    Ledai_doubtful: Ne pas avoir de plan d'action préalable, ou ne pas avoir de priorité claire dans ma liste de tâches.  #line:05c6676 
    <<jump QuestionsLedai>> 
-> Demander comment Ledai se définirait
    You: Comment tu te définirais ? #line:09897ab 
    Ledai_explaining: Je suis une machine de précision. J'aime agir selon un protocole que j'ai eu le temps de valider. #line:0aa9a21 
    <<jump QuestionsLedai>> 
-> Interroger un autre Sage
    <<jump Questions_Sages>>
===

title: QuestionsAzul
tags: noUi
colorID: 0
position: -892,1220
---
<< show Azul >>
-> Demander ce qu'Azul aime faire
    You: Qu'est-ce que tu aimes faire le plus ?
    Azul_happy: J'aime réfléchir, discuter avec les autres, imaginer une solution.  #line:0b109d5 
    <<jump QuestionsAzul>> 
-> Demander ce qu'Azul n'aime pas faire
    You: Qu'est-ce que tu n'aimes pas du tout faire ? #line:066fd74 
    Azul_sad: Perdre du temps à rédiger ou à mettre sur papier un plan bien propre sur soi qui découle d'une problématique bien creusée, dérouler un raisonnement sans donner tout de suite mon opinion. #line:0e75db1
    <<jump QuestionsAzul>> 
-> Demander comment Azul se définirait
    You: Comment tu te définirais ? #line:09897ab 
    Azul_proud: Je suis un créatif, un intuitif, j'aime déceler une piste que personne n'a encore explorée.
    <<jump QuestionsAzul>> 
-> Interroger un autre Sage
    <<jump Questions_Sages>>
===

title: Trouver_Fred
tags: noUi
colorID: 0
position: -575,926
---
-> C'est vous, Master Sapiens  #line:0e8379b 
    <<jump SapiensNo>>
-> C'est vous, Miss Temdé  #line:0f5d309 
    <<jump TemdeNo>>
-> C'est vous, Azul  #line:0c550df 
    <<jump AzulYes>>
-> C'est vous, Ledai  #line:0cb251b 
    <<jump LedaiNo>>
-> Interroger les Sages
    <<jump Questions_Sages>>
===

title: SapiensNo
tags: noUi
colorID: 0
position: -626,1181
---
<< fx Failure >>

Sapiens_angry: Sûrement pas ! Contrairement à ton ami et aux apparences, je ne suis pas qu'une boîte à idées. 
Sapiens_angry: Je t'aurais transmis une bibliographie étendue, le plan de travail et un premier jet de l'introduction et de la conclusion. Avec moi, tu aurais eu le squelette de ton mémoire dès la première heure… 

->  Une autre idée?  #line:0d9274c 
    <<jump Trouver_Fred>>
===

title: TemdeNo
tags: noUi
colorID: 0
position: -308,922
---
<< fx Failure >>

Temde_thinking: Pas loin. Mais ton ami et moi, nous ne sommes pas si proches. Moi, j'adore le concret, me servir de mes mains. C'est bien pour cela que j'en ai quatre !… Cherche encore. #line:0de3710 

->  Une autre idée ?
    <<jump Trouver_Fred>>
===

title: LedaiNo
tags: noUi
colorID: 0
position: -380,1192
---
<< fx Failure >>

Ledai_doubtful: Hélas non. Ton ami et moi sommes opposés à presque tous les niveaux. Ce Fred a l'esprit chaotique, qui ne cesse de bouillir et de se disperser. Iel préfère le monde des hypothèses plutôt que la confrontation mécanique ou schématique.
Ledai_doubtful: Iel préfère observer et discuter avec tout le monde que le démontage méthodique d'un problème technique.  #line:0285458 

-> Une autre idée ?
    <<jump Trouver_Fred>>
===

title: AzulYes
tags: noUi
colorID: 0
position: -116,1177
---
<< fx Success >>
<< music Victory >>

Azul_happy: Bravo ! Tu as trouvé ! Je suis imaginatif et intuitif, comme ton ami Fred. Je suis capable de voir ce que les autres ne peuvent voir. #line:01ba3b3 
Azul_sad: Cependant, j'ai comme faiblesse de manquer de discipline. Je me disperse, je peine à développer et à écrire noir sur blanc toutes mes réflexions.
<<jump Azul_FreeQuestions>>
===

title: Azul_FreeQuestions
tags: noUi
colorID: 0
position: -116,1177
---
<< completeSages >>
-> Alors, comment motiver quelqu'un comme Fred ? #once
    Azul_happy: Comme tout le monde, Fred et moi, nous avons nos faiblesses. #line:0a39098 
    Azul_sad: En ce qui me concerne, je perds facilement mes moyens quand il s'agit de mettre par écrit mes idées et mes arguments. Je peine à respecter un plan rigide et méthodique, c'est fastidieux et démotivant. #line:07b9cd1 
    Azul_happy: J'ai besoin de donner mon avis, d'échanger avec les autres, de confronter mon ressenti avec le leur, ça m'aide à fixer mon jugement.
    Azul_proud: Pour guider quelqu'un comme nous, il faut nous donner des idées à explorer, de quoi échanger.
    Ledai_happy: Il faut le guider pas-à-pas jusqu'à la fin du projet, en lui demandant son avis verbalement. Iel te fournira des idées, des pistes de réflexion, et tu n'auras plus qu'à trier.
    Temde_happy: Sans oublier de le solliciter et de lui rapporter ce que tu as trouvé, verbalement aussi ! Et de lui demander ce qu'iel a noté ou retenu. C'est comme ça qu'un esprit divergent fonctionne.
    You: Un esprit "divergent" ?! #line:00adb08 
    Azul_proud: C'est ce que nous sommes, Fred et moi.
    <<jump Azul_FreeQuestions>>
-> Et moi, c'est quoi, mon profil ? #once
    You: Et moi ? Je ressemble à quel sage ?
    Azul_cheerful: À toi de trouver. N'oublie pas de le noter dans ton Journal Mental quand tu nous quitteras !
    You: J'y penserai !
    <<jump Azul_FreeQuestions>>
-> J'ai compris (Quitter les sages)
    You: Je crois que j'ai compris la leçon d'aujourd'hui. Bonne nuit, les Sages !
    << hide >>
    <<jump Journal>>
===

title: Journal
tags: noUi
colorID: 0
position: -1002,1672
---
<< fx Warp >>
<< fadeIn 1500 12ea12 >>
<< setBackground bedroom night >>
<< fadeOut >>

Vous voilà encore dans cette parenthèse de douce pénombre maternelle… Vous êtes au calme, seul avec vous-même. Ou presque. Vous avez un compagnon qui ne vous quitte jamais : votre journal mental.
<< completeJournal >>
<<if !visited("Win")>>
-> Recommencer la journée
    << fx Warp >>
    << fadeIn 1500 12ea12 >>
    << resetLevel >>
    <<jump Start>>
<<else>>
-> Passer au prochain chapitre
<<endif>>
===