#gauges:learning,sleep,food,mentalLoad

title: Start
tags:
position: -1632,-461
colorID: 8
---
<< fadeIn 1 >>
<< setTime 8:00 3 >>
<< set $silence to false >>
<< setBackground bedroom >>
<< setGauge learning 0 >>
<< setGauge sleep 100 >>
<< setGauge food 100 >>
<< setGauge mentalLoad 0 >>
<< showGauges >>
<< music Neutral >>
<< fadeOut >>

Après vous être réveillé en douceur, vous prenez votre petit déjeuner tranquillement en attendant l'appel de Fred.
Aujourd'hui, vous devez travailler en binôme pour rédiger un mémoire… Sur quoi, déjà ?
Ah, oui ! La théorie du chaos dans la macrobiologie post-darwinienne. C'est compliqué, comme sujet, il vaut mieux être à deux pour l'aborder !
Quelque chose cloche… Fred ne vous a toujours pas appelé ! Ça ne lui ressemble pas…
-> Appeler Fred sur son Palmphone
    << loopFX PhoneRing_LOOP 5000 >>
    …
    << stopFX PhoneRing_LOOP 5000 >>
    Fred@holo_excited: Yooo, c'est Fred !
    You: Ah, Fred, tu es là ! Pourquoi tu m'as pas…
    Fred@holo_smiling: Je suis pas dispo pour le moment, mais si vous voulez, laissez-moi un message…
    Fred@holo_happy: …Je le lirai sûrement pas, mais rien ne vous empêche d'essayer !
    You: Super… Ça m'avance bien, tout ça !
-> Faire autre chose

Vous vous étiez mis d'accord pour aller travailler sur le mémoire à la bibliothèque de l'Académie… Tout seul, qu'est-ce que vous allez faire ?
<< fadeIn >>
<<jump Actions_List>>
===

title: Actions_List
tags: freechoice
position: -1210,-459
---
<< setBackground bedroom >>
<< fadeOut >>

<<if isTimeOver("10:30")>>
<<jump Fred_Call>>
<<else>>
-> Cuisine@cuisine #line:0717775 
    <<jump cuisine_subchoice>>
-> Porte@porte #line:0d21566 
    <<jump porte_subchoice>>
-> Bureau@bureau #line:048f567 
    <<jump bureau_subchoice>>
-> Lit@lit #line:03dbf5c 
    <<jump lit_subchoice>>
-> Salon@salon #line:0645318 
    <<jump salon_subchoice>>
<<endif>>
===

title: lit_subchoice
tags: subchoice
position: -1229,-636
---
<< fadeIn >>
<< setBackground bedroom bed >>
<< fadeOut >>
-> Se recoucher en attendant Fred (1h30)
    <<jump Sleep_Morning>>
-> back
    <<jump Actions_List>>
===

title: porte_subchoice
tags: subchoice
position: -701,-342
---
-> Aller chez Fred
    <<jump ChezFred>>
-> Aller seul à la bibliothèque
    <<jump Bibli_soloAM>>
-> back
    <<jump Actions_List>>
===

title: cuisine_subchoice
tags: subchoice
position: -1358,-576
---
<< fadeIn >>
<< setBackground bedroom kitchen >>
<< fadeOut >>
-> Casser la croûte (30 min) #once
    <<jump Lunch>>
-> back #line:0fad580 
    << fadeIn >>
===

title: bureau_subchoice
tags: subchoice
position: -971,-604
---
<< fadeIn >>
<< setBackground bedroom desk >>
<< fadeOut >>
-> Contacter Fred sur son compte Disboard #once
    <<jump Fred_Disboard>>
-> Travailler sur le mémoire tout seul #once
    <<jump Memoire_Home>>
-> back #line:0a06fb6 
    << fadeIn >>
    <<jump Actions_List>>
===

title: salon_subchoice
tags: subchoice
position: -874,-487
---
<< fadeIn >>
<< setBackground bedroom chill >>
<< fadeOut >>
-> Attendre Fred en  regardant Star Mars #once
    <<jump TV>>
-> back #line:0341673 
    <<jump Actions_List>>
===

title: Fred_Disboard
tags: 
colorID: 0
position: -816,-724
---
<< visit FredCall >>
Vous vous installez sur votre poste et mettez en route l'application Disboard. Vous constatez avec surprise que Fred est connecté, et vous lancez sans tarder un appel holo.
Fred@holo_surprised: { $name }… Il faut que je te dise quelque chose…
You: Quoi ?! Qu'est-ce qu'y a, Fred ? Tu vas bien ??
Fred@holo_surprised: Il s'est passé un truc fou ce matin…
You: Accouche !!! QU'EST-CE QU'IL S'EST PASSÉ, FRED ??
Fred@holo_excited: J'AI REÇU… LE DERNIER OPUS… DE GRAND THRILL RODEO !!! JE SAIS, C'EST INCROYABLE !
-> FRED !! Tu m'as fait peur !
    You: Ça va pas, la tête ?! Tu m'as fait peur !
    Fred@holo_happy: Quoi ? Ahhh, t'as essayé de m'appeler… J'étais en plein dans le jeu ! J'en suis au chapitre 4 !
    You: Ouais, super. J'en ai pas grand chose à faire, en fait.
-> Ça nous aidera pas à finir le mémoire…
    Fred@holo_happy: Ah ouais… C'est vrai, tu m'as appelé… J'étais en plein dans le jeu ! J'en suis au chapitre 4 !
    You: Ouais, super. J'en ai pas grand chose à faire, en fait.
Fred@holo_excited: Tu te rends pas compte ! Le dernier opus était  il y a 36 ans, { $name } ! Ça se jouait encore à la MANETTE à l'époque ! Tu te souviens des "manettes" ??
You: Euh… Ouais, je crois. Bref, on a rendez-vous à la bibliothèque, tu te souviens ?
Fred@holo_happy: Euh… Ouais, on fera ça plus tard ! Pour l'instant, il FAUT que tu viennes chez moi, on va tester le mode multi !
You: On finira jamais à temps si on fait comme ça !
Fred@holo_happy: C'est bon, viens ! On a le temps !
-> Bon, je veux bien venir chez toi, mais pas pour jouer !
    You: Bon, je viens te chercher, mais pas pour traîner sur GTR !
    Fred@holo_bored: Ouais… Comme tu veux…
-> Non, on se retrouve à la bibliothèque !
    You: Non, je vais à la bibliothèque pour bosser, on se rejoint là-bas directement !
    Fred@holo_bored: Pfff… Comme tu veux… Mais je finis le chapitre d'abord…
<<jump Actions_List>>
===

title: Sleep_Morning
tags: 
colorID: 0
position: -1244,-900
---
<< loopFX Sleep_LOOP >>
<< addToGauge sleep 40 >>
Pas grave… Si Fred ne veut pas travailler, alors c'est autant son problème que c'est le vôtre. Vous vous recouchez. Au moins, vous serez plus frais pour la journée !
<< stopFX Sleep_LOOP >>
<< advanceTimeBy 1:30 sleep food >>
<<jump Actions_List>>
===

title: Lunch
tags: 
colorID: 0
position: -1423,-748
---
<< fx EatCook >>
Un peu de gelée vitaminée en complément du petit-déjeuner de ce matin ne vous ferait pas de mal…
<< addToGauge food 40 >>
…Cet en-cas supplémentaire a ravi votre estomac.
<< stopFX EatCook >>
<< advanceTimeBy 0:30 food >>
<<jump Actions_List>>
===

title: TV
tags: 
colorID: 0
position: -675,-613
---
Vous allumez votre télé holographique en attendant Fred… Après tout, vous n'avez pas à porter toute la responsabilité tout seul…
<< show lightsaber@holo >>
<< loopFX TVStarwars_LOOP 10000 >>
Aujourd'hui, pas d'épisode de votre show favori, mais à la place un dessin animé dérivé… Kids Mars…
C'est à peu près la même chose que le matériel original, mais sans violence ni une trace de sang.
<< advanceTimeBy 2:00 >>
Pas très intéressant, donc. Mais au moins, cela vous vide la tête.
<< addToGauge mentalLoad 40 >>
<< stopFX TVStarwars_LOOP >>
<<jump Actions_List>>
===

title: Memoire_Home
tags: 
colorID: 0
position: -1069,-766
---
<< visit WorkWithoutFred >>
Convaincu que Fred vous a lâché, vous entamez le premier jet du mémoire. Le sujet est incroyablement complexe et vous avez du mal à structurer votre pensée sans l'aide des notes de Fred.
<< addToGauge learning 10 >>
<< addToGauge mentalLoad 20 >>
Peu satisfait, vous pliez bagage et vous posez à nouveau votre regard sur votre chambre d'étudiant.
<< advanceTimeBy 2:00>>
<<jump Actions_List>>
===

title: ChezFred
tags: 
colorID: 0
position: -605,-425
---
<< fadeIn >>
<< setBackground bedroom_fred >>
<< fadeOut >>

Fred vous fait entrer à la première sonnerie. #line:024dd08 

Fred_excited: Aaaah, { $name } ! Juste à temps pour tester le nouveau GTR !!
You: Je suis pas venu jouer, Fred… On a le mémoire, rappelle-toi…
Fred_happy: Ouaiiiiis, je sais… T'inquiètes, j'ai quasiment tout le plan en tête ! C'est comme si c'était déjà rédigé, en fait !
You: On doit se rendre à la bibliothèque avant qu'elle ferme…
Fred_smiling: Ouais, ouais, mais avant ça, j'ai un chapitre 4 à finir !
You: Moi aussi…
-> Rester jouer avec Fred
    You: Bon, ok… T'as gagné, je te rejoins en multi…
    Fred_excited: Yeeeesssss !! Allez, regarde, là, on doit démanteler le cartel du terrible Dr. Stronghold…
    << advanceTimeBy 1:30 >>
    Vous passez un bon moment à descendre des gangsters avec votre pote, mais le temps file…
    << removeFromGauge mentalLoad 30 >>
    Fred_happy: C'était génial, non ? Allez, on fera le boss une prochaine fois, si tu veux.
    You: Maintenant, on doit finir ce satané mémoire !
    <<jump Bibli_binome>>
-> Insister pour que Fred vienne
    You: Non, Fred, trop, c'est trop ! On va à la bibliothèque, maintenant.
    << addToGauge mentalLoad 10 >>
    Fred_bored: Bon, t'as gagné… On va le faire vite fait, bien fait, ce devoir.
    <<jump Bibli_binome>>
===

title: Bibli_soloAM
tags: 
colorID: 0
position: -705,-208
---
<< fadeIn >>
<< setBackground library >>
<< advanceTimeBy 0:30 >>
<< fadeOut >>

Au lieu d'attendre bêtement Fred, vous vous rendez dans cette immense et riche bibliothèque de l'Académie.
Vous lui précisez par message dans quelle partie du bâtiment vous vous trouvez, puis déballez vos affaires.
Cet immeuble très ancien modernisé vous inspire autant qu'il vous accable.
Que faire en attendant ?
-> Commencer à travailler sur le mémoire << if !visited("WorkWithoutFred") >>
    << visit WorkWithoutFred >>
    Tant pis pour Fred, il n'avait qu'à être là ! Vous entamez le premier jet du mémoire.
    << advanceTimeBy 2:00 >>
    Le sujet est incroyablement complexe et vous avez du mal à structurer votre pensée sans l'aide des notes de Fred.
    << addToGauge learning 10 >>
    << addToGauge mentalLoad 20 >>
-> Etoffer ses recherches sur la macrobiologie
    Vous décidez de ne pas entamer la rédaction avant que Fred ne soit là, et préférez étoffer vos recherches en attendant.
    << visit MoreResearch >>
-> Attendre Fred en écoutant de la musique
    Vous décidez d'attendre Fred pour commencez, et de vous détendre en écoutant un peu de musique.
    You: Hey, BibliMate ! Crée une bulle sonore autour du bureau.
    RoomMate: Bien entendu. Une requête musicale ?
    -> Un bon vieux morceau rock des Frosty Mankees !
        You: Un petit Frosty Mankees est de mise !
        RoomMate: Entendu !
        << music Solo1 >>
        << advanceTimeBy 2:00 >>
        Le morceau mêle des motifs de basse et guitare qui se répètent sans cesse, accompagnant avec merveille la voix d'Alec Burner. Un classique !
        << removeFromGauge mentalLoad 20 >>
        Vous vous sentez détendu et revigoré par cet interlude musical.
        << music Neutral >>
    -> Un mix Xéno-Techno Bass-Boosted de EarsKiller !
        RoomMate: Entendu !
        << music DanceQuiet >>
        << advanceTimeBy 2:00 >>
        Les basses frénétiques et hypnotisantes de EarsKiller vous prennent aux tripes et vous mettent dans un état de transe hypnotique.
        << addToGauge sleep 20 >>
        Vous vous sentez énergisé et prêt à tout donner.
        << music Neutral >>
Vous levez les yeux pour enfin voir Fred !
<< show fred >>
Fred_happy: Yo !! Bon... Je peux officiellement dire que le nouveau GTR est encore mieux que l'ancien !!
You: Bon... Maintenant que tu es enfin là, on pourrait se mettre au travail !... La bibliothèque ferme à 20h.
Fred_smiling: T'inquiète, j'ai tout dans la tête, ça va aller super vite.
You: On va voir ça...
<<jump Liste_Taches>>
===

title: Bibli_binome
tags: 
colorID: 0
position: -442,-200
---
<< fadeIn >>
<< setBackground library >>
<< fadeOut >>

Fred_happy: Hey, quand on a fini, on pourra se faire un petit GTR, tu crois pas ?
-> Si ça te fait plaisir...
    You: Oui, si on a encore le temps. Au pire, on pourra faire ça un autre jour !
    Fred_excited: Yes ! Bon alors, on s'y met ?
-> Arrête avec ton GTR !
    You: Bon sang mais Fred, calme-toit avec GTR ! Tu pourras y jouer plus tard !
    Fred_bored: Pffff... Ouais, t'as sûrement raison...
Fred_smiling: Bon, on s'y met ? C'est toi le chef de projet, non ? Alors, on fait quoi ?
<< hide >>
<<jump Liste_Taches>>
===

title: Liste_Taches
tags:
position: -870,-52
colorID: 1
---
<< if !isTimeOver("20:00") >>
-> Travailler sur le mémoire...
    <<jump Travail_Memoire_subchoice>>
-> Faire une pause... <<if $silence == false>>
    <<jump Pause_subchoice>>
-> Générer une bulle d'immersion studieuse <<if $silence == false>>
    <<jump Zero_Distraction>>
-> Désactiver le mode immersion studieuse <<if $silence == true>>
    <<jump Desactiver_Zero_Distraction>>
<<else>>
    <<jump Exit_Bibli>>
<< endif>>
===

title: Travail_Memoire_subchoice
tags: subchoice
position: -914,77
colorID: 1
---
-> Montrer vos notes à Fred #once #line:0611eb4 
    <<jump Notes_You>>
-> Demander à Fred ses notes #once #line:0ead95b 
    <<jump Notes_Fred>>
-> Rédiger le mémoire <<if visited("Plan")>> #once
    <<jump Redaction>>
-> Valider le plan du mémoire #once
    <<jump Plan>>
-> back
    <<jump Liste_Taches>>
===

title: Pause_subchoice
tags: subchoice
position: -497,-21
colorID: 1
---
-> Pause Déjeuner (1h) #once
    <<jump Pause_Lunch>>
-> Pause Café (30 min) #once
    <<jump Pause_Coffee>>
-> back
    <<jump Liste_Taches>>
===

title: Pause_Lunch
tags: 
colorID: 0
position: -968,-254
---
You: T'as pas le ventre creux, par hasard ?
Fred_excited: Ah, enfin ! Tu peux pas savoir à quel point j'attendais que tu dises ça.
You: Bon, et bien dans ce cas, cap sur la cafétéria !
Fred_happy: Ça nous fera une pause !

<< hide >>
<< fadeIn >>
<< setBackground cafeteria >>
<< fadeOut >>

Vous commandez un déjeuner à la cafétéria. Autour de vous, les discussions des autres élèves portent soit sur les cours, soit sur le boss final du nouveau GTR...
<< fx EatCook >>

<< addToGauge food 40 >>

Fred_angry: Hé ! NO SPOIL !! On a pas fini le jeu, nous autres !
Vous riez quand vous observez Fred engloutir son repas en une bouchée et tourner le dos furieusement au groupe d'étudiants qui parlaient du jeu pour allumer sa console portable.
<< removeFromGauge mentalLoad 20 >>
<< advanceTimeBy 1:00 >>

<<if !visited("Leave_Pause")>>
<<jump Leave_Pause>>
<<else>>
Bon... C'est reparti pour le travail...
<<jump Liste_Taches>>
<<endif>>
===

title: Pause_Coffee
tags: 
colorID: 0
position: -968,-254
---
You: Un petit café pour se remonter ?
Fred_surprised: Je savais pas que tu buvais du café...
You: Pas d'habitude, mais là, je suis crévé. On y va ?
Fred_happy: Allez !

<< hide >>
<< fadeIn >>
<< setBackground cafeteria >>
<< fadeOut >>

<< addToGauge sleep 20 >>
<< fx EatCook >>
Vous prenez votre café synthétique, pendant que Fred joue à GTR sur sa console portable. La boisson pleine de caféine vous remplit d'énergie pour la suite.
Pas une solution à long terme, certes, mais au moins, ça vous donne de l'avance sur la journée !
<< advanceTimeBy 0:30 sleep >>

<<if !visited("Leave_Pause")>>
<<jump Leave_Pause>>
<<else>>
Bon... C'est reparti pour le travail...
<<jump Liste_Taches>>
<<endif>>
===

title: Leave_Pause
tags: 
colorID: 0
position: -968,-254
---
<< visit Leave_Pause >>
You: Fred, on y retourne, j'ai besoin de toi.
Fred_excited: Yes, yes, attends 5 minutes, encore.

-> Bon, ok. Passe, on joue en multi.
    You: Bon, dans ce cas, passe au moins un module mental, que je joue avec toi !
    Fred sourit et vous tend ce petit dispositif coloré permettant de contrôler le personnage, que vous accrochez sur votre tempe.
    << removeFromGauge mentalLoad 20 >>
    C'est sympa de jouer les gangsters mais le temps passe ! Vous retournez à la bibliothèque pour travailler.
    << advanceTimeBy 1:00 >>
-> Insister pour que Fred vienne avec vous  #line:071a93f 
    You: Fred, en tant que chef d'ouvrage, j'ai besoin de ton esprit créatif ! Viens !
    Fred_smiling: Bon... Ok... J'imagine que le boss attendra.

<< fadeIn >>
<< setBackground library >>
<< fadeOut >>
<<jump Liste_Taches>>
===

title: Redaction
tags: 
colorID: 0
position: -893,282
---
You: On se met à la rédaction du mémoire ?
Fred: Yes. On se répartit la tâche comment ? #line:005280e 

<<visit Redaction>>

-> Demander l'avis de Fred  #line:06e222c 
    <<jump Redaction_Good>>
-> Dire à Fred quoi faire et l'assister
    <<jump Redaction_Bad>>
===

title: Redaction_Good
tags: 
position: -781,408
colorID: 2
---
Vous prenez le temps de lui résumer oralement ce que vous avez déjà sur la question des modèles chaotiques, déterministes dont le degré d'erreur croît de manière exponentielle. Votre présentation n'ennuie pas Fred, au contraire, votre pote se trouve être une source inépuisable d'idées et de référence pour étayer cette partie. #line:0d6c894 
Vous enchaînez avec la portée de l'effet papillon et l'imprévisibilité des systèmes dynamiques à long terme. Là encore, échanger avec Fred vous permet de consolider vos arguments et de donner plus de profondeur à cette question. #line:0863687 
Enfin, vous énumérez brièvement les éléments que vous avez déjà intégrés dans la troisième partie sur la macrobiologie et la remise en question du postulat darwinien de l'évolution comme un schéma linéaire et donc prévisible. Fred se montre encore généreux et vous propose des réflexions, des idées, des hypothèses auxquelles vous n'aviez pas pensées. #line:0a38763 
Au final, vous constatez qu'avec le concours de Fred, votre rédaction s'est considérablement étoffée. Elle est vraiment solide et bien charpentée. Il ne vous reste plus qu'à arranger votre démonstration de manière plus académique et plus littéraire et le tour sera joué.  #line:0d7870c 
Alors que vous terminez de noter un truc dans votre agenda, l'IA vous informe qu'elle a enregistré toute la conversation. Elle vous expédie le fichier audio sur votre ordinateur. Parfait ! Si vous avez oublié un truc, vous n'aurez qu'à réécouter l'enregistrement… #line:0399372 
<< advanceTime 3:30 >>
<< addToGauge learning 20 >>

<<jump Liste_Taches>>
===

title: Redaction_Bad
tags: 
position: -1001,420
colorID: 2
---
You: Je m'occupe du problème des modèles déterministes dont le degré d'erreur croît de manière exponentielle. Je prends également en charge l'effet papillon et l'imprévisibilité des systèmes dynamiques à long terme. Je te laisse réfléchir sur la révolution du paradigme post-darwinien en macrobiologie pour la conclusion ? #line:0a59d4a 
Fred_smiling: OK. Et qui fait l'introduction ? #line:03a4d80 
You: Toi, par exemple. N'oublie pas le problème à 3 corps de Poincaré dans l'introduction. Je te demande pas d'écrire beaucoup, non ? #line:0fff9aa 
Fred_sad: Non, c'est vrai. #line:0699c86 
You: Cool. #line:0da4c78 
Vous reprenez vos brouillons et vos notes et vous commencez à mettre au propre vos idées. Vous vous voulez bien faire les choses. Vous prenez le temps de contrôler régulièrement vos sources et de réutiliser dans votre argumentation les données que vous avez glanées.  #line:0ce4d55 

<< hide >>
<< fadeIn >>
<< advanceTime 3:00 >>
<< addToGauge learning 10 >>
<< fadeOut >>

Quand vous relevez la tête, vous voyez Fred, qui vous sourit. #line:0256ae6 
Fred_smiling: J'ai plein d'idée pour la conclusion. Et pour l'introduction aussi. #line:0bf97db 
You: Noon ! #line:0feaaae 
Manifestement, ce n'était pas le meilleur choix possible. #line:0583471 
<<jump Liste_Taches>>
===

title: Zero_Distraction
tags: 
colorID: 0
position: -516,177
---
You: Hey, BibliMate, active le mode Immersion Studieuse !
RoomMate: C'est parti ! Une sphère de travail immersive, une !
<<if !visited("Zero_Distraction")>>
Aussitôt, un dôme de lumière opaque se referme sur votre table et vous isole du reste de la bibliothèque et des étudiants qui y sont. Le son devient net et réverbéré, comme dans un studio d'enregistrement.
Fred_smiling: Bonne idée, la bulle de silence. On va pouvoir parler comme on veut tous les deux !
You: Bon, on reste concentré, Fred. On doit finir la rédaction du mémoire aujourd'hui. #line:0f7cd07 
Fred_happy: Ouais…  Et si je te montrais ce qu'on peut faire dans le dernier GTR...
You: Fred, on reste concentré sur le sujet du mémoire. De toute manière, ça ne marcherait pas. Nos connexions sont limitées aux sites académiques. #line:0056a1d 
<<endif>>

<< set $silence to true >>
<< visit Zero_Distraction>>
<<jump Liste_Taches>>
===

title: Desactiver_Zero_Distraction
tags: 
colorID: 0
position: -516,177
---
You: Hey, BibliMate, désactive le mode immersif, s'il te plaît !
RoomMate: Pas de souci ! 
<< set $silence to false >>
<<jump Liste_Taches>>
===

title: Notes_You
tags: 
colorID: 0
position: -611,270
---
<< advanceTime 1:00 >>

You: Bon, on passe mes notes en revue ?
Fred_smiling: Yeeees. Je t'écoute ! #line:0a51cb6 

<<visit Notes_You>>

<< if $silence == false >>
You: Non, mais je ne peux pas te lire à haute voix, on est à la bibliothèque ! #line:08e64e8 
<< endif >> 

-> Vous lui lisez vos notes << if $silence == true >> #line:0bfb9d2 
    << jump Notes_You_Good >>
-> C'est à lui de lire vos notes #line:01f3a4c 
    << jump Notes_You_Bad >>
===

title: Notes_You_Good
tags: 
position: -341,404
colorID: 2
---
You: Grace à la bulle immersive, on va pouvoir faire ça à l'oral ! Cool, non ?
Fred_smiling: Super ! Vas-y, je t'écoute !
Ne sachant pas par quoi commencer, vous dites la première remarque qui vous vient en tête. Et puis, de fil en aiguille, une remarque en amène une autre, et ainsi de suite.
Fred est un auditeur très bienveillant, il rebondit sur certaines de vos idées et vous incite à les clarifier, les critiquer, les nuancer.

<< advanceTime 1:00 >> 
<< addToGauge learning 20 >>

RoomMate: Voulez-vous que je conserve le fichier audio de la conversation pour en garder la trace ?
Fred_happy: Vas-y, BibliMate !

<<jump Liste_Taches>>
===

title: Notes_You_Bad
tags: 
position: -545,441
colorID: 2
---
You: On va faire ça par écrit, ok ?
Fred_surprised: Euh... Je sais pas si c'est la meilleure façon de faire...
You: Je suis le chef de projet, ou pas ?
Fred_sad: Oui, chef… #line:0c5e7f6 

Vous envoyez vos documents de travail à Fred. Vous voyez votre ami grincer des dents. #line:0b9fc4e 
Il peine à lire et relire vos docs, et n'arrête pas de faire des pauses, de vous interpeller, mais vous limitez les discussions pour vous concentrer sur le travail.
<< advanceTime 1:00 >>
<< addToGauge learning 10 >>
<<jump Liste_Taches>>
===

title: Exit_Bibli
tags: 
position: -2114,133
colorID: 1
---
<< if getGauge("learning") > 66 >>

<< fx Notification >>
Ding Ding Dong. L'horloge officielle de la bibliothèque de l'Académie d'Omniscience et de Polytechnologie Universelle vient de sonner pour la dernière fois. Les robots vous font signe de remballer vos affaires et de quitter les lieux. C'est à regret que Fred et vous, sortez de l'établissement. #line:00f03b9 
You: Non, on a presque fini. #line:0e613d9 
Fred_happy: Cool, tu as tout dans les mains : le plan, les idées fortes. Tu n'as plus qu'à développer tout ce qu'on a dit. No worry, ça va le faire, je te fais confiance. #line:06a417f 
You: Et toi ? #line:0c6c678 
Fred_happy: Je m'occupe de la conclusion et de l'intro. #line:0e56f3e 
You: OK. #line:02bda36 
Fred_smiling: Keep faith. #line:031f90d 
<< hide >>
C'est drôle. C'est Fred désormais qui vous pilote et vous rassure sur votre capacité à finir dans les temps. Troublé, un peu hésitant, vous retournez dans votre chambre. #line:03fa023 

<<jump Bedroom>>

// Sinon, joueur n'a pas réussi toutes ses conditions de victoire
<< else >>
<< fx Notification >>

Ding Ding Dong. L'horloge officielle de la bibliothèque de l'Académie d'Omniscience et de Polytechnologie Universelle vient de sonner pour la dernière fois. Les robots vous font signe de remballer vos affaires et de quitter les lieux.  #line:07ecec4 
You: Non, on est loin d'avoir fini !! #line:0c62d43 
Vous criez, vous paniquez complètement, vous vous mettez à suer à grosses gouttes, votre vue se trouble, votre ouïe sature, parasité par un effet de larsen. Vous avez encore échoué !  #line:04492ae 
-> Vous allez devoir recommencer cette journée épuisante à la bibliothèque #line:07dace0 
    << resetLevel >>
    << hide >>
    << jump Start>>

<< endif >>
===

title: Notes_Fred
tags: 
colorID: 0
position: -1211,149
---
<< advanceTime 2:00 >>

You: Je sais que toi aussi, tu as avancé sur le travail de rédaction du mémoire. #line:01b1dcd 
Fred_smiling: Bien sûr !
You: Tu as tes notes avec toi ? #line:0bf6862 
Fred_happy: Tu veux pas plutôt qu'on en parle de vive voix ? #line:0a57c1c 

<< if $silence == false >>
You: Heu… C'est plus facile pour moi de les lire. Surtout qu'on est à la bibliothèque, il ne faut pas faire du bruit. #line:0f34e21 
<< endif >> 

<<visit Notes_Fred>>

-> Écouter Fred présenter ses notes <<if $silence == true >> #line:031034a 
    << jump Notes_Fred_Good >>
-> Demander à Fred de vous envoyer ses notes écrites #line:0b165d9 
    << jump Notes_Fred_Bad >>
===

title: Notes_Fred_Good
tags: 
position: -1456,140
colorID: 2
---
Fred_happy: Tout est dans ma tête. Tu veux que je commence par quoi ? #line:0ca8f0f 

<< advanceTime 1:00 >>
<< addToGauge learning 20 >>

Vous comprenez que c'est à vous de questionner Fred et, en quelque sorte, de lui tirer les vers du nez. Vous passez les deux heures suivantes à lui poser des questions, à écouter ses réponses, à rebondir sur ses idées, ses commentaires. 
Fred a beau ne pas répondre aux standards académiques, il a un don, c'est certain, il a remarqué plein de choses que vous n'auriez pas vues.
Quand vous pensez avoir fait le tour de la question avec lui, l'IA vous informe qu'elle a enregistré toute la conversation. Elle vous transfère le fichier audio sur votre ordinateur. Génial ! #line:0b03dfd 

<<jump Liste_Taches>>
===

title: Notes_Fred_Bad
tags: 
position: -1349,229
colorID: 2
---
Fred pianote sur son ordinateur, l'instant d'après, vous réceptionnez ses documents… Il n'y a pas grand chose d'exploitable... Que des bouts de phrases, de mots et quelques commentaires en annexe.
You: Je sais pas trop quoi faire de ça, Fred... Tu peux reprendre ton travail, s'il te plaît ?
Fred: Bon, d'accord. Puisque tu insistes. C'est bien parce que c'est toi… #line:0b3e63f 
Vous voyez à son air pincé que Fred se fait violence pour vous faire plaisir. Après un bon moment, Fred, vous renvoie les fichiers mis à jour. #line:0d827ea 
Fred: J'ai fait un effort. J'ai écrit des vraies phrases. #line:058646a 
En effet, c'est mieux, mais ça reste laconique. Il reste encore beaucoup à faire mais vous n'obtiendrez pas plus de Fred de cette manière. #line:0057664 
<<advanceTime 1:00>>
<< addToGauge learning 10 >>

<<jump Liste_Taches>>
===

title: Plan
tags: 
colorID: 0
position: -1139,276
---
You: Tu te souviens du plan ? #line:0608a08 
Fred_smiling: Ton plan ?  #line:010b808 
You: Oui, je t'ai demandé d'en proposer un, t'as refusé. #line:0a49c05 
Fred_smiling: Le plan, ça vient avec les idées, les breakthrough. #line:02066f7 
You: Sans plan, on ne peut pas avancer dans la rédaction. Et on en est là, la rédaction, la dernière ligne droite. #line:0696f59 
Fred_sad: Mais pourquoi on doit suivre l'axe que tu as proposé ?  #line:091835b 
You: T'en vois d'autre, #line:0d08340 
Fred_happy: Oui, plein, y a l'embarras du choix. #line:0b977ec 
You: On n'a plus le temps. #line:004dc90 
Fred_smiling: On a toujours le temps. #line:0e52680 

<< visit Plan >>

-> Vous prenez le temps de discuter avec Fred de tous les plans possibles  #line:0d5fcf7 
    << jump Plan_Good >>
-> Vous présentez votre plan à Fred #line:08a8a75 
    << jump Plan_Bad >>
===

title: Plan_Good
tags: 
position: -1358,349
colorID: 2
---
You: C'est le dernier jour, on doit choisir un plan et s'y tenir.  #line:0774d64 
Fred_happy: Très bien. Dis-moi ce que tu as retenu et je te dirai ce que j'en pense. #line:0da186f 
You: OK.  #line:02303dd 
Vous farfouillez dans vos dossier et vous ouvrez le fichier qui contient le plan détaillé du mémoire sur la théorie du chaos en macrobiologie post-darwinienne. #line:0d3ee9f 
Vous commencez ensuite à lui présentez vos trois grandes parties, vos sections et sous-sections. Fred se révèle un infatigable représentant du jury. Iel ne manque pas d'arguments ni de pertinence dans ces remarques et ces questions. #line:0a18b2d 
En clair, iel vous déstabilise, iel vous secoue. Iel vous fait douter de votre problématique et de vous. Sauf que, c'est là le génie de Fred. Fred ne se contente pas de vous perturber, iel vous propose des pistes alternatives, des questions et des arguments auxquels vous n'aviez pas pensés. #line:07d69b4 
En fait, iel secoue l'arbre de votre argumentation pour vous aider à cueillir tous les fruits qu'il a produit. Grâce à la contribution de Fred, vous comprenez que vous avez un plan en béton armé. #line:03502f1 
<< advanceTime 2:00 >>
<< addToGauge learning 20 >>

<< jump Liste_Taches >>
===

title: Plan_Bad
tags: 
position: -1268,409
colorID: 2
---
You: Ben c'est le problème. On n'a plus le temps d'hésiter. On doit choisir un plan et s'y tenir.  #line:04260cf 
Fred_sad: C'est vraiment dommage. #line:0c92611 
You: Je te rappelle qu'on doit le rendre ce soir ce mémoire. C'est maintenant trop tard pour changer d'avis. Je te renvoie le plan et on le valide. Un point, c'est tout. #line:0fe17ad 
Vous sentez que vous n'avez peut-être pas pris la bonne décision. Fred se révèle un infatigable avocat de l'accusation. Iel ne manque pas d'arguments ni de pertinence dans ces remarques et ces questions. Commentaires qui ne font que fragiliser votre problématique et douter de vous. #line:0531726 
Si Fred avait raison… Vous allez devoir revivre encore une fois cette journée de jeudi. Pourvu que non… #line:0264a4e 

<< advanceTime 1:30 >>
<< addToGauge learning 10 >>

<<jump Liste_Taches>>
===

title: Bedroom
tags: 
colorID: 0
position: -2100,-19
---
<< fadeIn >>
<< setBackground bedroom night >>
<< fadeOut >>

Vous demandez à l'IA de votre studio de passer en mode "Immersion Studieuse".   #line:0949681 
// y-a-t-il une animation son/visuelle? La jauge de la charge mentale est renforcée? Le joueur le voit? 
// C'est une proposition, pas indispensable dans cette quête, juste possibilité que le joueur réutilise ce qu'il a débloqué
Vous ne vous accordez qu'une pause dîner. Une soupe de nouilles aux légumes et un sorbet multi-fruits. Vous vous attaquez ensuite à la rédaction finale de votre mémoire. Les échanges avec Fred sont encore frais dans votre tête. Vous n'hésitez pas à convertir ses paroles en texte pour enrichir vos propres paragraphes.  #line:04d4a5b 
Heureusement que l'IA de la bibliothèque a enregistré tous les échanges que vous avez eu avec lui aujourd'hui : ça vous fait gagner un temps fou. #line:064a2e4 
<<setTime 21:30>>
<< addToGauge learning 20 >>
Une fois que vous estimez avoir fini, vous annulez le mode "Immersion Studieuse" et vous contactez Fred. #line:0f9806b 
Fred@holo_excited: T'as fini ? #line:0eb7fce 
You: Oui. Et toi ? #line:05c5e53 
Fred@holo_happy: Je t'envoie ma participation à mon tour. Désolé, c'est modeste. #line:01d28a8 
You: Je sais, l'écrit, c'est pas ton truc. Mais tu m'as énormément aidé sur ma partie. Avec tes idées et tes remarques. #line:0e6d6c9 
Fred@holo_smiling: Merci. On fait la paire. #line:03fb032 
You: Je sais pas. #line:0c35c9a 
Fred@holo_smiling: Yes yes, je te le dis. #line:0d43b47 
Fred raccroche. Vous recevez dans votre boîte mail sa contribution. Vous l'intégrez dans le texte final et vous déposez le tout dans le FTP de l'Académie. Bravo ! Mission accomplie… #line:0076812 
===


title: WiP
tags: 
position: -383,66
colorID: 2
---
Work in Progress
===